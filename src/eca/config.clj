(ns eca.config
  "Waterfall of ways to get eca config, deep merging from top to bottom:

  1. base: fixed config var `eca.config/initial-config`.
  2. env var: searching for a `ECA_CONFIG` env var which should contains a valid json config.
  3. local config-file: searching from a local `.eca/config.json` file.
  4. `initializatonOptions` sent in `initialize` request.

  Finally, any files listed in `:extraConfigs` are deep merged last, overriding all of the above.

  When `:config-file` from cli option is passed, it uses that instead of searching default locations."
  (:require
   [babashka.fs :as fs]
   [camel-snake-kebab.core :as csk]
   [cheshire.core :as json]
   [cheshire.factory :as json.factory]
   [clojure.core.memoize :as memoize]
   [clojure.java.io :as io]
   [clojure.string :as string]
   [clojure.walk :as walk]
   [eca.cache :as cache]
   [eca.features.agents :as agents]
   [eca.interpolation :as interpolation]
   [eca.logger :as logger]
   [eca.messenger :as messenger]
   [eca.shared :as shared :refer [multi-str]]
   [rewrite-json.core :as rj])
  (:import
   [java.io File]))

(set! *warn-on-reflection* true)

(def ^:private logger-tag "[CONFIG]")

(def ^:dynamic *env-var-config-error* false)
(def ^:dynamic *custom-config-error* false)
(def ^:dynamic *global-config-error* false)
(def ^:dynamic *local-config-error* false)
(def ^:dynamic *extra-config-error* false)

(def ^:private listen-idle-ms 3000)

(def custom-config-file-path* (atom nil))

(defn get-env [env] (System/getenv env))
(defn get-property [property] (System/getProperty property))
(defn user-home [] (cache/user-home))

(def ^:private dangerous-commands-regexes
  [".*[12&]?>>?\\s*(?!/dev/null\\b)(?!/tmp/\\S*\\b)(?!&\\d+\\b)(?!>)\\S+.*" ;; output redirection (except /dev/null and /tmp/)
   ".*\\|\\s*(tee|dd|xargs).*",                                                          ;; pipe to tee/dd/xargs
   ".*\\b(sed|awk|perl)\\s+.*-i.*",                                                      ;; in-place editing
   ".*\\b(rm|mv|cp|touch|mkdir)\\b.*",                                                   ;; file mutation commands
   ".*git\\s+(add|commit|push).*",                                                       ;; git write ops
   ".*npm\\s+install.*",                                                                 ;; npm install
   ".*-c\\s+[\"'].*open.*[\"']w[\"'].*",                                                 ;; python open(...,'w')
   ".*bash.*-c.*[12&]?>>?\\s*(?!/dev/null\\b)(?!/tmp/\\S*\\b)(?!&\\d+\\b)(?!>)\\S+.*"])

(def ^:private openai-variants
  {"none" {:reasoning {:effort "none"}}
   "low" {:reasoning {:effort "low" :summary "auto"}}
   "medium" {:reasoning {:effort "medium" :summary "auto"}}
   "high" {:reasoning {:effort "high" :summary "auto"}}
   "xhigh" {:reasoning {:effort "xhigh" :summary "auto"}}})

(def ^:private openai-gpt-5-6-variants
  "Variants for gpt-5.6 models (gpt-5.6-sol, gpt-5.6-terra, gpt-5.6-luna).
   Same reasoning effort levels as `openai-variants` (none, low, medium, high,
   xhigh) plus \"max\". Note: codex also defines \"ultra\" but downgrades it to
   \"max\" on the wire, so it is not included here."
  {"none" {:reasoning {:effort "none"}}
   "low" {:reasoning {:effort "low" :summary "auto"}}
   "medium" {:reasoning {:effort "medium" :summary "auto"}}
   "high" {:reasoning {:effort "high" :summary "auto"}}
   "xhigh" {:reasoning {:effort "xhigh" :summary "auto"}}
   "max" {:reasoning {:effort "max" :summary "auto"}}})

(def ^:private openai-gpt-6-variants
  "Variants for gpt-6 models (gpt-6-astra). Same as `openai-gpt-5-6-variants`
   minus \"none\": OpenAI documents that GPT-6 Astra rejects the none reasoning
   effort. Codex also lists \"ultra\" but downgrades it to \"max\" on the wire."
  {"low" {:reasoning {:effort "low" :summary "auto"}}
   "medium" {:reasoning {:effort "medium" :summary "auto"}}
   "high" {:reasoning {:effort "high" :summary "auto"}}
   "xhigh" {:reasoning {:effort "xhigh" :summary "auto"}}
   "max" {:reasoning {:effort "max" :summary "auto"}}})

(defn ^:private chat-completions-variants
  "Chat Completions counterpart of a Responses API variant table: the same
   effort levels, sent as the top-level `reasoning_effort` string that
   /chat/completions expects instead of the `reasoning.effort` object, which
   OpenAI-compatible gateways reject as an unknown parameter (#609)."
  [responses-variants]
  (into {}
        (map (fn [[variant-name {{:keys [effort]} :reasoning}]]
               [variant-name {:reasoning_effort effort}]))
        responses-variants))

(def ^:private openai-chat-variants (chat-completions-variants openai-variants))
(def ^:private openai-chat-gpt-5-6-variants (chat-completions-variants openai-gpt-5-6-variants))
(def ^:private openai-chat-gpt-6-variants (chat-completions-variants openai-gpt-6-variants))

(def ^:private openai-responses-apis
  "Provider `api` values routed to the Responses API (`openai` is a legacy alias)."
  ["openai-responses" "openai"])

(def ^:private anthropic-variants
  {"low" {:output_config {:effort "low"} :thinking {:type "adaptive"}}
   "medium" {:output_config {:effort "medium"} :thinking {:type "adaptive"}}
   "high" {:output_config {:effort "high"} :thinking {:type "adaptive"}}
   "max" {:output_config {:effort "max"} :thinking {:type "adaptive"}}})

(def ^:private anthropic-v2-variants
  {"default" {:thinking {:type "adaptive" :display "summarized"}}
   "low" {:output_config {:effort "low"} :thinking {:type "adaptive" :display "summarized"}}
   "medium" {:output_config {:effort "medium"} :thinking {:type "adaptive" :display "summarized"}}
   "high" {:output_config {:effort "high"} :thinking {:type "adaptive" :display "summarized"}}
   "xhigh" {:output_config {:effort "xhigh"} :thinking {:type "adaptive" :display "summarized"}}
   "max" {:output_config {:effort "max"} :thinking {:type "adaptive" :display "summarized"}}})

(def ^:private openai-chat-claude-variants
  "Claude 4.5/4.6 models served through OpenAI-compatible gateways (e.g.
   OpenRouter). The top-level `verbosity` param is mapped by OpenRouter to
   Anthropic's output_config.effort; thinking is opt-in via the unified
   `reasoning` param."
  {"low" {:verbosity "low" :reasoning {:enabled true}}
   "medium" {:verbosity "medium" :reasoning {:enabled true}}
   "high" {:verbosity "high" :reasoning {:enabled true}}
   "max" {:verbosity "max" :reasoning {:enabled true}}})

(def ^:private openai-chat-claude-v2-variants
  "Adaptive-thinking Claude models (Opus 4.7+, Opus/Sonnet/Fable/Mythos 5)
   served through OpenAI-compatible gateways (e.g. OpenRouter). Thinking is
   always on, and `verbosity` is the only effort lever: OpenRouter ignores
   `reasoning.effort` for these models."
  {"low" {:verbosity "low"}
   "medium" {:verbosity "medium"}
   "high" {:verbosity "high"}
   "xhigh" {:verbosity "xhigh"}
   "max" {:verbosity "max"}})

(def ^:private deepseek-variants
  {"none" {:thinking {:type "disabled"}}
   "high" {:reasoning_effort "high"}
   "max" {:reasoning_effort "max"}})

(def ^:private glm-variants
  {"none" {:reasoning_effort "none"}
   "high" {:reasoning_effort "high"}
   "max" {:reasoning_effort "max"}})

(def ^:private initial-config*
  {:providers {"openai" {:api "openai-responses"
                         :url "${env:OPENAI_API_URL:https://api.openai.com}"
                         :key "${env:OPENAI_API_KEY}"
                         :requiresAuth? true
                         :models {"gpt-4.1" {}
                                  "gpt-5" {}
                                  "gpt-5-mini" {}
                                  "gpt-5.2" {}
                                  "gpt-5.3-codex" {}}}
               "anthropic" {:api "anthropic"
                            :url "${env:ANTHROPIC_API_URL:https://api.anthropic.com}"
                            :key "${env:ANTHROPIC_API_KEY}"
                            :requiresAuth? true
                            :models {"claude-sonnet-4-6" {}
                                     "claude-sonnet-5" {}
                                     "claude-opus-4-6" {}
                                     "claude-opus-4-7" {}
                                     "claude-opus-4-8" {}
                                     "claude-opus-5" {}
                                     "claude-opus-5-5" {}
                                     "claude-fable-5" {}
                                     "claude-fable-5-1" {}
                                     "claude-mythos-5" {}
                                     "claude-mythos-5-1" {}}}
               "github-copilot" {:api "openai-chat"
                                 :url "${env:GITHUB_COPILOT_API_URL:https://api.githubcopilot.com}"
                                 :key nil ;; not supported, requires login auth
                                 :requiresAuth? true
                                 ;; models come from the account's /models catalog after login;
                                 ;; a static entry here would bypass its plan filtering.
                                 :models {}}
               "google" {:api "openai-chat"
                         :url "${env:GOOGLE_API_URL:https://generativelanguage.googleapis.com/v1beta/openai}"
                         :key "${env:GOOGLE_API_KEY}"
                         :requiresAuth? true
                         :models {"gemini-2.5-pro" {}}}
               "ollama" {:url "${env:OLLAMA_API_URL:http://localhost:11434}"}}
   :defaultAgent "code"
   :agent {"code" {:mode "primary"
                   :prompts {:chat "${classpath:prompts/code_agent.md}"}
                   :disabledTools ["preview_file_change"]}
           "plan" {:mode "primary"
                   :prompts {:chat "${classpath:prompts/plan_agent.md}"}
                   :disabledTools ["edit_file" "write_file" "move_file" "git"]
                   :toolCall {:approval {:byDefault "ask"
                                         :allow {"eca__shell_command"
                                                 {:argsMatchers {"command" ["pwd"
                                                                            "git\\s+diff(\\s+.*)?"
                                                                            "git\\s+log(\\s+.*)?"
                                                                            "git\\s+show(\\s+.*)?"
                                                                            "find(\\s+.*)?"
                                                                            "ls(\\s+.*)?"]}}
                                                 "eca__compact_chat" {}
                                                 "eca__preview_file_change" {}
                                                 "eca__read_file" {}
                                                 "eca__directory_tree" {}
                                                 "eca__grep" {}
                                                 "eca__editor_diagnostics" {}
                                                 "eca__skill" {}
                                                 "eca__search_tools" {}
                                                 "eca__task" {}
                                                 "eca__fetch_rule" {}
                                                 "eca__spawn_agent" {}}
                                         :deny {"eca__shell_command"
                                                {:argsMatchers {"command" dangerous-commands-regexes}}}}}}
           "explorer" {:mode "subagent"
                       :description "${classpath:prompts/explorer_agent_description.md}"
                       :systemPrompt "${classpath:prompts/explorer_agent.md}"
                       :disabledTools ["edit_file" "write_file" "move_file" "preview_file_change" "git"]
                       :toolCall {:approval {:byDefault "ask"
                                             :allow {"eca__shell_command"
                                                     {:argsMatchers {"command" ["pwd"
                                                                                "git\\s+diff(\\s+.*)?"
                                                                                "git\\s+log(\\s+.*)?"
                                                                                "git\\s+show(\\s+.*)?"
                                                                                "find(\\s+.*)?"
                                                                                "ls(\\s+.*)?"]}}
                                                     "eca__compact_chat" {}
                                                     "eca__read_file" {}
                                                     "eca__directory_tree" {}
                                                     "eca__grep" {}
                                                     "eca__editor_diagnostics" {}
                                                     "eca__skill" {}
                                                     "eca__search_tools" {}
                                                     "eca__task" {}
                                                     "eca__fetch_rule" {}}
                                             :deny {"eca__shell_command"
                                                    {:argsMatchers {"command" dangerous-commands-regexes}}}}}}
           "general" {:mode "subagent"
                      :description "${classpath:prompts/general_agent_description.md}"
                      :systemPrompt "${classpath:prompts/code_agent.md}"
                      :disabledTools ["preview_file_change"]}}
   :defaultModel nil
   :prompts {:chat "${classpath:prompts/code_agent.md}" ;; default to code agent
             :chatTitle "${classpath:prompts/title.md}"
             :compact "${classpath:prompts/compact.md}"
             :init "${classpath:prompts/init.md}"
             :skillCreate "${classpath:prompts/skill_create.md}"
             :completion "${classpath:prompts/inline_completion.md}"
             :rewrite "${classpath:prompts/rewrite.md}"}
   :chat {:title true
          :defaultTrust false
          :autoSyncSystemPrompt false}
   :chatInline {}
   :chatRetentionDays 30
   :rewrite {:fullFileMaxLines 2000}
   :hooks {}
   :rules []
   :commands []
   :skills []
   :extraConfigs []
   :disabledTools []
   :mcpToolSearch {:deferAllWhenTotalTokensExceedPercentOfContext nil
                   :includePattern []
                   :excludePattern []}
   :toolCall {:approval {:byDefault "ask"
                         :allow {"eca__compact_chat" {}
                                 "eca__preview_file_change" {}
                                 "eca__read_file" {}
                                 "eca__directory_tree" {}
                                 "eca__grep" {}
                                 "eca__editor_diagnostics" {}
                                 "eca__skill" {}
                                 "eca__search_tools" {}
                                 "eca__task" {}
                                 "eca__ask_user" {}
                                 "eca__fetch_rule" {}
                                 "eca__spawn_agent" {}}
                         :ask {}
                         :deny {}}
              :readFile {:maxLines 2000}
              :shellCommand {:summaryMaxLength 35}
              :editorNav {:enabled true}
              :outputTruncation {:lines 2000 :sizeKb 50}}
   ;; Same Claude regexes appear twice with different payload dialects per API;
   ;; the (?:...) wrapper only keeps the map keys unique.
   :variantsByModel {".*sonnet[-._]4[-._]6|opus[-._]4[-._][56]" {:variants anthropic-variants
                                                                 :api ["anthropic" "bedrock"]}
                     ".*opus[-._]4[-._][78]|.*opus[-._]5|.*sonnet[-._]5|.*fable[-._]5|.*mythos[-._]5" {:variants anthropic-v2-variants
                                                                                                       :api ["anthropic" "bedrock"]}
                     "(?:.*sonnet[-._]4[-._]6|opus[-._]4[-._][56])" {:variants openai-chat-claude-variants
                                                                     :api "openai-chat"
                                                                     :excludeProviders ["github-copilot"]}
                     "(?:.*opus[-._]4[-._][78]|.*opus[-._]5|.*sonnet[-._]5|.*fable[-._]5|.*mythos[-._]5)" {:variants openai-chat-claude-v2-variants
                                                                                                           :api "openai-chat"
                                                                                                           :excludeProviders ["github-copilot"]}
                     ".*gpt[-._]5(?:[-._](?:2|4|5)(?!\\d)|[-._]3[-._]codex)" {:variants openai-variants
                                                                              :api openai-responses-apis
                                                                              :excludeProviders ["github-copilot"]}
                     ".*gpt[-._]5[-._]6(?!\\d)" {:variants openai-gpt-5-6-variants
                                                 :api openai-responses-apis
                                                 :excludeProviders ["github-copilot"]}
                     ;; gpt-6 family (gpt-6-astra), not gpt-6.x point releases.
                     ".*gpt[-._]6(?![-._]?\\d)" {:variants openai-gpt-6-variants
                                                 :api openai-responses-apis
                                                 :excludeProviders ["github-copilot"]}
                     ;; Same GPT families served through openai-chat providers (e.g.
                     ;; LiteLLM/Azure gateways): /chat/completions takes a top-level
                     ;; `reasoning_effort` string instead of `reasoning.effort` (#609).
                     "(?:.*gpt[-._]5(?:[-._](?:2|4|5)(?!\\d)|[-._]3[-._]codex))" {:variants openai-chat-variants
                                                                                  :api "openai-chat"
                                                                                  :excludeProviders ["github-copilot"]}
                     "(?:.*gpt[-._]5[-._]6(?!\\d))" {:variants openai-chat-gpt-5-6-variants
                                                     :api "openai-chat"
                                                     :excludeProviders ["github-copilot"]}
                     "(?:.*gpt[-._]6(?![-._]?\\d))" {:variants openai-chat-gpt-6-variants
                                                     :api "openai-chat"
                                                     :excludeProviders ["github-copilot"]}
                     ".*deepseek[-._]v4[-._](?:pro|flash)" {:variants deepseek-variants
                                                            :api "openai-chat"}
                     "(?i).*glm[-._]5[-._]2" {:variants glm-variants}}
   :mcpTimeoutSeconds 60
   :mcpKeepAliveSeconds 30
   :lspTimeoutSeconds 30
   :streamIdleTimeoutSeconds 300
   :connectTimeoutSeconds 15
   :mcpServers {}
   :welcomeMessage (multi-str "# Welcome to ECA!"
                              ""
                              "Type `/` to see all commands"
                              ""
                              "- `/login` to authenticate with providers"
                              "- `/init` to create/update AGENTS.md"
                              "- `/doctor` or `/config` to troubleshoot"
                              "- `/remote` for remote connection details"
                              ""
                              "__User context__ (preserved through history):"
                              "- Complete `#` to expand to a resource path (let LLM find it)"
                              "- Complete `@` to insert that resource's content (pass content to LLM)."
                              ""
                              "__System context__ (available only during the prompt being sent):"
                              "- Add it to the `@` area above the user prompt"
                              ""
                              "Toggle **trust mode** to auto-accept all tool calls"
                              "")
   :index {:ignoreFiles [{:type :gitignore}]
           :repoMap {:maxTotalEntries 800
                     :maxEntriesPerDir 50}}
   :completion {:model "openai/gpt-4.1"}
   :netrcFile nil
   :autoCompactPercentage 75
   :includeParentAgentsFiles false
   :plugins {"eca" {:source "https://github.com/editor-code-assistant/eca-plugins.git"}}
   :remote {:enabled false}
   :env "prod"})

(defn ^:private parse-dynamic-string-values
  "walk through config parsing dynamic string contents if value is a string."
  [config cwd]
  (walk/postwalk
   (fn [x]
     (if (string? x)
       (interpolation/replace-dynamic-strings x cwd config)
       x))
   config))

(defn initial-config []
  (parse-dynamic-string-values initial-config* (io/file ".")))

(defn provider-base
  "Returns the provider whose built-in behavior (login flows, request options,
   API quirks) `provider` follows: the provider it `inherit`s from, following
   the chain, or `provider` itself."
  [provider config]
  (when provider
    (loop [current (name provider)
           seen #{current}]
      (let [parent (get-in config [:providers current :inherit])]
        (if (and parent (not (contains? seen parent)))
          (recur parent (conj seen parent))
          current)))))

(defn ^:private regex-matches? [pattern-str s]
  (try
    (some? (re-find (re-pattern pattern-str) s))
    (catch Exception e
      (logger/warn logger-tag "Invalid regex pattern in variantsByModel:" pattern-str (.getMessage e))
      false)))

(defn effective-model-variants
  "Returns effective variants for a model. Discovered provider variants
   override built-in regex variants, effort variants parsed from the provider
   /models response are the last fallback, and user variants have final
   priority. A variant set to {} is removed from the result."
  ([config provider model-name user-variants]
   (effective-model-variants config provider model-name nil user-variants))
  ([config provider model-name model-capabilities user-variants]
   (let [provider-api (or (some-> (:api model-capabilities) name)
                          (get-in config [:providers provider :api]))
         base-provider (provider-base provider config)
         api-match? (fn [api config-val]
                      (cond (sequential? config-val) (some #{api} config-val)
                            config-val (= api config-val)
                            :else true))
         builtin (when model-name
                   (some (fn [[pattern-str {:keys [variants excludeProviders api]}]]
                           (when (and (regex-matches? pattern-str model-name)
                                      (not (some #{base-provider} excludeProviders))
                                      (api-match? provider-api api))
                             variants))
                         (:variantsByModel config)))
         merged (merge (or (not-empty (:variants model-capabilities))
                           builtin
                           (not-empty (:effort-variants model-capabilities)))
                       user-variants)]
     (when (seq merged)
       (let [filtered (into {} (remove (fn [[_ v]] (= {} v))) merged)]
         (when (seq filtered)
           filtered))))))

(defn selectable-variant-names
  "Returns sorted variant names suitable for UI display, excluding internal-only
           variants like \"default\" which are applied automatically."
  [variants]
  (when (seq variants)
    (vec (sort (remove #{"default"} (keys variants))))))

(def ^:private fallback-agent "code")

(def ^:private default-modes #{"primary" "subagent"})

(defn agent-modes
  "Returns the effective set of modes for an agent config.

   The `:mode` field accepts either a single string (e.g. \"primary\") or a
   collection of strings (e.g. [\"primary\" \"subagent\"]). When `:mode` is
   absent, nil, or an empty collection, defaults to both `primary` and
   `subagent`."
  [agent-config]
  (let [mode (:mode agent-config)]
    (cond
      (string? mode) #{mode}
      (and (coll? mode) (seq mode)) (set mode)
      :else default-modes)))

(defn subagent-available?
  "Returns whether an agent config is available as a subagent to parent-agent-name."
  [agent-config parent-agent-name]
  (let [spawnable-by (:spawnableBy agent-config)
        allowed-parents (cond
                          (string? spawnable-by) #{spawnable-by}
                          (coll? spawnable-by) (set spawnable-by)
                          :else #{})]
    (and (contains? (agent-modes agent-config) "subagent")
         (or (empty? allowed-parents)
             (and parent-agent-name
                  (contains? allowed-parents parent-agent-name))))))

(defn available-subagents
  "Returns configured subagents visible to parent-agent-name."
  [config parent-agent-name]
  (filter (fn [[_ agent-config]]
            (subagent-available? agent-config parent-agent-name))
          (:agent config)))

(defn primary-agent-names
  "Returns the names of agents usable as primary (i.e. whose effective
   modes include \"primary\")."
  [config]
  (->> (:agent config)
       (filter (fn [[_ v]] (contains? (agent-modes v) "primary")))
       (map key)
       distinct))

(defn validate-agent-name
  "Validates if an agent exists in config. Returns the agent name if valid,
   or the fallback agent if not."
  [agent-name config]
  (if (contains? (:agent config) agent-name)
    agent-name
    (do (logger/warn logger-tag (format "Unknown agent '%s' specified, falling back to '%s'"
                                        agent-name fallback-agent))
        fallback-agent)))

(def ^:private ttl-cache-config-ms 5000)

(defn ^:private safe-read-json-string [raw-string config-dyn-var]
  (try
    (alter-var-root config-dyn-var (constantly false))
    (binding [json.factory/*json-factory* (json.factory/make-json-factory
                                           {:allow-comments true})]
      (json/parse-string raw-string))
    (catch Exception e
      (alter-var-root config-dyn-var (constantly true))
      (logger/warn logger-tag "Error parsing config json:" (.getMessage e)))))

(defn ^:private config-from-envvar []
  (some-> (System/getenv "ECA_CONFIG")
          (safe-read-json-string (var *env-var-config-error*))
          (parse-dynamic-string-values (io/file "."))))

(defn ^:private config-from-custom* []
  (when-some [path @custom-config-file-path*]
    (let [config-file (io/file path)]
      (when (.exists config-file)
        (some-> (safe-read-json-string (slurp config-file) (var *custom-config-error*))
                (parse-dynamic-string-values (fs/file (fs/parent config-file))))))))

(def ^:private config-from-custom
  (memoize/ttl config-from-custom* :ttl/threshold ttl-cache-config-ms))

(defn global-config-file ^File []
  (io/file (shared/global-config-dir) "config.json"))

(defn ^:private config-from-global-file []
  (let [config-file (global-config-file)]
    (when (.exists config-file)
      (some-> (safe-read-json-string (slurp config-file) (var *global-config-error*))
              (parse-dynamic-string-values (shared/global-config-dir))))))

(declare merge-config)

(defn ^:private merge-config-layers
  "Merges file config layers into config. Non-plugin keys stay aggregated
   across layers with aggregate-merge (historical per-source behavior: shallow
   for workspace roots, deep for extraConfigs), then each layer's plugins merge
   one by one so install lists combine across layers."
  [aggregate-merge config layers]
  (reduce merge-config
          config
          (cons (reduce aggregate-merge {} (map #(dissoc % "plugins") layers))
                (map #(select-keys % ["plugins"]) layers))))

(defn ^:private config-from-local-file [roots config]
  (let [layers (mapv (fn [{:keys [uri]}]
                       (let [config-dir (io/file (shared/uri->filename uri) ".eca")
                             config-file (io/file config-dir "config.json")]
                         (when (.exists config-file)
                           (some-> (safe-read-json-string (slurp config-file) (var *local-config-error*))
                                   (parse-dynamic-string-values config-dir)))))
                     roots)]
    (merge-config-layers merge config layers)))

(def initialization-config* (atom {}))

(def plugin-components* (atom nil))

(def ^:private plugins-resolved* (promise))

(defn deliver-plugins-resolved!
  "Signal that plugin resolution has finished (successfully or not)."
  []
  (deliver plugins-resolved* true))

(defn await-plugins-resolved!
  "Block until plugin resolution has finished. Returns true when resolved,
   nil on timeout (30s)."
  []
  (deref plugins-resolved* 30000 nil))

(defn ^:private deep-merge [& maps]
  (apply merge-with (fn [& args]
                      (if (every? #(or (map? %) (nil? %)) args)
                        (apply deep-merge args)
                        (last args)))
         maps))

(defn ^:private resolve-extra-config-file
  "Resolves an `:extraConfigs` path entry to a `File`. Absolute paths (and `~`)
   are used as-is; relative paths resolve against the first workspace root, or
   the process cwd when there are no workspace roots."
  ^File [path roots]
  (let [expanded (fs/expand-home (str path))]
    (if (fs/absolute? expanded)
      (fs/file expanded)
      (if-let [root (some-> (first roots) :uri shared/uri->filename)]
        (fs/file (fs/path root (str expanded)))
        (fs/file expanded)))))

(defn ^:private config-from-extra-configs
  "Reads and deep-merges every existing file listed in `:extraConfigs`, in
   listed order (later entries win). Missing paths are logged and skipped;
   parse errors are logged, surfaced via `*extra-config-error*` and skipped.
   Non-recursive: an `:extraConfigs` declared inside an extra file is ignored."
  [paths roots config]
  (let [paths (cond
                (string? paths) [paths]
                (sequential? paths) paths
                :else [])
        layers (mapv (fn [path]
                       (let [^File config-file (resolve-extra-config-file path roots)]
                         (if (.exists config-file)
                           (some-> (safe-read-json-string (slurp config-file) (var *extra-config-error*))
                                   (parse-dynamic-string-values (fs/file (fs/parent config-file))))
                           (logger/warn logger-tag (format "extraConfigs path not found, skipping: %s" (.getPath config-file))))))
                     paths)]
    (merge-config-layers deep-merge config layers)))

(defn ^:private resolve-agent-inheritance
  "Resolves :inherit keys in agent configs. When an agent has :inherit \"other\",
   its config is deep-merged on top of the parent agent's config (child wins).
   The :inherit key is stripped from the resolved config."
  [agents]
  (reduce-kv
   (fn [result agent-name agent-config]
     (if-let [parent-name (:inherit agent-config)]
       (let [parent-config (get agents parent-name)]
         (cond
           (= parent-name agent-name)
           (do (logger/warn logger-tag (format "Agent '%s' inherits from itself, ignoring inherit" agent-name))
               (assoc result agent-name (dissoc agent-config :inherit)))

           (nil? parent-config)
           (do (logger/warn logger-tag (format "Agent '%s' inherits from unknown agent '%s', ignoring inherit" agent-name parent-name))
               (assoc result agent-name (dissoc agent-config :inherit)))

           :else
           (assoc result agent-name (deep-merge (dissoc parent-config :inherit)
                                                (dissoc agent-config :inherit)))))
       (assoc result agent-name agent-config)))
   {}
   agents))

(def ^:private provider-credential-keys
  "Never inherited: an inheriting provider is a separate account."
  [:key :keyRc :keyEnv])

(defn ^:private resolve-provider-inheritance
  "Resolves :inherit keys in provider configs. A provider with :inherit \"other\"
   is deep-merged on top of the resolved parent config (child wins), minus the
   parent's credentials, so it can log in to a different account while behaving
   like the parent. The :inherit key is kept, normalized to the parent provider
   id, so `provider-base` can find the parent at runtime. Self, unknown and
   circular parents are ignored with a warning."
  [providers]
  (if-not (some :inherit (vals providers))
    providers
    (letfn [(parent-of [provider-name]
              (some-> (get-in providers [provider-name :inherit]) str string/trim not-empty csk/->kebab-case))
            (circular? [provider-name]
              (loop [current (parent-of provider-name)
                     seen #{provider-name}]
                (cond
                  (nil? current) false
                  (= provider-name current) true
                  (contains? seen current) false
                  :else (recur (parent-of current) (conj seen current)))))
            (resolve-provider [provider-name]
              (let [parent-name (parent-of provider-name)
                    own-config (dissoc (get providers provider-name) :inherit)]
                (cond
                  (nil? parent-name)
                  own-config

                  (= parent-name provider-name)
                  (do (logger/warn logger-tag (format "Provider '%s' inherits from itself, ignoring inherit" provider-name))
                      own-config)

                  (not (contains? providers parent-name))
                  (do (logger/warn logger-tag (format "Provider '%s' inherits from unknown provider '%s', ignoring inherit" provider-name parent-name))
                      own-config)

                  (circular? provider-name)
                  (do (logger/warn logger-tag (format "Provider '%s' has a circular inherit through '%s', ignoring inherit" provider-name parent-name))
                      own-config)

                  :else
                  (assoc (deep-merge (apply dissoc (resolve-provider parent-name) provider-credential-keys)
                                     own-config)
                         :inherit parent-name))))]
      (reduce-kv (fn [result provider-name _]
                   (assoc result provider-name (resolve-provider provider-name)))
                 {}
                 providers))))

(defn ^:private eca-version* []
  (string/trim (slurp (io/resource "ECA_VERSION"))))

(def eca-version (memoize eca-version*))

(def ollama-model-prefix "ollama/")

(defn ^:private normalize-fields
  "Converts a deep nested map where keys are strings to keywords.
   normalization-rules follow the nest order, :ANY means any field name.
    :kebab-case-key means convert field names to kebab-case.
    :stringfy-key means convert field names to strings."
  [normalization-rules m]
  (let [kc-paths (set (:kebab-case-key normalization-rules))
        str-paths (set (:stringfy-key normalization-rules))
        keywordize-paths (set (:keywordize-val normalization-rules))
        ; match a current path against a rule path with :ANY wildcard
        matches-path? (fn [rule-path cur-path]
                        (and (= (count rule-path) (count cur-path))
                             (every? true?
                                     (map (fn [rp cp]
                                            (or (= rp :ANY)
                                                (= rp cp)))
                                          rule-path cur-path))))
        applies? (fn [paths cur-path]
                   (some #(matches-path? % cur-path) paths))
        normalize-map (fn normalize-map [cur-path m*]
                        (cond
                          (map? m*)
                          (let [apply-kebab-key? (applies? kc-paths cur-path)
                                apply-string-key? (applies? str-paths cur-path)
                                apply-keywordize-val? (applies? keywordize-paths cur-path)]
                            (into {}
                                  (map (fn [[k v]]
                                         (let [base-name (cond
                                                           (keyword? k) (name k)
                                                           (string? k) k
                                                           :else (str k))
                                               kebabed (if apply-kebab-key?
                                                         (csk/->kebab-case base-name)
                                                         base-name)
                                               new-k (if apply-string-key?
                                                       kebabed
                                                       (keyword kebabed))
                                               new-v (if apply-keywordize-val?
                                                       (keyword v)
                                                       v)
                                               new-v (normalize-map (conj cur-path new-k) new-v)]
                                           [new-k new-v])))
                                  m*))

                          (sequential? m*)
                          (mapv #(normalize-map cur-path %) m*)

                          :else m*))]
    (normalize-map [] m)))

(def ^:private normalization-rules
  {:kebab-case-key
   [[:providers]
    [:network]]
   :keywordize-val
   [[:providers :ANY :httpClient]
    [:providers :ANY :models :ANY :reasoningHistory]]
   :stringfy-key
   [[:agent]
    [:providers]
    [:providers :ANY :extraHeaders]
    [:providers :ANY :models]
    [:providers :ANY :models :ANY :extraHeaders]
    [:providers :ANY :models :ANY :variants]
    [:hooks :ANY :matcher]
    [:hooks :ANY :matcher :ANY :argsMatchers]
    [:toolCall :approval :allow]
    [:toolCall :approval :allow :ANY :argsMatchers]
    [:toolCall :approval :ask]
    [:toolCall :approval :ask :ANY :argsMatchers]
    [:toolCall :approval :deny]
    [:toolCall :approval :deny :ANY :argsMatchers]
    [:customTools]
    [:customTools :ANY :schema :properties]
    [:mcpServers]
    [:variantsByModel]
    [:variantsByModel :ANY :variants]
    [:prompts :tools]
    [:agent :ANY :prompts :tools]
    [:agent :ANY :toolCall :approval :allow]
    [:agent :ANY :toolCall :approval :allow :ANY :argsMatchers]
    [:agent :ANY :toolCall :approval :ask]
    [:agent :ANY :toolCall :approval :ask :ANY :argsMatchers]
    [:agent :ANY :toolCall :approval :deny]
    [:agent :ANY :toolCall :approval :deny :ANY :argsMatchers]
    ;; Legacy: support old "behavior" config key
    [:behavior]
    [:behavior :ANY :prompts :tools]
    [:behavior :ANY :toolCall :approval :allow]
    [:behavior :ANY :toolCall :approval :allow :ANY :argsMatchers]
    [:behavior :ANY :toolCall :approval :ask]
    [:behavior :ANY :toolCall :approval :ask :ANY :argsMatchers]
    [:behavior :ANY :toolCall :approval :deny]
    [:behavior :ANY :toolCall :approval :deny :ANY :argsMatchers]
    [:otlp]
    [:plugins]]})

(defn ^:private migrate-legacy-agent-name
  "Migrates legacy agent names 'agent' and 'build' to 'code'."
  [agent-name]
  (case agent-name
    ("agent" "build") "code"
    agent-name))

(defn ^:private migrate-legacy-config
  "Migrates legacy config keys to new names for backward compatibility:
   - 'behavior' config key → 'agent'
   - 'defaultBehavior' → 'defaultAgent'
   - 'agent'/'build' agent name → 'code' (inside agent map)"
  [config]
  (cond-> config
    ;; Migrate 'behavior' key → 'agent' (merge, don't overwrite)
    (contains? config :behavior)
    (-> (update :agent (fn [existing]
                         (let [legacy (:behavior config)
                               ;; Rename legacy "agent"/"build" entries to "code"
                               migrated (reduce-kv (fn [m k v]
                                                     (assoc m (migrate-legacy-agent-name k) v))
                                                   {}
                                                   legacy)]
                           (merge migrated existing))))
        (dissoc :behavior))

    ;; Migrate 'defaultBehavior' → 'defaultAgent'
    (and (contains? config :defaultBehavior)
         (not (contains? config :defaultAgent)))
    (-> (assoc :defaultAgent (migrate-legacy-agent-name (:defaultBehavior config)))
        (dissoc :defaultBehavior))

    ;; Also migrate defaultBehavior when nested under :chat (legacy)
    (and (get-in config [:chat :defaultBehavior])
         (not (get-in config [:chat :defaultAgent])))
    (-> (assoc-in [:chat :defaultAgent] (migrate-legacy-agent-name (get-in config [:chat :defaultBehavior])))
        (update :chat dissoc :defaultBehavior))))

(defn ^:private merge-config
  "Deep-merges a normalized config layer into config. The plugins install
   lists combine across layers: a layer's entries append unless its own
   installMode is \"replace\"."
  [config layer]
  (let [layer (normalize-fields normalization-rules layer)
        plugins (:plugins layer)
        {:strs [install installMode]} plugins
        merged (deep-merge config layer)]
    (if (contains? plugins "install")
      (assoc-in merged [:plugins "install"]
                (->> (concat (when-not (= "replace" installMode)
                               (get-in config [:plugins "install"]))
                             install)
                     reverse distinct reverse vec))
      merged)))

(defn ^:private all* [db]
  (let [initialization-config @initialization-config*
        pure-config? (:pureConfig initialization-config)
        plugin-data (when-not pure-config? @plugin-components*)
        plugin-config (when plugin-data
                        (let [cfg (:config-fragment plugin-data)]
                          ;; commands/rules are vectors — separate them to avoid deep-merge replacement
                          (dissoc cfg :commands :rules)))
        plugin-commands (:commands plugin-data)
        plugin-rules (:rules plugin-data)
        plugin-agents (:agents plugin-data)]
    (-> (as-> {} $
          (merge-config $ (initial-config))
          (merge-config $ initialization-config)
          (merge-config $ (when-not pure-config?
                            (config-from-envvar)))
          (if-let [custom-config (config-from-custom)]
            (merge-config $ (when-not pure-config? custom-config))
            (let [config (merge-config $ (when-not pure-config? (config-from-global-file)))]
              (if pure-config?
                config
                (config-from-local-file (:workspace-folders db) config))))
          ;; Plugin config merges after all file configs (user local config wins via later merge)
          (merge-config $ plugin-config)
          ;; extraConfigs merge last, overriding all previous sources
          (config-from-extra-configs (:extraConfigs $) (:workspace-folders db) $))
        ;; Append plugin commands/rules (vector concat, not deep-merge replace)
        (cond->
         (seq plugin-commands) (update :commands #(vec (concat % plugin-commands)))
         (seq plugin-rules) (update :rules #(vec (concat % plugin-rules))))
        migrate-legacy-config
        ;; Merge markdown-defined agents (lowest priority — JSON config agents win)
        ;; Plugin agents merge at same level as markdown agents
        (as-> config
              (let [md-agent-configs (when-not pure-config?
                                       (agents/all-md-agents (:workspace-folders db)))]
                (if (or (seq md-agent-configs) (seq plugin-agents))
                  (update config :agent (fn [existing]
                                          (merge md-agent-configs plugin-agents existing)))
                  config)))
        (update :agent resolve-agent-inheritance)
        (update :providers resolve-provider-inheritance))))

(def ^:private all-memo
  (memoize/ttl (fn [workspace-folders]
                 (all* {:workspace-folders workspace-folders}))
               :ttl/threshold ttl-cache-config-ms))

(defn all
  "Returns the merged config from all sources, cached for a few seconds.
   The cache is keyed on workspace-folders (the only db field `all*` reads)
   instead of the whole db, otherwise any unrelated db change would miss it
   and re-run every config source, including `${cmd:...}` commands.
   Writers of the other inputs `all*` reads (config files,
   `initialization-config*`, `plugin-components*`) must call `clear-cache!`."
  [db]
  (all-memo (:workspace-folders db)))

(defn clear-cache!
  "Drops the cached config so the next `all` call re-reads all sources."
  []
  (memoize/memo-clear! all-memo))

(defn read-file-configs
  "Reads and merges config from file-based sources only (initial config,
  env var, global file, custom file). Does not include
  `initializationOptions` or local project config. Useful for config
  needed before the server is fully initialized (e.g. network/TLS
  settings)."
  []
  (-> {}
      (merge-config (initial-config))
      (merge-config (config-from-envvar))
      (merge-config (if (some? @custom-config-file-path*)
                      (config-from-custom)
                      (config-from-global-file)))))

(defn validation-error []
  (cond
    *env-var-config-error* "ENV"
    *global-config-error* "global"
    *local-config-error* "local"
    *extra-config-error* "extraConfigs"

    ;; all good
    :else nil))

(defn ^:private maybe-notify-validation-error! [messenger prev-error new-error]
  (when (and new-error (not= new-error prev-error))
    (try
      (messenger/showMessage messenger
                             {:type "warning"
                              :message (format "Failed to parse '%s' config, check stderr logs."
                                               new-error)})
      (catch Exception e
        (logger/warn logger-tag "Failed to notify config validation error:" (.getMessage e))))))

(defn listen-for-changes!
  "Polling loop that detects config changes and dispatches to registered
   `:config-updated-fns` listeners. Listeners receive `[prev-config new-config]`
   so they can diff against the last seen snapshot (e.g. MCP reconciliation).
   Parse/validation failures surface to the client via `$/showMessage`."
  [db* messenger]
  (loop [prev-config nil
         prev-validation-error nil]
    (when-not (:stopping @db*)
      (Thread/sleep ^long listen-idle-ms)
      (let [db @db*
            new-config (try (all db)
                            (catch Exception e
                              (logger/warn logger-tag "Error reloading config:" (.getMessage e))
                              prev-config))
            new-validation-error (validation-error)]
        (maybe-notify-validation-error! messenger prev-validation-error new-validation-error)
        (let [new-config-hash (hash new-config)]
          (when (not= new-config-hash (:config-hash db))
            (swap! db* assoc :config-hash new-config-hash)
            (doseq [config-updated-fns (vals (:config-updated-fns db))]
              (try
                (config-updated-fns prev-config new-config)
                (catch Exception e
                  (logger/error logger-tag "Error in config-updated fn:" (.getMessage e)))))))
        (recur new-config new-validation-error)))))

(defn diff-keeping-vectors
  "Like (second (clojure.data/diff a b)) but if a value is a vector, keep vector value from b.

  Example1: (diff-keeping-vectors {:a 1 :b 2}  {:a 1 :b 3}) => {:b 3}
  Example2: (diff-keeping-vectors {:a 1 :b [:bar]}  {:b [:bar :foo]}) => {:b [:bar :foo]}"
  [a b]
  (letfn [(diff-maps [a b]
            (let [all-keys (set (concat (keys a) (keys b)))]
              (reduce
               (fn [acc k]
                 (let [a-val (get a k)
                       b-val (get b k)]
                   (cond
                     ;; Key doesn't exist in b, skip
                     (and (contains? a k) (not (contains? b k)))
                     acc

                     ;; Key doesn't exist in a, include from b
                     (and (not (contains? a k)) (contains? b k))
                     (assoc acc k b-val)

                     ;; Both are vectors and they differ, use the entire vector from b
                     (and (vector? a-val) (vector? b-val) (not= a-val b-val))
                     (assoc acc k b-val)

                     ;; Both are maps, recurse
                     (and (map? a-val) (map? b-val))
                     (let [nested-diff (diff-maps a-val b-val)]
                       (if (seq nested-diff)
                         (assoc acc k nested-diff)
                         acc))

                     ;; Values are different, use value from b
                     (not= a-val b-val)
                     (assoc acc k b-val)

                     ;; Values are the same, skip
                     :else
                     acc)))
               {}
               all-keys)))]
    (let [result (diff-maps a b)]
      (when (seq result)
        result))))

(defn notify-fields-changed-only!
  "Emit `config/updated` with the fields that have changed against the
   session-level mirror (`:last-config-notified`) and update that mirror.

   When called with `chat-id` (4-arity), the broadcast is scoped to that
   chat: it includes `:chat-id` in the payload so the client can apply
   the change only to that chat's UI state, and bypasses the session-
   level mirror diff (so per-chat changes never collapse against the
   session mirror or each other). Used by `chat/selectedModelChanged`
   and `chat/selectedAgentChanged` when the client supplies a `chatId`."
  ([config-updated messenger db*]
   (let [config-to-notify (diff-keeping-vectors (:last-config-notified @db*)
                                                config-updated)]
     (when (seq config-to-notify)
       (swap! db* update :last-config-notified shared/deep-merge config-to-notify)
       (messenger/config-updated messenger config-to-notify))))
  ([config-updated messenger _db* chat-id]
   (when chat-id
     (messenger/config-updated messenger (assoc config-updated :chat-id chat-id)))))

(defn notify-selected-model-changed!
  "Server-initiated equivalent of a client `chat/selectedModelChanged`: aligns
   the client-side selected model to `full-model`, re-computing the available
   variants and the suggested selected variant. Emits via
   `notify-fields-changed-only!`, so it is a no-op when nothing changed.
   Returns the effective `{:model :variant :variants}` selection, or nil when
   `full-model` is missing or no longer in `(:models @db*)`, so a stale
   persisted model does not bubble to the UI. Used by chat resume flows
   (`chat/open`, `/resume`) to restore the model each chat was using.

   When `chat-variant` is provided (the chat's persisted `:variant`) and is
   still supported by `full-model`, the broadcast keeps it; otherwise it
   falls back to the default agent's configured variant when valid, else
   nil. This lets resume preserve the variant the user last saw on the
   chat instead of resetting to the default agent's variant.

   When `chat-id` is provided, the broadcast is scoped to that chat and does
   not update the session-level config mirror."
  ([full-model db* messenger config]
   (notify-selected-model-changed! full-model db* messenger config nil nil))
  ([full-model db* messenger config chat-variant]
   (notify-selected-model-changed! full-model db* messenger config chat-variant nil))
  ([full-model db* messenger config chat-variant chat-id]
   (when (and full-model (contains? (:models @db*) full-model))
     (let [default-agent-name (validate-agent-name
                               (or (:defaultAgent (:chat config))
                                   (:defaultAgent config))
                               config)
           agent-config (get-in config [:agent default-agent-name])
           [provider model-name] (shared/full-model->provider+model full-model)
           model-capabilities (get-in @db* [:models full-model])
           model-config (when (and provider model-name)
                          (get-in config [:providers provider :models model-name]))
           user-variants (:variants model-config)
           variants (when (and provider model-name)
                      (selectable-variant-names
                       (effective-model-variants config provider model-name model-capabilities user-variants)))
           agent-variant (:variant agent-config)
           model-default-variant (or (:defaultVariant model-config)
                                     (:default-variant model-config))
           valid? (fn [v] (and v variants (some #{v} variants)))
           select-variant (cond
                            (valid? chat-variant) chat-variant
                            (valid? agent-variant) agent-variant
                            (valid? model-default-variant) model-default-variant
                            :else nil)
           selection {:model full-model
                      :variants (or variants [])
                      :variant select-variant}
           payload {:chat {:select-model (:model selection)
                           :variants (:variants selection)
                           :select-variant (:variant selection)}}]
       (if chat-id
         (notify-fields-changed-only! payload messenger db* chat-id)
         (notify-fields-changed-only! payload messenger db*))
       selection))))

(defn notify-selected-trust-changed!
  "Server-initiated equivalent of a client-side trust toggle: aligns the
   client-side trust indicator with the chat's persisted `:trust` value.
   Emits via `notify-fields-changed-only!`, so it is a no-op when nothing
   changed. Used by chat resume flows (`chat/open`, `/resume`) so the icon
   the client shows matches the auto-approval behavior the server is about
   to apply (#426).

   When `chat-id` is provided, the broadcast is scoped to that chat and does
   not update the session-level config mirror. Returns the normalized boolean
   trust selection."
  ([trust db* messenger]
   (notify-selected-trust-changed! trust db* messenger nil))
  ([trust db* messenger chat-id]
   (let [selection (boolean trust)
         payload {:chat {:select-trust selection}}]
     (if chat-id
       (notify-fields-changed-only! payload messenger db* chat-id)
       (notify-fields-changed-only! payload messenger db*))
     selection)))

(defn notify-selected-agent-changed!
  "Server-initiated equivalent of a client-side agent selection: aligns the
   client's selected agent for `chat-id` with the agent the server persisted
   on that chat. Clients send their selected agent on every `chat/prompt`, so
   without this a server-side change (`/agent`, `/resume`, remote UI) would be
   reverted by the next prompt.

   Always scoped to `chat-id`; no-op when `agent` or `chat-id` is missing."
  [agent db* messenger chat-id]
  (when agent
    (notify-fields-changed-only! {:chat {:select-agent agent}} messenger db* chat-id)))

(def ^:private config-schema-url "https://eca.dev/config.json")

(defn ^:private flatten-to-paths
  "Recursively walks a nested map and returns a sequence of [path value] pairs,
   where path is a vector of string keys and value is a leaf (non-map) value.
   Mirrors deep-merge semantics: only the touched leaf paths are written."
  ([m] (flatten-to-paths [] m))
  ([prefix m]
   (reduce-kv (fn [acc k v]
                (let [path (conj prefix (if (keyword? k) (name k) (str k)))]
                  (if (and (map? v) (seq v))
                    (into acc (flatten-to-paths path v))
                    (conj acc [path v]))))
              []
              m)))

(defn update-global-config! [config]
  (let [file (global-config-file)
        raw (if (.exists file) (slurp file) "{}")
        root (rj/parse-string raw)
        root (reduce (fn [r [path v]] (rj/assoc-in r path v))
                     root
                     (flatten-to-paths config))
        root (rj/assoc-in root ["$schema"] config-schema-url)]
    (io/make-parents file)
    (spit file (rj/to-string root))
    (clear-cache!)))