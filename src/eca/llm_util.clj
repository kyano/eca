(ns eca.llm-util
  (:require
   [clojure.string :as string]
   [camel-snake-kebab.core :as csk]
   [cheshire.core :as json]
   [ring.util.codec :as ring.util]
   [eca.config :as config]
   [eca.logger :as logger]
   [eca.secrets :as secrets]
   [eca.shared :as shared])
  (:import
   [java.io BufferedReader Closeable EOFException]
   [java.net ConnectException SocketTimeoutException UnknownHostException]
   [java.net.http HttpConnectTimeoutException]
   [javax.net.ssl SSLException]))

(set! *warn-on-reflection* true)

(defn find-last-msg-idx
  "Returns the index of the last message in `msgs` for which `(pred msg)` is true."
  [pred msgs]
  (->> msgs
       (keep-indexed (fn [i msg] (when (pred msg) i)))
       last))

(defn find-last-user-msg-idx [messages]
  ;; Returns the index of the last :role "user" message, or nil if none.
  (find-last-msg-idx #(= "user" (:role %)) messages))

(defn event-data-seq [^BufferedReader rdr]
  (letfn [(next-group []
            (loop [event-line nil]
              (let [line (.readLine rdr)]
                (cond
                  ;; EOF
                  (nil? line)
                  nil

                  ;; skip blank lines
                  (string/blank? line)
                  (recur event-line)

                  ;; event: <event>
                  (string/starts-with? line "event:")
                  (recur line)

                  ;; data: <data>
                  (string/starts-with? line "data:")
                  (let [data-str (string/triml (subs line 5))]
                    (if (= data-str "[DONE]")
                      (recur event-line) ; skip [DONE]
                      (let [event-type (if event-line
                                         ;; Handle both "event: foo" and "event:foo" formats
                                         (string/triml (subs event-line 6))
                                         (-> (json/parse-string data-str true)
                                             :type))]
                        (cons [event-type (json/parse-string data-str true)]
                              (lazy-seq (next-group))))))

                  ;; data directly
                  (string/starts-with? line "{")
                  (cons ["data" (json/parse-string line true)]
                        (lazy-seq (next-group)))

                  :else
                  (recur event-line)))))]
    (next-group)))

(defn gen-rid
  "Generates a request-id for tracking requests"
  []
  (str (rand-int 9999)))

(defn stringfy-tool-result [result]
  (reduce
   (fn [acc content]
     (str acc
          (case (:type content)
            :image (format "[Image: %s]" (:media-type content))
            (:text content))
          "\n"))
   ""
   (-> result :output :contents)))

(defn expand-model-placeholder
  "Replaces a `{model}` placeholder in `url-relative-path` with the
   URL-encoded `model` name. Used by providers (e.g. Gemini) that
   support encoding the model into a custom completion endpoint path."
  [url-relative-path model]
  (string/replace url-relative-path #"\{model\}" (fn [_] (ring.util/url-encode model))))

(defn log-request [tag rid url body headers]
  (let [obfuscated-headers (-> headers
                               (shared/update-some "Authorization" #(shared/obfuscate % {:preserve-num 8}))
                               (shared/update-some "x-api-key" shared/obfuscate)
                               (shared/update-some "x-goog-api-key" shared/obfuscate))]
    (logger/debug tag (format "[%s] Sending body: '%s', headers: '%s', url: '%s'" rid body obfuscated-headers url))))

(defn log-response [tag rid event data]
  (logger/debug tag (format "[%s] %s %s" rid (or event "") data)))

(def ^:private default-stream-idle-timeout-ms 120000)
(def ^:private default-stream-check-interval-ms 500)

(defn start-stream-watchdog!
  "Starts a daemon thread that monitors a streaming connection.
   Closes `closeable` when `cancelled?` returns true or when no SSE events
   have been received for `idle-timeout-ms` while actively reading.

   Returns a map with:
   - :touch-fn        - call on each received event to reset the idle timer
   - :set-reading-fn  - call with true/false to track whether blocked on .readLine
   - :stop-fn         - call to stop the watchdog (e.g. in finally)
   - :reason*         - atom; :cancelled or :idle-timeout when triggered, nil otherwise"
  [^Closeable closeable cancelled? {:keys [idle-timeout-ms check-interval-ms]
                                    :or {idle-timeout-ms default-stream-idle-timeout-ms
                                         check-interval-ms default-stream-check-interval-ms}}]
  (let [last-activity* (atom (System/currentTimeMillis))
        in-read?* (atom false)
        running?* (atom true)
        reason* (atom nil)
        thread (Thread.
                (fn []
                  (try
                    (while @running?*
                      (Thread/sleep (long check-interval-ms))
                      (when @running?*
                        (cond
                          (and cancelled? (cancelled?))
                          (do (reset! reason* :cancelled)
                              (reset! running?* false)
                              (.close closeable))

                          (and @in-read?*
                               (> (- (System/currentTimeMillis) @last-activity*)
                                  idle-timeout-ms))
                          (do (reset! reason* :idle-timeout)
                              (reset! running?* false)
                              (.close closeable)))))
                    (catch Exception _))))]
    (.setDaemon thread true)
    (.start thread)
    {:touch-fn (fn [] (reset! last-activity* (System/currentTimeMillis)))
     :set-reading-fn (fn [reading?] (reset! in-read?* (boolean reading?)))
     :stop-fn (fn []
                (reset! running?* false)
                (.interrupt thread))
     :reason* reason*}))

(defn provider-api-key [provider provider-auth config]
  (or (when-let [key (not-empty (get-in config [:providers (name provider) :key]))]
        [:auth/token key])
      (when-let [key (:api-key provider-auth)]
        [(get provider-auth :type :auth/oauth) key])
      (when-let [key (config/get-env (str (csk/->SCREAMING_SNAKE_CASE (name provider)) "_API_KEY"))]
        [:auth/token key])
      ;; legacy
      (when-let [key (some-> (get-in config [:providers (name provider) :keyRc])
                             (secrets/get-credential (:netrcFile config)))]
        [:auth/token key])
      (when-let [key (some-> (get-in config [:providers (name provider) :keyEnv])
                             config/get-env)]
        [:auth/token key])))

(defn provider-api-url [provider config]
  (some-> (or (not-empty (get-in config [:providers (name provider) :url]))
              (config/get-env (str (csk/->SCREAMING_SNAKE_CASE (name provider)) "_API_URL"))
              (some-> (get-in config [:providers (name provider) :urlEnv]) config/get-env)) ;; legacy
          shared/normalize-api-url
          not-empty))

(defmulti provider-models-override
  "Provider+auth specific override for native /models fetching.

   Dispatches on `[provider auth-type]`. Implementations live in the provider
   namespace (e.g. `eca.llm-providers.openai`) and return a map of
   model-id -> model-config in the same shape users put under
   `:providers <p> :models` (e.g. `{\"gpt-5.5\" {:limit {:context 272000}}}`),
   letting the generic catalog code apply them through the existing override
   path. Return nil to fall back to the generic native /models fetch."
  (fn [{:keys [provider auth-type]}] [provider auth-type]))

(defmethod provider-models-override :default [_] nil)

(defn copilot-ide-headers
  "GitHub Copilot's API authenticates IDE requests by a recognized editor
   identity. A non-editor value (e.g. `eca/...`) is rejected with
   `400 missing/unknown Editor-Version header for IDE auth`, so we present the
   VS Code Copilot client identity its `/models` and chat endpoints expect."
  []
  {"editor-version" "vscode/1.107.0"
   "editor-plugin-version" "copilot-chat/0.35.0"
   "copilot-integration-id" "vscode-chat"})

(defn ^:private cause-chain
  "Returns a seq of `e` followed by every nested cause."
  [^Throwable e]
  (->> (iterate (fn [^Throwable t] (.getCause t)) e)
       (take-while some?)))

(defn ^:private root-message [^Throwable e]
  (or (ex-message e) (.getName (class e))))

(defn classify-connection-exception
  "Walks the cause chain of `e` and classifies common HTTP/TLS failures
   into a user-friendly map: {:kind <keyword> :message <string>}.

   Recognized kinds:
   - :tls-untrusted     - PKIX path building failed (private/corporate CA not trusted)
   - :connection-closed - connection dropped mid-request (EOF, reset, closed)
   - :tls-other         - other TLS/SSL handshake errors
   - :dns               - UnknownHostException
   - :connect-refused   - ConnectException (connection refused, etc.)
   - :timeout           - connection/socket timeouts
   - :unknown           - fallback; keeps the historical 'Connection error: ...' format"
  [^Throwable e]
  (let [msg (root-message e)
        causes (cause-chain e)
        pkix? (some (fn [^Throwable c]
                      (some-> (ex-message c)
                              (string/includes? "PKIX path building failed")))
                    causes)
        closed? (some (fn [^Throwable c]
                        (or (instance? EOFException c)
                            (when-let [m (some-> (ex-message c) string/lower-case)]
                              (or (string/includes? m "eof reached")
                                  (string/includes? m "connection reset")
                                  (= m "closed")))))
                      causes)
        ssl?  (some #(instance? SSLException %) causes)
        dns?  (some #(instance? UnknownHostException %) causes)
        connect-refused? (some #(instance? ConnectException %) causes)
        timeout? (some #(or (instance? HttpConnectTimeoutException %)
                            (instance? SocketTimeoutException %))
                       causes)]
    (cond
      pkix?
      {:kind :tls-untrusted
       :message (str "TLS certificate not trusted: PKIX path building failed. "
                     "The server's certificate is signed by a CA not in the JVM truststore "
                     "(common with private/corporate CAs). "
                     "Fix: set `network.caCertFile` in your ECA config or the `SSL_CERT_FILE` "
                     "env var to a PEM bundle containing the missing CA. "
                     "See docs/config/network.md for details. Original error: " msg)}

      closed?
      {:kind :connection-closed
       :message (str "Connection closed unexpectedly: " msg
                     ". The server, a proxy or the network dropped the connection mid-request"
                     " (common behind corporate proxies/VPNs with idle or streaming timeouts).")}

      ssl?
      {:kind :tls-other
       :message (str "TLS error: " msg
                     ". See docs/config/network.md for trust and mTLS configuration.")}

      dns?
      {:kind :dns
       :message (str "DNS resolution failed: " msg
                     ". Check the provider URL and your network/proxy settings.")}

      connect-refused?
      {:kind :connect-refused
       :message (str "Could not connect: " msg
                     ". Check the provider URL and whether the server is reachable. "
                     "Corporate networks may require HTTP_PROXY / HTTPS_PROXY env vars.")}

      timeout?
      {:kind :timeout
       :message (str "Connection timed out: " msg ".")}

      :else
      {:kind :unknown
       :message (format "Connection error: %s" msg)})))

(defn connection-error-message
  "Returns a user-friendly message describing a connection-level exception.
   Always non-nil. See `classify-connection-exception` for recognized error kinds."
  [^Throwable e]
  (:message (classify-connection-exception e)))
