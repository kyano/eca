(ns eca.features.chat-test
  (:require
   [babashka.fs :as fs]
   [clojure.string :as string]
   [clojure.test :refer [deftest is testing]]
   [eca.config :as config]
   [eca.db :as db]
   [eca.features.chat :as f.chat]
   [eca.features.chat.lifecycle :as lifecycle]
   [eca.features.context :as f.context]
   [eca.features.index :as f.index]
   [eca.features.prompt :as f.prompt]
   [eca.features.rules :as f.rules]
   [eca.features.skills :as f.skills]
   [eca.features.tools :as f.tools]
   [eca.features.tools.chat :as f.tools.chat]
   [eca.features.tools.mcp :as f.mcp]
   [eca.llm-api :as llm-api]
   [eca.llm-util :as llm-util]
   [eca.logger :as logger]
   [eca.test-helper :as h]
   [matcher-combinators.matchers :as m]
   [matcher-combinators.test :refer [match?]]))

(deftest list-chats-test
  (testing "uses map key as :id even when value lacks an :id field
            (regression: legacy DB rows pre-date the per-chat :id field
            and would otherwise come back as :id nil, breaking chat/open)"
    (let [db {:chats {"c1" {:title "With id" :messages [{} {}] :updated-at 2}
                      "c2" {:id "stale-id" :title "Mismatched" :messages [{}] :updated-at 1}
                      "c3" {:title "Subagent" :subagent true :messages [{}] :updated-at 3}}}
          {:keys [chats]} (f.chat/list-chats db {})]
      (is (= 2 (count chats)))
      (is (every? :id chats))
      (is (= ["c1" "c2"] (mapv :id chats)))))

  (testing "respects :limit"
    (let [db {:chats {"c1" {:title "a" :messages [] :updated-at 3}
                      "c2" {:title "b" :messages [] :updated-at 2}
                      "c3" {:title "c" :messages [] :updated-at 1}}}
          {:keys [chats]} (f.chat/list-chats db {:limit 2})]
      (is (= 2 (count chats)))
      (is (= ["c1" "c2"] (mapv :id chats)))))

  (testing "tags inline chats with :kind"
    (let [db {:chats {"c1" {:title "regular" :messages [] :updated-at 2}
                      "c2" {:title "inline: q" :kind :inline :messages [] :updated-at 1}}}
          {:keys [chats]} (f.chat/list-chats db {})]
      (is (= [nil :inline] (mapv :kind chats)))
      (is (not (contains? (first chats) :kind))))))

(deftest prompt-steer-test
  (let [test-config (assoc (config/initial-config) :env "test")]
    (testing "steered message on a running chat is queued"
      (h/reset-components!)
      (swap! (h/db*) assoc-in [:chats "chat-1"] {:id "chat-1" :status :running})
      (f.chat/prompt-steer {:chat-id "chat-1" :message "also do X"}
                           (h/db*) (h/messenger) test-config (h/metrics))
      (is (= "also do X" (get-in @(h/db*) [:chats "chat-1" :steer-message]))))

    (testing "steered /btw is diverted to an immediate prompt instead of queued"
      (h/reset-components!)
      (swap! (h/db*) assoc-in [:chats "chat-1"] {:id "chat-1"
                                                 :status :running
                                                 :agent "code"
                                                 :variant "high"})
      (let [prompted* (atom nil)]
        (with-redefs [f.chat/prompt (fn [params & _] (reset! prompted* params))]
          (f.chat/prompt-steer {:chat-id "chat-1" :message "/btw what is foo?"}
                               (h/db*) (h/messenger) test-config (h/metrics)))
        (is (= {:chat-id "chat-1"
                :message "/btw what is foo?"
                :agent "code"
                :variant "high"}
               @prompted*))
        (is (nil? (get-in @(h/db*) [:chats "chat-1" :steer-message])))))

    (testing "steered native command that can't run mid-turn is refused with a notice instead of queued"
      (h/reset-components!)
      (swap! (h/db*) assoc-in [:chats "chat-1"] {:id "chat-1" :status :running})
      (f.chat/prompt-steer {:chat-id "chat-1" :message "/model openai/gpt-5.2"}
                           (h/db*) (h/messenger) test-config (h/metrics))
      (is (nil? (get-in @(h/db*) [:chats "chat-1" :steer-message])))
      (is (match? [{:role :system :content {:type :text :text #(string/includes? % "`/model` can't run while the chat is working")}}]
                  (:chat-content-received (h/messages)))))

    (testing "steered native command that is safe mid-turn is queued for the turn boundary"
      (h/reset-components!)
      (swap! (h/db*) assoc-in [:chats "chat-1"] {:id "chat-1" :status :running})
      (f.chat/prompt-steer {:chat-id "chat-1" :message "/costs"}
                           (h/db*) (h/messenger) test-config (h/metrics))
      (is (= "/costs" (get-in @(h/db*) [:chats "chat-1" :steer-message])))
      (is (empty? (:chat-content-received (h/messages)))))

    (testing "steered text starting with a path is still queued as plain text"
      (h/reset-components!)
      (swap! (h/db*) assoc-in [:chats "chat-1"] {:id "chat-1" :status :running})
      (f.chat/prompt-steer {:chat-id "chat-1" :message "/path/to/file has a bug"}
                           (h/db*) (h/messenger) test-config (h/metrics))
      (is (= "/path/to/file has a bug" (get-in @(h/db*) [:chats "chat-1" :steer-message]))))

    (testing "steered message on an idle chat is dropped"
      (h/reset-components!)
      (swap! (h/db*) assoc-in [:chats "chat-1"] {:id "chat-1" :status :idle})
      (f.chat/prompt-steer {:chat-id "chat-1" :message "hello"}
                           (h/db*) (h/messenger) test-config (h/metrics))
      (is (nil? (get-in @(h/db*) [:chats "chat-1" :steer-message]))))))

(deftest query-context-test
  (testing "preserves order and removes already added contexts"
    (with-redefs [f.context/all-contexts (fn [& _]
                                           [{:type "file" :path "/a"}
                                            {:type "directory" :path "/b"}
                                            {:type "file" :path "/a"}
                                            {:type "file" :path "/c"}])]
      (is (= {:chat-id "c1"
              :contexts [{:type "file" :path "/a"}
                         {:type "file" :path "/c"}]}
             (f.chat/query-context {:chat-id "c1"
                                    :query ""
                                    :contexts [{:type "directory" :path "/b"}]}
                                   (atom {})
                                   {}))))))

(deftest query-files-test
  (testing "preserves order and dedupes"
    (with-redefs [f.context/all-contexts (fn [& _]
                                           [{:type "file" :path "/b"}
                                            {:type "file" :path "/a"}
                                            {:type "file" :path "/b"}])]
      (is (= {:chat-id "c1"
              :files [{:type "file" :path "/b"}
                      {:type "file" :path "/a"}]}
             (f.chat/query-files {:chat-id "c1" :query "x"} (atom {}) {}))))))

(h/reset-components-before-test)

(defn ^:private prompt! [params mocks]
  (let [{:keys [chat-id] :as resp}
        (with-redefs [llm-api/sync-or-async-prompt! (:api-mock mocks)
                      llm-api/sync-prompt! (constantly nil)
                      f.tools/call-tool! (:call-tool-mock mocks)
                      f.tools/all-tools (:all-tools-mock mocks)
                      f.tools/approval (constantly :allow)
                      config/await-plugins-resolved! (constantly true)]
          (h/config! {:env "test"})
          (swap! (h/db*) update :models
                 (fn [models]
                   (merge {"openai/gpt-5.2" {:tools true}}
                          (or models {}))))
          (f.chat/prompt params (h/db*) (h/messenger) (h/config) (h/metrics)))]
    (is (match? {:chat-id string? :status :prompting} resp))
    {:chat-id chat-id}))

(defn ^:private deep-sleep
  "Sleep for the given duration in milliseconds, ignoring interrupts.
   Continues sleeping until the full duration has elapsed."
  [millis]
  (let [deadline (+ (System/currentTimeMillis) millis)]
    (loop [remaining (- deadline (System/currentTimeMillis))]
      (when (pos? remaining)
        (try
          (Thread/sleep (long remaining))
          (catch InterruptedException _))
        (recur (- deadline (System/currentTimeMillis)))))))

(deftest prompt-lazy-config-test
  (testing "user prompt is echoed and progress sent before config is resolved"
    (h/reset-components!)
    (let [messages-at-resolve* (atom nil)
          {:keys [chat-id]}
          (with-redefs [llm-api/sync-or-async-prompt! (fn [{:keys [on-first-response-received on-message-received]}]
                                                        (on-first-response-received {:type :text :text "Hey"})
                                                        (on-message-received {:type :text :text "Hey"})
                                                        (on-message-received {:type :finish}))
                        llm-api/sync-prompt! (constantly nil)
                        f.tools/all-tools (constantly [])
                        f.tools/approval (constantly :allow)
                        config/await-plugins-resolved! (constantly true)]
            (h/config! {:env "test"})
            (swap! (h/db*) update :models
                   (fn [models] (merge {"openai/gpt-5.2" {:tools true}} (or models {}))))
            (f.chat/prompt {:message "Hey!"}
                           (h/db*)
                           (h/messenger)
                           (fn []
                             (reset! messages-at-resolve* (:chat-content-received (h/messages)))
                             (h/config))
                           (h/metrics)))]
      (is (match? [{:chat-id chat-id :role :user :content {:type :text :text "Hey!\n"}}
                   {:chat-id chat-id :role :system :content {:type :progress :state :running :text "Loading config"}}]
                  @messages-at-resolve*))
      (is (match? {:chat-content-received
                   [{:role :user :content {:type :text :text "Hey!\n"}}
                    {:role :system :content {:type :progress :state :running :text "Loading config"}}
                    {:role :system :content {:type :progress :state :running :text "Waiting model"}}
                    {:role :system :content {:type :progress :state :running :text "Generating"}}
                    {:role :assistant :content {:type :text :text "Hey"}}
                    {:role :system :content {:type :progress :state :finished}}]}
                  (h/messages)))))
  (testing "config resolution failure reports the error and finishes the progress"
    (h/reset-components!)
    (let [resp (f.chat/prompt {:message "Hey!"}
                              (h/db*)
                              (h/messenger)
                              (fn [] (throw (ex-info "boom" {})))
                              (h/metrics))]
      (is (match? {:chat-id string? :model "error" :status :error} resp))
      (is (match? {:chat-content-received
                   [{:role :user :content {:type :text :text "Hey!\n"}}
                    {:role :system :content {:type :progress :state :running :text "Loading config"}}
                    {:role :system :content {:type :text :text #"^Error: boom"}}
                    {:role :system :content {:type :progress :state :finished}}]}
                  (h/messages))))))

(deftest prompt-basic-test
  (testing "Simple hello"
    (h/reset-components!)
    (let [{:keys [chat-id]}
          (prompt!
           {:message "Hey!"}
           {:all-tools-mock (constantly [])
            :api-mock
            (fn [{:keys [on-first-response-received
                         on-message-received]}]
              (on-first-response-received {:type :text :text "Hey"})
              (on-message-received {:type :text :text "Hey"})
              (on-message-received {:type :text :text " you!"})
              (on-message-received {:type :finish}))})]
      (is (match?
           {chat-id {:id chat-id
                     :messages [{:role "user" :content [{:type :text :text "Hey!"}]}
                                {:role "assistant" :content [{:type :text :text "Hey you!"}]}]}}
           (:chats (h/db))))
      (is (match?
           {:chat-content-received
            [{:chat-id chat-id
              :content {:type :text :text "Hey!\n"}
              :role :user}
             {:chat-id chat-id
              :content {:type :progress :state :running :text "Waiting model"}
              :role :system}
             {:chat-id chat-id
              :content {:type :progress :state :running :text "Generating"}
              :role :system}
             {:chat-id chat-id
              :content {:type :text :text "Hey"}
              :role :assistant}
             {:chat-id chat-id
              :content {:type :text :text " you!"}
              :role :assistant}
             {:chat-id chat-id
              :content {:state :finished :type :progress}
              :role :system}]}
           (h/messages)))))
  (testing "LLM error"
    (h/reset-components!)
    (let [{:keys [chat-id]}
          (prompt!
           {:message "Hey!"}
           {:all-tools-mock (constantly [])
            :api-mock
            (fn [{:keys [on-error]}]
              (on-error {:message "Error from mocked API"}))})]
      (is (match?
           {chat-id {:id chat-id :messages [{:role "user" :content [{:type :text :text "Hey!"}]}]}}
           (:chats (h/db)))
          "the unanswered message is kept so the next prompt and /resume still have it (eca-intellij#27)")
      (is (match?
           {:chat-content-received
            [{:chat-id chat-id
              :content {:type :text :text "Hey!\n"}
              :role :user}
             {:chat-id chat-id
              :content {:type :progress :state :running :text "Waiting model"}
              :role :system}
             {:chat-id chat-id
              :content {:type :text :text "\n\nError from mocked API"}
              :role :system}
             {:chat-id chat-id
              :content {:state :finished :type :progress}
              :role :system}]}
           (h/messages))))))

(deftest terminal-prompt-error-test
  (testing "records a terminal provider error after chat recovery is exhausted"
    (h/reset-components!)
    (let [attempts* (atom 0)
          {:keys [chat-id]}
          (prompt!
           {:message "Investigate the failure"}
           {:all-tools-mock (constantly [])
            :api-mock
            (fn [{:keys [on-error]}]
              (swap! attempts* inc)
              (on-error {:status 503
                         :body "{\"error\":{\"message\":\"auth_unavailable: no auth available\",\"type\":\"server_error\",\"code\":\"internal_server_error\"}}"
                         :message "OpenAI response status: 503 body: auth_unavailable"}))})]
      (is (= 4 @attempts*) "one initial request plus three chat-level recovery attempts")
      (is (match? {:message "OpenAI response status: 503 body: auth_unavailable"
                   :error-type :overloaded
                   :status 503}
                  (get-in (h/db) [:chats chat-id :prompt-error]))))))

(deftest provider-max-auto-continues-test
  (is (= 3 (#'f.chat/provider-max-auto-continues {} "openai")))
  (is (= 7 (#'f.chat/provider-max-auto-continues
            {:providers {"openai" {:retry {:maxAutoContinues 7}}}}
            "openai")))
  (is (= 0 (#'f.chat/provider-max-auto-continues
            {:providers {"openai" {:retry {:maxAutoContinues 0}}}}
            "openai")))
  (is (= 3 (#'f.chat/provider-max-auto-continues
            {:providers {"openai" {:retry {:maxAutoContinues -1}}}}
            "openai"))))

(deftest transient-error-recovery-test
  (testing "retries the original request when overload happens before model output"
    (h/reset-components!)
    (let [requests* (atom [])
          {:keys [chat-id]}
          (prompt!
           {:message "Investigate the failure"}
           {:all-tools-mock (constantly [])
            :api-mock
            (fn [{:keys [user-messages on-first-response-received on-message-received on-error]}]
              (let [attempt (count (swap! requests* conj user-messages))]
                (if (= 1 attempt)
                  (on-error {:code "server_is_overloaded"
                             :message "Our servers are currently overloaded. Please try again later."
                             :error/source :openai-responses})
                  (do
                    (on-first-response-received {:type :text :text "Recovered"})
                    (on-message-received {:type :text :text "Recovered"})
                    (on-message-received {:type :finish})))))})]
      (is (= 2 (count @requests*)))
      (is (= (first @requests*) (second @requests*))
          "a no-output retry must replay the original task, not a contextless continuation")
      (is (match? [{:role "user" :content [{:type :text :text "Investigate the failure"}]}
                   {:role "assistant" :content [{:type :text :text "Recovered"}]}]
                  (get-in (h/db) [:chats chat-id :messages])))
      (is (nil? (get-in (h/db) [:chats chat-id :prompt-error]))))))

(deftest transient-tls-record-failure-recovery-test
  (testing "auto-continues after a partial response is interrupted by bad_record_mac"
    (h/reset-components!)
    (let [requests* (atom [])
          api-mock (fn [{:keys [user-messages on-first-response-received on-message-received on-error]}]
                     (let [attempt (count (swap! requests* conj user-messages))]
                       (if (= 1 attempt)
                         (do
                           (on-first-response-received {:type :text :text "Partial"})
                           (on-message-received {:type :text :text "Partial"})
                           (on-error {:exception (javax.net.ssl.SSLException.
                                                  "(bad_record_mac) Received fatal alert: bad_record_mac")
                                      :message "Connection closed unexpectedly: (bad_record_mac) Received fatal alert: bad_record_mac"}))
                         (do
                           (on-first-response-received {:type :text :text "Recovered"})
                           (on-message-received {:type :text :text "Recovered"})
                           (on-message-received {:type :finish})))))
          chat-id (:chat-id
                   (prompt! {:message "Investigate the failure"}
                            {:all-tools-mock (constantly [])
                             :api-mock api-mock}))]
    (is (= 2 (count @requests*)))
    (is (match? [{:role "user" :content [{:type :text :text "Investigate the failure"}]}
                 {:role "assistant" :content [{:type :text :text "Partial"}]}
                 {:role "user"
                  :content [{:type :text
                             :text "Your previous response was interrupted mid-stream. Continue from where you left off, do not redo completed steps."}]}
                 {:role "assistant" :content [{:type :text :text "Recovered"}]}]
                (get-in (h/db) [:chats chat-id :messages])))
    (is (match? {:chat-content-received
                 (m/embeds [{:role :system
                             :content {:type :progress
                                       :text #(string/includes? % "Connection closed unexpectedly")}}])}
                (h/messages))))))

(deftest idle-timeout-recovery-asks-to-split-large-tool-calls-test
  (h/reset-components!)
  (let [requests* (atom [])
        api-mock (fn [{:keys [user-messages on-first-response-received on-message-received on-error]}]
                   (if (= 1 (count (swap! requests* conj user-messages)))
                     (do
                       (on-first-response-received {:type :text :text "Writing the file"})
                       (on-message-received {:type :text :text "Writing the file"})
                       (on-error (llm-util/idle-timeout-error 300 (java.io.IOException. "closed"))))
                     (on-message-received {:type :finish})))]
    (prompt! {:message "Write a big file"}
             {:all-tools-mock (constantly [])
              :api-mock api-mock})
    (is (match? [{:role "user"
                  :content [{:type :text
                             :text #(string/includes? % "split it into a few smaller calls")}]}]
                (second @requests*)))))

(deftest transient-tls-recovery-limit-test
  (doseq [[configured limit reason] [[nil 3 :limit-reached] [2 2 :limit-reached] [0 0 :disabled]]]
    (testing (str "bounded TLS recovery with maxAutoContinues=" configured)
      (h/reset-components!)
      (when (some? configured)
        (h/config! {:providers {"openai" {:retry {:maxAutoContinues configured}}}}))
      (let [attempts* (atom 0)
            logs* (atom [])
            exception (javax.net.ssl.SSLException. "(bad_record_mac) Received fatal alert: bad_record_mac")
            error-data {:exception exception :message (llm-util/connection-error-message exception)}
            {:keys [chat-id]}
            (with-redefs [logger/info (fn [& args] (swap! logs* conj args))]
              (prompt!
               {:message "Keep working"}
               {:all-tools-mock (constantly [])
                :api-mock (fn [{:keys [on-first-response-received on-message-received on-error]}]
                            (swap! attempts* inc)
                            (on-first-response-received)
                            (on-message-received {:type :text :text "Partial"})
                            (on-error error-data)
                            (on-error error-data))}))
            recovery-progress (->> (:chat-content-received (h/messages))
                                   (map :content)
                                   (filter #(and (= :progress (:type %))
                                                 (string/includes? (or (:text %) "") "recovery "))))
            skipped (filter #(= "Automatic recovery skipped" (second %)) @logs*)]
        (is (= (inc limit) @attempts*))
        (is (= :idle (get-in (h/db) [:chats chat-id :status])))
        (is (= :network (get-in (h/db) [:chats chat-id :prompt-error :error-type])))
        (is (= limit (count recovery-progress)))
        (doseq [[i progress] (map-indexed vector recovery-progress)]
          (is (string/includes? (:text progress) (format "recovery %d/%d" (inc i) limit))))
        (is (= 1 (count skipped)) "late duplicate failures must not deliver another terminal error")
        (is (match? {:reason reason :auto-continue-count limit :max-auto-continues limit}
                    (last (first skipped))))
        (is (match? {:chat-content-received
                     (m/embeds [{:role :system
                                 :content {:type :text
                                           :text #(and (string/includes? % "bad_record_mac")
                                                       (string/includes? % (if (zero? limit)
                                                                             "Automatic recovery is disabled"
                                                                             (format "Automatic recovery limit reached (%d/%d for this turn)" limit limit))))}}])}
                    (h/messages)))))))

(deftest truncated-response-shares-recovery-budget-test
  (h/config! {:providers {"openai" {:retry {:maxAutoContinues 1}}}})
  (let [attempts* (atom 0)
        exception (javax.net.ssl.SSLException. "Received fatal alert: bad_record_mac")
        {:keys [chat-id]}
        (prompt!
         {:message "Keep working"}
         {:all-tools-mock (constantly [])
          :api-mock (fn [{:keys [on-first-response-received on-message-received on-error]}]
                      (let [attempt (swap! attempts* inc)]
                        (on-first-response-received)
                        (on-message-received {:type :text :text "Partial"})
                        (if (= 1 attempt)
                          (on-message-received {:type :finish :premature? true})
                          (on-error {:exception exception
                                     :message (llm-util/connection-error-message exception)}))))})]
    (is (= 2 @attempts*))
    (is (= :network (get-in (h/db) [:chats chat-id :prompt-error :error-type])))
    (is (match? {:chat-content-received
                 (m/embeds [{:role :system
                             :content {:type :progress :text #"Response interrupted.*recovery 1/1"}}
                            {:role :system
                             :content {:type :text :text #"(?s).*Automatic recovery limit reached \(1/1 for this turn\).*"}}])}
                (h/messages)))))

(deftest stream-error-rejects-preparing-tool-calls-test
  (let [exception (javax.net.ssl.SSLException. "Received fatal alert: bad_record_mac")
        error-data {:exception exception :message (llm-util/connection-error-message exception)}
        prepare-write! (fn [on-first-response-received on-prepare-tool-call]
                         (on-first-response-received)
                         (on-prepare-tool-call {:id "call-1"
                                                :full-name "eca__write_file"
                                                :arguments-text "{\"path\":\"/foo/core.cljs\",\"content\":\"(ns"}))
        rejections (fn []
                     (->> (:chat-content-received (h/messages))
                          (filter #(= :toolCallRejected (get-in % [:content :type])))))]
    (testing "a tool call still preparing when the stream drops is rejected before the automatic retry"
      (h/reset-components!)
      (let [attempts* (atom 0)
            {:keys [chat-id]}
            (prompt!
             {:message "Write the file"}
             {:all-tools-mock (constantly [])
              :api-mock (fn [{:keys [on-first-response-received on-message-received on-prepare-tool-call on-error]}]
                          (if (= 1 (swap! attempts* inc))
                            (do (prepare-write! on-first-response-received on-prepare-tool-call)
                                (on-error error-data))
                            (do (on-first-response-received {:type :text :text "Done"})
                                (on-message-received {:type :text :text "Done"})
                                (on-message-received {:type :finish}))))})]
        (is (= 2 @attempts*))
        (is (match? [{:role :assistant :content {:id "call-1" :reason :interrupted}}] (rejections)))
        (is (not= :preparing (get-in (h/db) [:chats chat-id :tool-calls "call-1" :status])))))

    (testing "a tool call still preparing is rejected when automatic recovery gives up"
      (h/reset-components!)
      (h/config! {:providers {"openai" {:retry {:maxAutoContinues 0}}}})
      (let [{:keys [chat-id]}
            (prompt!
             {:message "Write the file"}
             {:all-tools-mock (constantly [])
              :api-mock (fn [{:keys [on-first-response-received on-prepare-tool-call on-error]}]
                          (prepare-write! on-first-response-received on-prepare-tool-call)
                          (on-error error-data))})]
        (is (match? [{:role :assistant :content {:id "call-1" :reason :interrupted}}] (rejections)))
        (is (not= :preparing (get-in (h/db) [:chats chat-id :tool-calls "call-1" :status])))))

    (testing "a tool call still preparing is rejected when the request throws"
      (h/reset-components!)
      (let [{:keys [chat-id]}
            (prompt!
             {:message "Write the file"}
             {:all-tools-mock (constantly [])
              :api-mock (fn [{:keys [on-first-response-received on-prepare-tool-call]}]
                          (prepare-write! on-first-response-received on-prepare-tool-call)
                          (throw (ex-info "boom" {})))})]
        (is (match? [{:role :assistant :content {:id "call-1" :reason :interrupted}}] (rejections)))
        (is (not= :preparing (get-in (h/db) [:chats chat-id :tool-calls "call-1" :status])))))))

(deftest unanswered-user-message-is-kept-test
  (testing "eca-intellij#27: a message the LLM never answered (network down until
            automatic recovery gave up) stays in history and reaches the model
            with the next prompt"
    (h/reset-components!)
    (h/config! {:providers {"openai" {:retry {:maxAutoContinues 0}}}})
    (let [exception (java.net.ConnectException. "Connection refused")
          error-data {:exception exception :message (llm-util/connection-error-message exception)}
          past-messages* (atom nil)
          {:keys [chat-id]} (prompt! {:message "hello"}
                                     {:all-tools-mock (constantly [])
                                      :api-mock (fn [{:keys [on-error]}] (on-error error-data))})]
      (is (match? [{:role "user" :content [{:type :text :text "hello"}]}]
                  (get-in (h/db) [:chats chat-id :messages])))
      (is (not (contains? (get-in (h/db) [:chats chat-id]) :unsent-user-messages)))
      (prompt! {:message "second" :chat-id chat-id}
               {:all-tools-mock (constantly [])
                :api-mock (fn [{:keys [past-messages on-first-response-received on-message-received]}]
                            (reset! past-messages* past-messages)
                            (on-first-response-received {:type :text :text "ok"})
                            (on-message-received {:type :text :text "ok"})
                            (on-message-received {:type :finish}))})
      (is (match? (m/embeds [{:role "user" :content [{:type :text :text "hello"}]}]) @past-messages*))
      (is (match? [{:role "user" :content [{:type :text :text "hello"}]}
                   {:role "user" :content [{:type :text :text "second"}]}
                   {:role "assistant" :content [{:type :text :text "ok"}]}]
                  (get-in (h/db) [:chats chat-id :messages])))))

  (testing "a message stopped before the LLM answered stays in history"
    (h/reset-components!)
    (let [chat-id "stopped-before-response"]
      (prompt! {:message "hello" :chat-id chat-id}
               {:all-tools-mock (constantly [])
                :api-mock (fn [_]
                            (f.chat/prompt-stop {:chat-id chat-id} (h/db*) (h/messenger) (h/config) (h/metrics) {}))})
      (is (match? [{:role "user" :content [{:type :text :text "hello"}]}]
                  (get-in (h/db) [:chats chat-id :messages]))))))

(deftest transient-tls-recovery-guards-test
  (doseq [[state reason] [[{:status :stopping} :stopping]
                          [{:auto-compacting? true} :compacting]
                          [{:compacting? true} :compacting]]]
    (testing (str "TLS recovery stays blocked by " state)
      (h/reset-components!)
      (let [chat-id "tls-recovery-guard"
            attempts* (atom 0)
            logs* (atom [])
            exception (javax.net.ssl.SSLException. "Received fatal alert: bad_record_mac")]
        (with-redefs [logger/info (fn [& args] (swap! logs* conj args))]
          (prompt!
           {:message "Keep working" :chat-id chat-id}
           {:all-tools-mock (constantly [])
            :api-mock (fn [{:keys [on-error]}]
                        (swap! attempts* inc)
                        (swap! (h/db*) update-in [:chats chat-id] merge state)
                        (on-error {:exception exception
                                   :message (llm-util/connection-error-message exception)}))}))
        (is (= 1 @attempts*))
        (is (match? {:reason reason :auto-continue-count 0 :max-auto-continues 3}
                    (->> @logs*
                         (filter #(= "Automatic recovery skipped" (second %)))
                         first
                         last)))
        (is (nil? (get-in (h/db) [:chats chat-id :auto-compacting?])))
        (is (nil? (get-in (h/db) [:chats chat-id :compacting?])))
        (when (= :stopping reason)
          (is (nil? (get-in (h/db) [:chats chat-id :prompt-error]))))))))

(deftest prompt-multiple-text-interaction-test
  (testing "Chat history"
    (h/reset-components!)
    (let [res-1
          (prompt!
           {:message "Count with me: 1 mississippi"}
           {:all-tools-mock (constantly [])
            :api-mock
            (fn [{:keys [on-first-response-received
                         on-message-received]}]
              (on-first-response-received {:type :text :text "2"})
              (on-message-received {:type :text :text "2"})
              (on-message-received {:type :text :text " mississippi"})
              (on-message-received {:type :finish}))})
          chat-id-1 (:chat-id res-1)]
      (is (match?
           {chat-id-1 {:id chat-id-1
                       :messages [{:role "user" :content [{:type :text :text "Count with me: 1 mississippi"}]}
                                  {:role "assistant" :content [{:type :text :text "2 mississippi"}]}]}}
           (:chats (h/db))))
      (is (match?
           {:chat-content-received
            [{:chat-id chat-id-1
              :content {:type :text :text "Count with me: 1 mississippi\n"}
              :role :user}
             {:chat-id chat-id-1
              :content {:type :progress :state :running :text "Waiting model"}
              :role :system}
             {:chat-id chat-id-1
              :content {:type :progress :state :running :text "Generating"}
              :role :system}
             {:chat-id chat-id-1
              :content {:type :text :text "2"}
              :role :assistant}
             {:chat-id chat-id-1
              :content {:type :text :text " mississippi"}
              :role :assistant}
             {:chat-id chat-id-1
              :content {:state :finished :type :progress}
              :role :system}]}
           (h/messages)))
      (h/reset-messenger!)
      (let [res-2
            (prompt!
             {:message "3 mississippi"
              :chat-id chat-id-1}
             {:all-tools-mock (constantly [])
              :api-mock
              (fn [{:keys [on-first-response-received
                           on-message-received]}]
                (on-first-response-received {:type :text :text "4"})
                (on-message-received {:type :text :text "4"})
                (on-message-received {:type :text :text " mississippi"})
                (on-message-received {:type :finish}))})
            chat-id-2 (:chat-id res-2)]
        (is (match?
             {chat-id-2 {:id chat-id-2
                         :messages [{:role "user" :content [{:type :text :text "Count with me: 1 mississippi"}]}
                                    {:role "assistant" :content [{:type :text :text "2 mississippi"}]}
                                    {:role "user" :content [{:type :text :text "3 mississippi"}]}
                                    {:role "assistant" :content [{:type :text :text "4 mississippi"}]}]}}
             (:chats (h/db))))
        (is (match?
             {:chat-content-received
              [{:chat-id chat-id-2
                :content {:type :text :text "3 mississippi\n"}
                :role :user}
               {:chat-id chat-id-2
                :content {:type :progress :state :running :text "Waiting model"}
                :role :system}
               {:chat-id chat-id-2
                :content {:type :progress :state :running :text "Generating"}
                :role :system}
               {:chat-id chat-id-2
                :content {:type :text :text "4"}
                :role :assistant}
               {:chat-id chat-id-2
                :content {:type :text :text " mississippi"}
                :role :assistant}
               {:chat-id chat-id-2
                :content {:state :finished :type :progress}
                :role :system}]}
             (h/messages)))))))

(deftest prompt-persists-chat-variant-test
  (let [api-mock (fn [{:keys [on-first-response-received on-message-received]}]
                   (on-first-response-received {:type :text :text "ok"})
                   (on-message-received {:type :text :text "ok"})
                   (on-message-received {:type :finish}))
        run-prompt! (fn [params extra-config]
                      (with-redefs [llm-api/sync-or-async-prompt! api-mock
                                    llm-api/sync-prompt! (constantly nil)
                                    f.tools/call-tool! (constantly nil)
                                    f.tools/all-tools (constantly [])
                                    f.tools/approval (constantly :allow)
                                    config/await-plugins-resolved! (constantly true)]
                        (h/config! (merge {:env "test"} extra-config))
                        (swap! (h/db*) update :models
                               (fn [models]
                                 (merge {"openai/gpt-5.2" {:tools true}}
                                        (or models {}))))
                        (f.chat/prompt params (h/db*) (h/messenger) (h/config) (h/metrics))))]
    (testing "Explicit :variant on the prompt is persisted on the chat record
              so subsequent agent/model changes can preserve it"
      (h/reset-components!)
      (let [{:keys [chat-id]} (run-prompt! {:message "Hey" :variant "max"} nil)]
        (is (= "max" (get-in (h/db) [:chats chat-id :variant])))))

    (testing "When prompt has no :variant, the agent's configured :variant is persisted"
      (h/reset-components!)
      (let [{:keys [chat-id]} (run-prompt! {:message "Hey"}
                                           {:defaultAgent "code"
                                            :agent {"code" {:variant "high"}}})]
        (is (= "high" (get-in (h/db) [:chats chat-id :variant])))))

    (testing "When neither prompt nor agent has a variant, persisted :variant is nil"
      (h/reset-components!)
      (let [{:keys [chat-id]} (run-prompt! {:message "Hey"} nil)]
        (is (nil? (get-in (h/db) [:chats chat-id :variant])))))

    (testing "When neither prompt nor agent has a variant, model's defaultVariant is persisted"
      (h/reset-components!)
      (let [{:keys [chat-id]} (run-prompt! {:message "Hey"}
                                           {:providers {"openai" {:models {"gpt-5.2" {:defaultVariant "medium"}}}}})]
        (is (= "medium" (get-in (h/db) [:chats chat-id :variant])))))))

(defn ^:private prompt-with-title!
  "Like prompt! but accepts a :sync-prompt-mock for title generation testing.
   Accepts optional :config in mocks to override default config."
  [params mocks]
  (let [api-mock (fn [{:keys [on-first-response-received on-message-received]}]
                   (on-first-response-received {:type :text :text "response"})
                   (on-message-received {:type :text :text "response"})
                   (on-message-received {:type :finish}))
        {:keys [chat-id] :as resp}
        (with-redefs [llm-api/sync-or-async-prompt! api-mock
                      llm-api/sync-prompt! (:sync-prompt-mock mocks)
                      f.tools/call-tool! (constantly nil)
                      f.tools/all-tools (constantly [])
                      f.tools/approval (constantly :allow)
                      config/await-plugins-resolved! (constantly true)]
          (h/config! (merge {:env "test" :chat {:title true}}
                            (:config mocks)))
          (swap! (h/db*) update :models
                 (fn [models]
                   (merge {"openai/gpt-5.2" {:tools true}}
                          (or models {}))))
          (f.chat/prompt params (h/db*) (h/messenger) (h/config) (h/metrics)))]
    (is (match? {:chat-id string? :status :prompting} resp))
    {:chat-id chat-id}))

(deftest sanitize-title-test
  (testing "strips markdown headers"
    (is (= "My Title" (#'f.chat/sanitize-title "## My Title")))
    (is (= "My Title" (#'f.chat/sanitize-title "### My Title"))))
  (testing "takes first non-blank line"
    (is (= "First Line" (#'f.chat/sanitize-title "\n\n  First Line\nSecond Line"))))
  (testing "skips a bare markdown header line when more content follows"
    ;; Regression: Opus in planning mode sometimes answers the title prompt with
    ;; '## Understand' as the first line. Skip bare headers when a real line follows.
    (is (= "actual body line" (#'f.chat/sanitize-title "## Understand\n\nactual body line")))
    (is (= "the real title" (#'f.chat/sanitize-title "# Explore\nthe real title"))))
  (testing "keeps a single-line header when nothing else follows"
    (is (= "Only Title" (#'f.chat/sanitize-title "## Only Title"))))
  (testing "strips control characters"
    (is (= "clean text" (#'f.chat/sanitize-title "clean\u0000 \u001ftext"))))
  (testing "collapses whitespace"
    (is (= "hello world" (#'f.chat/sanitize-title "hello   world"))))
  (testing "truncates to 40 chars"
    (is (= 40 (count (#'f.chat/sanitize-title (apply str (repeat 60 "a")))))))
  (testing "returns empty string for blank input"
    (is (= "" (#'f.chat/sanitize-title "  \n  \n  "))))
  (testing "returns nil for nil input"
    (is (nil? (#'f.chat/sanitize-title nil)))))

(deftest conversation-title-transcript-test
  (testing "renders user/assistant messages as a plain-text transcript"
    (is (= "user: hi\n\nassistant: hello"
           (#'f.chat/conversation->title-transcript
            [{:role "user" :content [{:type :text :text "hi"}]}
             {:role "assistant" :content [{:type :text :text "hello"}]}]))))
  (testing "ignores non-text parts"
    (is (= "user: hi"
           (#'f.chat/conversation->title-transcript
            [{:role "user" :content [{:type :text :text "hi"}
                                     {:type :image :url "x"}]}]))))
  (testing "skips messages with no text content"
    (is (= "user: hi"
           (#'f.chat/conversation->title-transcript
            [{:role "user" :content [{:type :text :text "hi"}]}
             {:role "assistant" :content []}]))))
  (testing "truncates very long messages"
    (let [long-text (apply str (repeat 3000 "x"))
          out (#'f.chat/conversation->title-transcript
               [{:role "user" :content [{:type :text :text long-text}]}])]
      (is (string/ends-with? out " …"))
      (is (< (count out) 2100))))
  (testing "accepts string content for robustness"
    (is (= "user: hi"
           (#'f.chat/conversation->title-transcript
            [{:role "user" :content "hi"}])))))

(deftest title-generation-test
  (testing "generates title on first message and re-generates at third with full context"
    (h/reset-components!)
    (let [sync-prompt-calls* (atom [])
          call-count* (atom 0)
          sync-mock (fn [params]
                      (swap! sync-prompt-calls* conj params)
                      {:output-text (str "Title " (swap! call-count* inc))})
          {:keys [chat-id]} (prompt-with-title! {:message "Help me debug"} {:sync-prompt-mock sync-mock})]
      ;; Message 1: should generate title
      (is (= 1 (count @sync-prompt-calls*))
          "Should call sync-prompt! on first message")
      (is (= "Title 1" (get-in (h/db) [:chats chat-id :title])))
      (is (= 1 (count (:user-messages (first @sync-prompt-calls*))))
          "First title uses only the current user message")

        ;; Message 2: should NOT re-generate title
      (h/reset-messenger!)
      (prompt-with-title! {:message "Also refactor" :chat-id chat-id} {:sync-prompt-mock sync-mock})
      (is (= 1 (count @sync-prompt-calls*))
          "Should NOT call sync-prompt! on second message")
      (is (= "Title 1" (get-in (h/db) [:chats chat-id :title])))

        ;; Inject noisy entries into history before the retitle fires, to
        ;; exercise the role/content cleanup applied to :past-messages.
      (swap! (h/db*) update-in [:chats chat-id :messages]
             (fnil into [])
             [{:role "reason" :content "internal thoughts"}
              {:role "tool_call" :content {:name "read_file"}}
              {:role "tool_call_output" :content "file contents"}
              {:role "flag" :content {:text "ui-only"}}])

        ;; Message 3: should re-generate title with full conversation context
      (h/reset-messenger!)
      (prompt-with-title! {:message "And add tests" :chat-id chat-id} {:sync-prompt-mock sync-mock})
      (is (= 2 (count @sync-prompt-calls*))
          "Should call sync-prompt! again on third message")
      (is (= "Title 2" (get-in (h/db) [:chats chat-id :title])))
      (let [retitle-call (second @sync-prompt-calls*)
            user-text (get-in (first (:user-messages retitle-call))
                              [:content 0 :text])]
          ;; On retitle, history is flattened into a single user message so the
          ;; title model can't mirror prior assistant styling (e.g. '## Understand').
        (is (= 1 (count (:user-messages retitle-call)))
            "Retitle should flatten conversation into a single user message")
        (is (nil? (:past-messages retitle-call))
            "Retitle should not replay role-structured past-messages")
        (is (string/includes? user-text "user:")
            "Flattened transcript should mark user turns")
        (is (string/includes? user-text "assistant:")
            "Flattened transcript should mark assistant turns")
        (is (string/includes? user-text "Help me debug")
            "Flattened transcript should include first user turn")
        (is (string/includes? user-text "And add tests")
            "Flattened transcript should include current user turn")
        (is (not (string/includes? user-text "internal thoughts"))
            "Reason/tool/flag entries should be filtered out of the transcript")
        (is (not (string/includes? user-text "tool_call"))
            "Reason/tool/flag entries should be filtered out of the transcript"))

        ;; Message 4: should NOT re-generate title
      (h/reset-messenger!)
      (prompt-with-title! {:message "One more thing" :chat-id chat-id} {:sync-prompt-mock sync-mock})
      (is (= 2 (count @sync-prompt-calls*))
          "Should NOT call sync-prompt! after third message")))

  (testing "retitle respects the last compact marker, dropping pre-compaction history"
    (h/reset-components!)
    (let [sync-prompt-calls* (atom [])
          call-count* (atom 0)
          sync-mock (fn [params]
                      (swap! sync-prompt-calls* conj params)
                      {:output-text (str "Title " (swap! call-count* inc))})
          {:keys [chat-id]} (prompt-with-title! {:message "Help me debug"} {:sync-prompt-mock sync-mock})]
      (h/reset-messenger!)
      (prompt-with-title! {:message "Also refactor" :chat-id chat-id} {:sync-prompt-mock sync-mock})

        ;; Insert a compact_marker so earlier history is treated as pre-compaction
        ;; and must not reach the title LLM.
      (swap! (h/db*) update-in [:chats chat-id :messages]
             (fnil conj []) {:role "compact_marker" :content {:auto? false}})

      (h/reset-messenger!)
      (prompt-with-title! {:message "And add tests" :chat-id chat-id} {:sync-prompt-mock sync-mock})
      (let [retitle-call (second @sync-prompt-calls*)
            user-text (get-in (first (:user-messages retitle-call))
                              [:content 0 :text])]
        (is (nil? (:past-messages retitle-call))
            "Past-messages should be nil on retitle (conversation is flattened into user-messages)")
        (is (not (string/includes? user-text "Help me debug"))
            "Pre-compact content must not leak into the title transcript")
        (is (not (string/includes? user-text "Also refactor"))
            "Pre-compact content must not leak into the title transcript")
        (is (string/includes? user-text "And add tests")
            "Current user message must be in the title transcript"))))

  (testing "manual rename suppresses automatic re-titling"
    (h/reset-components!)
    (let [sync-prompt-calls* (atom [])
          sync-mock (fn [params]
                      (swap! sync-prompt-calls* conj params)
                      {:output-text "Auto Title"})
          {:keys [chat-id]} (prompt-with-title! {:message "Help me debug"} {:sync-prompt-mock sync-mock})]
      ;; Message 1: generates initial title
      (is (= 1 (count @sync-prompt-calls*)))

        ;; Manual rename
      (f.chat/update-chat {:chat-id chat-id :title "My Custom Title"} (h/db*) (h/messenger) (h/metrics))
      (is (= "My Custom Title" (get-in (h/db) [:chats chat-id :title])))
      (is (true? (get-in (h/db) [:chats chat-id :title-custom?])))

        ;; Messages 2 and 3: should NOT re-generate because of manual rename
      (h/reset-messenger!)
      (prompt-with-title! {:message "Second msg" :chat-id chat-id} {:sync-prompt-mock sync-mock})
      (h/reset-messenger!)
      (prompt-with-title! {:message "Third msg" :chat-id chat-id} {:sync-prompt-mock sync-mock})
      (is (= 1 (count @sync-prompt-calls*))
          "Should NOT re-title after manual rename")
      (is (= "My Custom Title" (get-in (h/db) [:chats chat-id :title])))))

  (testing "title disabled in config skips all generation"
    (h/reset-components!)
    (let [sync-prompt-calls* (atom [])
          sync-mock (fn [params]
                      (swap! sync-prompt-calls* conj params)
                      {:output-text "Should not appear"})
          disabled-mocks {:sync-prompt-mock sync-mock :config {:chat {:title false}}}
          {:keys [chat-id]} (prompt-with-title! {:message "Hello"} disabled-mocks)]
      (prompt-with-title! {:message "Second" :chat-id chat-id} disabled-mocks)
      (prompt-with-title! {:message "Third" :chat-id chat-id} disabled-mocks)
      (is (zero? (count @sync-prompt-calls*))
          "Should never call sync-prompt! when title is disabled")
      (is (nil? (get-in (h/db) [:chats chat-id :title]))))))

(deftest update-chat-trust-test
  (testing "update-chat stores trust in chat state"
    (h/reset-components!)
    (let [chat-id "trust-chat"]
      (swap! (h/db*) assoc-in [:chats chat-id] {:id chat-id})
      (f.chat/update-chat {:chat-id chat-id :trust true} (h/db*) (h/messenger) (h/metrics))
      (is (true? (get-in (h/db) [:chats chat-id :trust])))))

  (testing "update-chat sets trust to false"
    (h/reset-components!)
    (let [chat-id "trust-chat"]
      (swap! (h/db*) assoc-in [:chats chat-id] {:id chat-id :trust true})
      (f.chat/update-chat {:chat-id chat-id :trust false} (h/db*) (h/messenger) (h/metrics))
      (is (false? (get-in (h/db) [:chats chat-id :trust])))))

  (testing "update-chat without trust does not change existing trust"
    (h/reset-components!)
    (let [chat-id "trust-chat"]
      (swap! (h/db*) assoc-in [:chats chat-id] {:id chat-id :trust true})
      (f.chat/update-chat {:chat-id chat-id :title "New Title"} (h/db*) (h/messenger) (h/metrics))
      (is (true? (get-in (h/db) [:chats chat-id :trust])))
      (is (= "New Title" (get-in (h/db) [:chats chat-id :title])))))

  (testing "update-chat with trust and title updates both"
    (h/reset-components!)
    (let [chat-id "trust-chat"]
      (swap! (h/db*) assoc-in [:chats chat-id] {:id chat-id})
      (f.chat/update-chat {:chat-id chat-id :title "My Chat" :trust true} (h/db*) (h/messenger) (h/metrics))
      (is (true? (get-in (h/db) [:chats chat-id :trust])))
      (is (= "My Chat" (get-in (h/db) [:chats chat-id :title]))))))

(deftest prompt-default-trust-test
  (let [finish-mock (fn [{:keys [on-first-response-received on-message-received]}]
                      (on-first-response-received {:type :text :text "ok"})
                      (on-message-received {:type :text :text "ok"})
                      (on-message-received {:type :finish}))]
    (testing "new chat with chat.defaultTrust true seeds trust and emits per-chat select-trust"
      (h/reset-components!)
      (h/config! {:chat {:defaultTrust true}})
      (let [{:keys [chat-id]} (prompt! {:message "Hi" :chat-id "default-trust-chat"}
                                       {:all-tools-mock (constantly [])
                                        :api-mock finish-mock})]
        (is (true? (get-in (h/db) [:chats chat-id :trust])))
        (is (match? {:config-updated (m/embeds [{:chat {:select-trust true}
                                                 :chat-id "default-trust-chat"}])}
                    (h/messages)))))

    (testing "explicit client trust wins over chat.defaultTrust"
      (h/reset-components!)
      (h/config! {:chat {:defaultTrust true}})
      (let [{:keys [chat-id]} (prompt! {:message "Hi" :chat-id "explicit-trust-chat" :trust false}
                                       {:all-tools-mock (constantly [])
                                        :api-mock finish-mock})]
        (is (false? (get-in (h/db) [:chats chat-id :trust])))))

    (testing "chat.defaultTrust false (default) does not seed trust"
      (h/reset-components!)
      (let [{:keys [chat-id]} (prompt! {:message "Hi" :chat-id "no-default-trust-chat"}
                                       {:all-tools-mock (constantly [])
                                        :api-mock finish-mock})]
        (is (nil? (get-in (h/db) [:chats chat-id :trust])))))))

(deftest context-overflow-auto-compact-guard-test
  (testing "context overflow after auto-compact reports error instead of looping"
    (h/reset-components!)
    (let [chat-id "overflow-compact-chat"
          api-call-count* (atom 0)
          auto-compact-count* (atom 0)]
      ;; Seed prior conversation so there is something to compact; with empty
      ;; history the overflow is surfaced immediately, see
      ;; context-overflow-first-turn-surfaces-error-test.
      (swap! (h/db*) assoc-in [:chats chat-id]
             {:id chat-id
              :messages [{:role "user" :content [{:type :text :text "earlier question"}]}
                         {:role "assistant" :content [{:type :text :text "earlier answer"}]}]})
      (with-redefs-fn
        {#'f.chat/trigger-auto-compact!
         (fn [chat-ctx _all-tools user-messages]
           (swap! auto-compact-count* inc)
           (#'f.chat/prompt-messages!
            user-messages
            :auto-compact
            (assoc chat-ctx :auto-compacted? true)))}
        (fn []
          (let [{:keys [_chat-id]}
                (prompt!
                 {:message "Do something" :chat-id chat-id}
                 {:all-tools-mock (constantly [])
                  :api-mock
                  (fn [{:keys [on-error]}]
                    (swap! api-call-count* inc)
                    (on-error {:error/type :context-overflow
                               :message "token limit exceeded"}))})]
            (is (= 1 @auto-compact-count*)
                "auto-compact should trigger exactly once, not loop")
            (is (= 2 @api-call-count*)
                "LLM should be called exactly twice: initial prompt and resume after compact")
            (is (match?
                 {:chat-content-received
                  (m/embeds [{:role :system
                              :content {:type :text
                                        :text "Context window exceeded. Auto-compacting conversation..."}}
                             {:role :system
                              :content {:type :text
                                        :text #(string/includes? % "Context window exceeded: this request is larger")}}])}
                 (h/messages))
                "Should show auto-compact attempt and then the final clear error")))))))

(deftest context-overflow-first-turn-surfaces-error-test
  (testing "context overflow with no history to compact surfaces a clear error without auto-compacting (#491)"
    (h/reset-components!)
    (let [api-call-count* (atom 0)
          auto-compact-count* (atom 0)]
      (with-redefs-fn
        {#'f.chat/trigger-auto-compact! (fn [& _] (swap! auto-compact-count* inc))}
        (fn []
          (let [{:keys [chat-id]}
                (prompt!
                 {:message "Do something"}
                 {:all-tools-mock (constantly [])
                  :api-mock
                  (fn [{:keys [on-error]}]
                    (swap! api-call-count* inc)
                    (on-error {:error/type :context-overflow
                               :message "prompt is too long"}))})]
            (is (zero? @auto-compact-count*)
                "auto-compact must NOT trigger when there is no conversation to compact")
            (is (= 1 @api-call-count*)
                "LLM should be called exactly once; no futile compact retry")
            (is (match?
                 {:chat-content-received
                  (m/embeds [{:role :system
                              :content {:type :text
                                        :text #(string/includes? % "Context window exceeded: this request is larger")}}])}
                 (h/messages))
                "Should surface a clear context-overflow error to the user")
            (is (= :idle (get-in (h/db) [:chats chat-id :status]))
                "Chat should finish in idle state")))))))

(def ^:private xai-invalid-image-body
  "Real OpenRouter/xAI 400 body for an image below the minimum pixels."
  "{\"error\":{\"message\":\"Provider returned error\",\"code\":400,\"metadata\":{\"raw\":\"{\\\"code\\\":\\\"invalid-argument\\\",\\\"error\\\":\\\"Image has 100 total pixels (10x10), which is below the minimum of 512 pixels.\\\"}\",\"provider_name\":\"xAI\"}}}")

(deftest invalid-image-recovery-test
  (testing "provider rejecting a tool-result image strips images from history and retries once"
    (h/reset-components!)
    (let [chat-id "invalid-image-chat"
          api-call-count* (atom 0)
          second-call-args* (atom nil)]
      ;; Seed a poisoned history: an MCP tool returned a tiny image (e.g.
      ;; Backseat Driver's eval-to-image) that xAI rejects on every replay.
      (swap! (h/db*) assoc-in [:chats chat-id]
             {:id chat-id
              :messages [{:role "user" :content [{:type :text :text "evaluate this"}]}
                         {:role "tool_call" :content {:id "tc-1" :name "eval_to_image" :arguments {}}}
                         {:role "tool_call_output" :content {:id "tc-1"
                                                             :name "eval_to_image"
                                                             :output {:error false
                                                                      :contents [{:type :text :text "rendered:"}
                                                                                 {:type :image
                                                                                  :media-type "image/png"
                                                                                  :base64 "abc123"}]}}}]})
      (let [_ (prompt!
               {:message "continue please" :chat-id chat-id}
               {:all-tools-mock (constantly [])
                :api-mock
                (fn [{:keys [on-error on-first-response-received on-message-received
                             past-messages user-messages]}]
                  (case (swap! api-call-count* inc)
                    1 (do (on-first-response-received)
                          (on-error {:status 400
                                     :body xai-invalid-image-body
                                     :message (str "LLM response status: 400 body: " xai-invalid-image-body)}))
                    2 (do (reset! second-call-args* {:past-messages past-messages
                                                     :user-messages user-messages})
                          (on-first-response-received)
                          (on-message-received {:type :text :text "Recovered!"})
                          (on-message-received {:type :finish}))))})]
        (is (= 2 @api-call-count*)
            "LLM should be called exactly twice: failing request and image-stripped retry")
        (let [{:keys [past-messages user-messages]} @second-call-args*]
          (is (not (string/includes? (pr-str past-messages) ":type :image"))
              "retry request must not replay any image content")
          (is (string/includes? (pr-str past-messages) "[image removed: rejected by the LLM provider]")
              "rejected image is replaced with a placeholder at send time")
          (is (match?
               [{:role "user"
                 :content [{:type :text
                            :text #(string/includes? % "rejected by the LLM provider")}]}]
               user-messages)
              "retry nudges the model to continue since the original user message is already in history"))
        (is (= (get-in (h/db) [:chats chat-id :model])
               (get-in (h/db) [:chats chat-id :images-rejected-by-model]))
            "chat is flagged so subsequent requests to this model omit images")
        (is (match?
             {chat-id {:messages (m/embeds [{:role "tool_call_output"
                                             :content {:output {:contents [{:type :text :text "rendered:"}
                                                                           {:type :image
                                                                            :media-type "image/png"
                                                                            :base64 "abc123"}]}}}
                                            {:role "assistant"
                                             :content [{:type :text :text "Recovered!"}]}])}}
             (:chats (h/db)))
            "history keeps the original image (only outgoing requests are stripped) plus the recovered answer")
        (is (match?
             {:chat-content-received
              (m/embeds [{:role :system
                          :content {:type :text
                                    :text "The LLM provider rejected an image in the conversation. Retrying without images (omitted while this model is selected)..."}}])}
             (h/messages)))
        (is (= :idle (get-in (h/db) [:chats chat-id :status])))))))

(deftest invalid-image-flag-strips-new-images-test
  (testing "once flagged, later prompts strip images (e.g. fresh tool results) at send time without errors"
    (h/reset-components!)
    (let [chat-id "invalid-image-flagged-chat"
          request-args* (atom nil)]
      (swap! (h/db*) assoc-in [:chats chat-id]
             {:id chat-id
              :images-rejected-by-model "openai/gpt-5.2"
              :messages [{:role "user" :content [{:type :text :text "evaluate this"}]}
                         {:role "tool_call_output" :content {:id "tc-1"
                                                             :output {:error false
                                                                      :contents [{:type :image
                                                                                  :media-type "image/png"
                                                                                  :base64 "abc123"}]}}}]})
      (prompt!
       {:message "continue please" :chat-id chat-id :model "openai/gpt-5.2"}
       {:all-tools-mock (constantly [])
        :api-mock
        (fn [{:keys [on-first-response-received on-message-received past-messages]}]
          (reset! request-args* {:past-messages past-messages})
          (on-first-response-received)
          (on-message-received {:type :text :text "Done"})
          (on-message-received {:type :finish}))})
      (is (not (string/includes? (pr-str (:past-messages @request-args*)) ":type :image"))
          "no image content is sent while the flag matches the selected model")
      (is (match?
           {chat-id {:messages (m/embeds [{:role "tool_call_output"
                                           :content {:output {:contents [{:type :image
                                                                          :media-type "image/png"
                                                                          :base64 "abc123"}]}}}])}}
           (:chats (h/db)))
          "history itself keeps the image"))))

(deftest invalid-image-retry-does-not-loop-test
  (testing "when the image-stripped retry still fails, the error surfaces instead of looping"
    (h/reset-components!)
    (let [chat-id "invalid-image-loop-chat"
          api-call-count* (atom 0)]
      (swap! (h/db*) assoc-in [:chats chat-id]
             {:id chat-id
              :messages [{:role "user" :content [{:type :text :text "evaluate this"}]}
                         {:role "tool_call_output" :content {:id "tc-1"
                                                             :output {:error false
                                                                      :contents [{:type :image
                                                                                  :media-type "image/png"
                                                                                  :base64 "abc123"}]}}}]})
      (let [_ (prompt!
               {:message "continue please" :chat-id chat-id}
               {:all-tools-mock (constantly [])
                :api-mock
                (fn [{:keys [on-error]}]
                  (swap! api-call-count* inc)
                  (on-error {:status 400
                             :body xai-invalid-image-body
                             :message (str "LLM response status: 400 body: " xai-invalid-image-body)}))})]
        (is (= 2 @api-call-count*)
            "LLM should be called exactly twice: initial request and single retry")
        (is (match?
             {:chat-content-received
              (m/embeds [{:role :system
                          :content {:type :text
                                    :text #(string/includes? % "below the minimum of 512 pixels")}}])}
             (h/messages))
            "the provider error surfaces after the failed retry")
        (is (= :idle (get-in (h/db) [:chats chat-id :status])))))))

(deftest strip-messages-images-test
  (let [strip-messages-images #'f.chat/strip-messages-images]
    (testing "replaces user-attached and tool-result images with placeholders"
      (is (= {:stripped 2
              :messages [{:role "user"
                          :content [{:type :text :text "look at this"}
                                    {:type :text :text "[image removed: rejected by the LLM provider]"}]}
                         {:role "tool_call"
                          :content {:id "t1"}}
                         {:role "tool_call_output"
                          :content {:id "t1"
                                    :output {:contents [{:type :text :text "result:"}
                                                        {:type :text :text "[image removed: rejected by the LLM provider]"}]}}}]}
             (strip-messages-images
              [{:role "user"
                :content [{:type :text :text "look at this"}
                          {:type :image :media-type "image/png" :base64 "abc"}]}
               {:role "tool_call"
                :content {:id "t1"}}
               {:role "tool_call_output"
                :content {:id "t1"
                          :output {:contents [{:type :text :text "result:"}
                                              {:type :image :media-type "image/png" :base64 "abc"}]}}}]))))
    (testing "no-op for messages without images"
      (is (= {:stripped 0
              :messages [{:role "user" :content "string content"}
                         {:role "assistant" :content [{:type :text :text "hi"}]}
                         {:role "tool_call_output" :content {:id "t2" :output {:error true}}}]}
             (strip-messages-images
              [{:role "user" :content "string content"}
               {:role "assistant" :content [{:type :text :text "hi"}]}
               {:role "tool_call_output" :content {:id "t2" :output {:error true}}}]))))))

(deftest limit-reached-clears-compact-flags-test
  (testing ":limit-reached handler clears stale compacting?/auto-compacting? flags so /compact can be retried"
    (h/reset-components!)
    (let [{:keys [chat-id]}
          (prompt!
           {:message "Do something"}
           {:all-tools-mock (constantly [])
            :api-mock
            (fn [{:keys [on-message-received]}]
              (let [chat-id (-> @(h/db*) :chats keys first)]
                ;; Simulate /compact (and a prior auto-compact attempt) being in
                ;; flight when the LLM aborts with stop_reason "max_tokens" but
                ;; the heuristic considers it a genuine output cap. Without the
                ;; fix the chat stays stuck with :compacting? true forever.
                (swap! (h/db*) update-in [:chats chat-id]
                       assoc :compacting? true :auto-compacting? true)
                (on-message-received
                 {:type :limit-reached
                  :tokens {:input_tokens 200000 :output_tokens 32000}})))})]
      (is (nil? (get-in (h/db) [:chats chat-id :compacting?]))
          "compacting? must be cleared so the user can retry /compact")
      (is (nil? (get-in (h/db) [:chats chat-id :auto-compacting?]))
          "auto-compacting? must also be cleared")
      (is (match?
           {:chat-content-received
            (m/embeds [{:role :system
                        :content {:type :text
                                  :text #(string/includes? % "API limit reached. Tokens:")}}])}
           (h/messages))
          "Should still surface the API limit reached system message"))))

(deftest auto-compact-hook-block-test
  (testing "preCompact continue:false stops the turn with the prefixed reason and does not resume"
    (h/reset-components!)
    (let [prompted* (atom nil)]
      (with-redefs [lifecycle/run-pre-compact-hooks! (constantly {:blocked? true
                                                                  :reason "auto blocked"
                                                                  :hook-name "blocker"
                                                                  :stop-turn? true})
                    f.chat/prompt-messages! (fn [user-messages prompt-type chat-ctx]
                                              (reset! prompted* {:user-messages user-messages
                                                                 :prompt-type prompt-type
                                                                 :chat-ctx chat-ctx}))]
        (#'f.chat/trigger-auto-compact!
         {:db* (h/db*)
          :config (h/config)
          :chat-id "chat-1"
          :agent "code"
          :messenger (h/messenger)
          :metrics (h/metrics)
          :parent-chat-id "parent-1"}
         []
         [{:role "user" :content [{:type :text :text "keep going"}]}]))
      (is (some #(= {:chat-id "chat-1"
                     :parent-chat-id "parent-1"
                     :role :system
                     :content {:type :text :text "Turn stopped by hook 'blocker': auto blocked"}}
                    %)
                (:chat-content-received (h/messages))))
      (is (nil? @prompted*)
          "stop-turn prevents resuming the original task")))

  (testing "preCompact exit 2 blocks compaction without stop-turn and resumes the original task"
    (h/reset-components!)
    (let [prompted* (atom nil)]
      (with-redefs [lifecycle/run-pre-compact-hooks! (constantly {:blocked? true
                                                                  :reason nil
                                                                  :hook-name "guard"
                                                                  :stop-turn? false})
                    f.chat/prompt-messages! (fn [user-messages prompt-type chat-ctx]
                                              (reset! prompted* {:user-messages user-messages
                                                                 :prompt-type prompt-type
                                                                 :chat-ctx chat-ctx}))]
        (#'f.chat/trigger-auto-compact!
         {:db* (h/db*)
          :config (h/config)
          :chat-id "chat-1"
          :agent "code"
          :messenger (h/messenger)
          :parent-chat-id "parent-1"}
         []
         [{:role "user" :content [{:type :text :text "keep going"}]}]))
      (is (match? {:chat-content-received
                   [{:chat-id "chat-1"
                     :parent-chat-id "parent-1"
                     :role :system
                     :content {:type :text :text "Compaction blocked by hook 'guard'."}}]}
                  (h/messages)))
      (is (match? {:prompt-type :auto-compact-blocked
                   :chat-ctx {:auto-compacted? true}}
                  @prompted*)))))


(defn ^:private make-tool-output-msg [id text]
  {:role "tool_call_output"
   :content {:id id
             :name "read_file"
             :output {:error false
                      :contents [{:type :text :text text}]}}})

(defn ^:private make-tool-call-msg [id]
  {:role "tool_call"
   :content {:id id :full-name "eca__read_file" :arguments {"path" "/foo"}}})

(defn ^:private make-server-tool-result-msg [id text]
  {:role "server_tool_result"
   :content {:tool-use-id id
             :raw-content [{:type "text" :text text}]}})

(deftest prune-tool-results!-test
  (testing "clears old tool results beyond protect budget"
    (let [large-text (apply str (repeat 100000 "x"))
          small-text (apply str (repeat 20000 "y"))
          messages [{:role "user" :content [{:type :text :text "hello"}]}
                    (make-tool-call-msg "1")
                    (make-tool-output-msg "1" large-text)
                    {:role "assistant" :content [{:type :text :text "found it"}]}
                    (make-tool-call-msg "2")
                    (make-tool-output-msg "2" small-text)
                    {:role "assistant" :content [{:type :text :text "done"}]}]
          db* (atom {:chats {"c1" {:messages messages}}})
          freed (#'f.chat/prune-tool-results! db* "c1" {:protect-budget 3000})]
      (is (pos? freed))
      (let [pruned (get-in @db* [:chats "c1" :messages])]
        (is (= "[content cleared to reduce context size]"
               (get-in (nth pruned 2) [:content :output :contents 0 :text])))
        (is (= small-text
               (get-in (nth pruned 5) [:content :output :contents 0 :text])))
        (is (= "hello" (get-in (nth pruned 0) [:content 0 :text])))
        (is (= "found it" (get-in (nth pruned 3) [:content 0 :text]))))))

  (testing "returns 0 and does not modify db when nothing to prune"
    (let [messages [{:role "user" :content [{:type :text :text "hello"}]}
                    (make-tool-call-msg "1")
                    (make-tool-output-msg "1" "short")
                    {:role "assistant" :content [{:type :text :text "ok"}]}]
          db* (atom {:chats {"c1" {:messages messages}}})
          freed (#'f.chat/prune-tool-results! db* "c1" {:protect-budget 40000})]
      (is (zero? freed))
      (is (= messages (get-in @db* [:chats "c1" :messages])))))

  (testing "clears server_tool_result messages beyond budget"
    (let [large-text (apply str (repeat 100000 "z"))
          messages [{:role "user" :content [{:type :text :text "hello"}]}
                    (make-server-tool-result-msg "s1" large-text)
                    {:role "assistant" :content [{:type :text :text "searched"}]}]
          db* (atom {:chats {"c1" {:messages messages}}})
          freed (#'f.chat/prune-tool-results! db* "c1" {:protect-budget 0})]
      (is (pos? freed))
      (is (= "[content cleared to reduce context size]"
             (get-in (nth (get-in @db* [:chats "c1" :messages]) 1) [:content :raw-content 0 :text])))))

  (testing "handles empty message history"
    (let [db* (atom {:chats {"c1" {:messages []}}})]
      (is (zero? (#'f.chat/prune-tool-results! db* "c1" {})))))

  (testing "stops at compact_marker boundary, does not prune pre-compaction messages"
    (let [large-text (apply str (repeat 100000 "x"))
          messages [(make-tool-call-msg "old")
                    (make-tool-output-msg "old" large-text)
                    {:role "compact_marker" :content {:auto? false}}
                    {:role "user" :content [{:type :text :text "summary"}]}
                    (make-tool-call-msg "new")
                    (make-tool-output-msg "new" large-text)]
          db* (atom {:chats {"c1" {:messages messages}}})
          freed (#'f.chat/prune-tool-results! db* "c1" {:protect-budget 0})]
      (is (pos? freed))
      (let [pruned (get-in @db* [:chats "c1" :messages])]
        ;; Pre-compaction tool output should be untouched
        (is (= large-text
               (get-in (nth pruned 1) [:content :output :contents 0 :text])))
        ;; Post-compaction tool output should be cleared
        (is (= "[content cleared to reduce context size]"
               (get-in (nth pruned 5) [:content :output :contents 0 :text])))))))

(deftest contexts-in-prompt-test
  (testing "When prompt contains @file we add a user message"
    (h/reset-components!)
    (with-redefs [fs/readable? (constantly true)
                  llm-api/refine-file-context (constantly "Mocked file content")]
      (let [{:keys [chat-id]}
            (prompt!
             {:message "Check @/path/to/file please"}
             {:all-tools-mock (constantly [])
              :api-mock
              (fn [{:keys [on-first-response-received
                           on-message-received]}]
                (on-first-response-received {:type :text :text "On it..."})
                (on-message-received {:type :text :text "On it..."})
                (on-message-received {:type :finish}))})]
        (is (match?
             {chat-id {:id chat-id
                       :messages [{:role "user"
                                   :content [{:type :text :text "Check @/path/to/file please"}
                                             {:type :text :text (m/pred #(string/includes? % "<file path"))}]}
                                  {:role "assistant" :content [{:type :text :text "On it..."}]}]}}
             (:chats (h/db)))))))
  (testing "When prompt contains @missing-file we do not add context noise"
    (h/reset-components!)
    (let [missing-file "definitely-does-not-exist-eca-test-ctx.md"
          msg (str "Check @" missing-file " please")
          {:keys [chat-id]}
          (prompt!
           {:message msg}
           {:all-tools-mock (constantly [])
            :api-mock
            (fn [{:keys [on-first-response-received
                         on-message-received]}]
              (on-first-response-received {:type :text :text "On it..."})
              (on-message-received {:type :text :text "On it..."})
              (on-message-received {:type :finish}))})]
      (is (match?
           {chat-id {:id chat-id
                     :messages [{:role "user" :content [{:type :text :text msg}]}
                                {:role "assistant" :content [{:type :text :text "On it..."}]}]}}
           (:chats (h/db))))
      (is (match?
           {:chat-content-received
            [{:chat-id chat-id
              :content {:type :text :text (str msg "\n")}
              :role :user}
             {:chat-id chat-id
              :content {:type :progress :state :running :text "Waiting model"}
              :role :system}
             {:chat-id chat-id
              :content {:type :progress :state :running :text "Generating"}
              :role :system}
             {:chat-id chat-id
              :content {:type :text :text "On it..."}
              :role :assistant}
             {:chat-id chat-id
              :content {:state :finished :type :progress}
              :role :system}]}
           (h/messages))))))

(deftest cursor-delivered-in-user-message-test
  (testing "cursor is appended to the user message and only re-sent when it changes (#464)"
    (h/reset-components!)
    (let [user-msgs* (atom [])
          api-mock (fn [{:keys [user-messages on-first-response-received on-message-received]}]
                     (swap! user-msgs* conj user-messages)
                     (on-first-response-received {:type :text :text "ok"})
                     (on-message-received {:type :text :text "ok"})
                     (on-message-received {:type :finish}))
          mocks {:all-tools-mock (constantly []) :api-mock api-mock}
          cursor-at (fn [line] {:type "cursor" :path "foo.clj"
                                :position {:start {:line line :character 0}
                                           :end {:line line :character 3}}})
          turn-text (fn [call-idx]
                      (->> (get @user-msgs* call-idx) first :content (keep :text) (string/join "\n")))
          {:keys [chat-id]} (prompt! {:message "one" :contexts [(cursor-at 10)]} mocks)]
      (testing "first turn includes the cursor block"
        (is (string/includes? (turn-text 0) "<cursor"))
        (is (string/includes? (turn-text 0) "10:0")))
      (h/reset-messenger!)
      (prompt! {:message "two" :chat-id chat-id :contexts [(cursor-at 10)]} mocks)
      (testing "unchanged cursor is not re-sent"
        (is (not (string/includes? (turn-text 1) "<cursor"))))
      (h/reset-messenger!)
      (prompt! {:message "three" :chat-id chat-id :contexts [(cursor-at 25)]} mocks)
      (testing "changed cursor is re-sent"
        (is (string/includes? (turn-text 2) "<cursor"))
        (is (string/includes? (turn-text 2) "25:0"))))))

(deftest prompt-show-does-not-consume-cursor-test
  (testing "/prompt-show displays editor state without updating last-editor-state"
    (h/reset-components!)
    (let [user-msgs* (atom [])
          cursor {:type "cursor" :path "foo.clj"
                  :position {:start {:line 10 :character 0}
                             :end {:line 10 :character 3}}}
          api-mock (fn [{:keys [user-messages on-first-response-received on-message-received]}]
                     (swap! user-msgs* conj user-messages)
                     (on-first-response-received {:type :text :text "ok"})
                     (on-message-received {:type :text :text "ok"})
                     (on-message-received {:type :finish}))
          mocks {:all-tools-mock (constantly []) :api-mock api-mock}
          {:keys [chat-id]} (prompt! {:message "/prompt-show" :contexts [cursor]} mocks)]
      (is (nil? (get-in (h/db) [:chats chat-id :last-editor-state])))
      (is (some #(and (= "system" (:role %))
                      (string/includes? (get-in % [:content :text] "") "<editor-state"))
                (:chat-content-received (h/messages))))
      (h/reset-messenger!)
      (prompt! {:message "real prompt" :chat-id chat-id :contexts [cursor]} mocks)
      (let [turn-text (->> @user-msgs* first first :content (keep :text) (string/join "\n"))]
        (is (string/includes? turn-text "<editor-state"))
        (is (string/includes? turn-text "<cursor"))))))

(deftest mcp-prompt-does-not-consume-cursor-test
  (testing "MCP prompts do not mark editor state as sent because they use MCP-provided messages"
    (h/reset-components!)
    (let [user-msgs* (atom [])
          cursor {:type "cursor" :path "foo.clj"
                  :position {:start {:line 10 :character 0}
                             :end {:line 10 :character 3}}}
          api-mock (fn [{:keys [user-messages on-first-response-received on-message-received]}]
                     (swap! user-msgs* conj user-messages)
                     (on-first-response-received {:type :text :text "ok"})
                     (on-message-received {:type :text :text "ok"})
                     (on-message-received {:type :finish}))
          mocks {:all-tools-mock (constantly []) :api-mock api-mock}]
      (with-redefs [f.mcp/all-prompts (constantly [{:name "my-prompt"
                                                    :server "test-server"
                                                    :arguments []}])
                    f.prompt/get-prompt! (fn [_name _arguments _db]
                                           {:messages [{:role "user"
                                                       :content [{:type :text
                                                                  :text "MCP prompt body"}]}]})]
        (let [{:keys [chat-id]} (prompt! {:message "/test-server:my-prompt" :contexts [cursor]} mocks)]
          (is (nil? (get-in (h/db) [:chats chat-id :last-editor-state])))
          (is (not (string/includes? (->> @user-msgs* first first :content (keep :text) (string/join "\n"))
                                     "<cursor")))
          (h/reset-messenger!)
          (prompt! {:message "real prompt" :chat-id chat-id :contexts [cursor]} mocks)
          (let [turn-text (->> @user-msgs* second first :content (keep :text) (string/join "\n"))]
            (is (string/includes? turn-text "<editor-state"))
            (is (string/includes? turn-text "<cursor"))))))))

(deftest basic-tool-calling-prompt-test
  (testing "Asking to list directories, LLM will check for allowed directories and then list files"
    (h/reset-components!)
    (let [{:keys [chat-id]}
          (prompt!
           {:message "List the files you are allowed to see"}
           {:all-tools-mock (constantly [{:name "list_allowed_directories" :full-name "eca__list_allowed_directories" :server {:name "eca"}}])
            :api-mock
            (fn [{:keys [on-first-response-received
                         on-message-received
                         on-prepare-tool-call
                         on-tools-called]}]
              (on-first-response-received {:type :text :text "Ok,"})
              (on-message-received {:type :text :text "Ok,"})
              (on-message-received {:type :text :text " working on it"})
              (on-prepare-tool-call {:id "call-1" :full-name "eca__list_allowed_directories" :arguments-text ""})
              (on-tools-called [{:id "call-1" :full-name "eca__list_allowed_directories" :arguments {}}])
              (on-message-received {:type :text :text "I can see: \n"})
              (on-message-received {:type :text :text "/foo/bar"})
              (on-message-received {:type :finish}))
            :call-tool-mock
            (constantly {:error false
                         :contents [{:type :text :text "Allowed directories: /foo/bar"}]})})]
      (is (match?
           {chat-id {:id chat-id
                     :messages [{:role "user" :content [{:type :text :text "List the files you are allowed to see"}]}
                                {:role "assistant" :content [{:type :text :text "Ok, working on it"}]}
                                {:role "tool_call" :content {:id "call-1" :full-name "eca__list_allowed_directories" :arguments {}}}
                                {:role "tool_call_output" :content {:id "call-1" :full-name "eca__list_allowed_directories" :arguments {}
                                                                    :output {:error false
                                                                             :contents [{:text "Allowed directories: /foo/bar"
                                                                                         :type :text}]}}}
                                {:role "assistant" :content [{:type :text :text "I can see: \n/foo/bar"}]}]}}
           (:chats (h/db))))
      ;; Note: We use m/in-any-order because there's a race between progress messages
      ;; ("Calling tool", "Generating") and tool state messages (toolCallRunning,
      ;; toolCalled) - their relative order in the middle section is non-deterministic.
      (is (match?
           {:chat-content-received
            (m/in-any-order [{:role :user :content {:type :text :text "List the files you are allowed to see\n"}}
                             {:role :system :content {:type :progress :state :running :text "Waiting model"}}
                             {:role :system :content {:type :progress :state :running :text "Generating"}}
                             {:role :assistant :content {:type :text :text "Ok,"}}
                             {:role :assistant :content {:type :text :text " working on it"}}
                             {:role :assistant :content {:type :toolCallPrepare :id "call-1" :name "list_allowed_directories" :arguments-text ""}}
                             {:role :assistant :content {:type :toolCallRun :id "call-1" :name "list_allowed_directories" :arguments {} :manual-approval false}}
                             {:role :assistant :content {:type :toolCallRunning :id "call-1" :name "list_allowed_directories" :arguments {}}}
                             {:role :system :content {:type :progress :state :running :text "Calling tool"}}
                             {:role :assistant :content {:type :toolCalled :id "call-1" :name "list_allowed_directories" :arguments {} :total-time-ms number? :outputs [{:text "Allowed directories: /foo/bar" :type :text}]}}
                             {:role :system :content {:type :progress :state :running :text "Generating"}}
                             {:role :assistant :content {:type :text :text "I can see: \n"}}
                             {:role :assistant :content {:type :text :text "/foo/bar"}}
                             {:role :system :content {:state :finished :type :progress}}])}
           (h/messages))))))

(deftest inactive-compact-tool-error-reaches-provider-continuation-test
  (testing "inactive compact_chat result is included in the next provider request"
    (h/reset-components!)
    (let [continuation* (atom nil)
          original-all-tools f.tools/all-tools
          original-call-tool! f.tools/call-tool!
          expected-error "Chat compaction is not active for this request. This tool is available only while chat compaction is in progress. To compact manually, the user must use the `/compact` command; compaction may also start automatically when context usage reaches the configured threshold."
          {:keys [chat-id]}
          (prompt!
           {:message "Continue normally"}
           {:all-tools-mock original-all-tools
            :call-tool-mock original-call-tool!
            :api-mock
            (fn [{:keys [on-first-response-received on-message-received
                         on-prepare-tool-call on-tools-called]}]
              (on-first-response-received)
              (on-prepare-tool-call {:id "compact-call-1"
                                     :full-name "eca__compact_chat"
                                     :arguments-text "{\"summary\":\"Must not be stored\"}"})
              (reset! continuation*
                      (on-tools-called [{:id "compact-call-1"
                                         :full-name "eca__compact_chat"
                                         :arguments {"summary" "Must not be stored"}}]))
              (on-message-received {:type :text :text "Understood"})
              (on-message-received {:type :finish}))})
          tool-output (->> (:new-messages @continuation*)
                           (filter #(= "tool_call_output" (:role %)))
                           last
                           :content)]
      (is (match? {:id "compact-call-1"
                   :full-name "eca__compact_chat"
                   :error true
                   :output {:error true
                            :contents [{:type :text :text expected-error}]}}
                  tool-output)
          "the provider continuation must contain the exact inactive-compaction error for the LLM")
      (is (nil? (get-in (h/db) [:chats chat-id :last-summary])))
      (is (nil? (get-in (h/db) [:chats chat-id :compact-done?])))
      (is (not (true? (get-in (h/db) [:chats chat-id :compacting?])))
          "the accidental call must not activate or complete compaction"))))

(def ^:private steered-compact-tools
  [{:name "read_file" :full-name "eca__read_file" :server {:name "eca"}}
   {:name "compact_chat" :full-name "eca__compact_chat" :server {:name "eca"}}])

(defn ^:private steered-compact-call-tool-mock
  "Runs the real compact_chat handler so the compaction flags/summary behave as
   in production; any other tool returns a canned result."
  [full-name arguments chat-id & _]
  (if (= "eca__compact_chat" full-name)
    ((get-in f.tools.chat/definitions ["compact_chat" :handler]) arguments {:db* (h/db*) :chat-id chat-id})
    {:error false :contents [{:type :text :text "file contents"}]}))

(deftest steered-compact-test
  (testing "a /compact steered while the turn runs compacts at the tool-call boundary with its instructions, then resumes the task (#600)"
    (h/reset-components!)
    ;; The test config has no resolved prompt templates; use a minimal compact one.
    (h/config! {:prompts {:compact "Summarize the chat. {{additionalUserInput}}"}})
    (let [chat-id "steered-compact-chat"
          api-calls* (atom [])
          pre-compact-hook-args* (atom nil)]
      (with-redefs [lifecycle/run-pre-compact-hooks! (fn [_chat-ctx trigger custom-instructions]
                                                        (reset! pre-compact-hook-args* {:trigger trigger
                                                                                        :custom-instructions custom-instructions})
                                                        {:blocked? false})]
        (prompt!
         {:message "Rename rfq to query" :chat-id chat-id}
         {:all-tools-mock (constantly steered-compact-tools)
          :call-tool-mock steered-compact-call-tool-mock
          :api-mock
          (fn [{:keys [user-messages on-first-response-received on-message-received
                       on-prepare-tool-call on-tools-called]}]
            (swap! api-calls* conj user-messages)
            (case (count @api-calls*)
              ;; Original task: the user steers /compact while the tool call runs.
              1 (do (on-first-response-received {:type :text :text "Reading"})
                    (on-message-received {:type :text :text "Reading"})
                    (on-prepare-tool-call {:id "call-1" :full-name "eca__read_file" :arguments-text "{\"path\":\"/foo\"}"})
                    (f.chat/prompt-steer {:chat-id chat-id :message "/compact Keep only the rename details"}
                                         (h/db*) (h/messenger) (h/config) (h/metrics))
                    (is (nil? (on-tools-called [{:id "call-1" :full-name "eca__read_file" :arguments {"path" "/foo"}}]))
                        "the compaction takes over the turn instead of continuing the LLM loop"))
              ;; Compaction prompt: the LLM submits the summary.
              2 (do (on-first-response-received)
                    (on-prepare-tool-call {:id "compact-1" :full-name "eca__compact_chat" :arguments-text "{\"summary\":\"Renamed rfq to query\"}"})
                    (on-tools-called [{:id "compact-1" :full-name "eca__compact_chat" :arguments {"summary" "Renamed rfq to query"}}]))
              ;; Resumed task.
              3 (do (on-first-response-received {:type :text :text "Done"})
                    (on-message-received {:type :text :text "Done"})
                    (on-message-received {:type :finish}))))}))
      (is (= 3 (count @api-calls*))
          "LLM is called for the task, the compaction and the resumed task")
      (is (= {:trigger "manual" :custom-instructions "Keep only the rename details"}
             @pre-compact-hook-args*)
          "preCompact hooks run as a manual compaction with the steered instructions")
      (is (match? [{:role "user" :content "Compact the chat following the template:"}
                   {:role "user" :content #(string/includes? % "Keep only the rename details")}]
                  (second @api-calls*))
          "the compact prompt carries the steered instructions")
      (is (match? [{:role "user" :content [{:type :text :text "Continue with the task. The previous user request was:"}]}
                   {:role "user" :content [{:type :text :text "Rename rfq to query"}]}]
                  (nth @api-calls* 2))
          "the original task resumes after the compaction")
      (let [messages (:chat-content-received (h/messages))
            steer-echo (some #(when (and (= :user (:role %))
                                         (= "/compact Keep only the rename details\n" (get-in % [:content :text])))
                                %)
                             messages)]
        (is (match? {:content {:type :text :content-id string?}} steer-echo)
            "the consumed steer is echoed to the client as the user message it was typed as")
        (is (match? {chat-id {:messages [{:role "user" :content [{:type :text :text "Rename rfq to query"}]}
                                         {:role "assistant" :content [{:type :text :text "Reading"}]}
                                         {:role "tool_call" :content {:id "call-1"}}
                                         {:role "tool_call_output" :content {:id "call-1"}}
                                         {:role "user" :content "Compact the chat following the template:"
                                          :content-id (get-in steer-echo [:content :content-id])}
                                         {:role "user" :content-id (get-in steer-echo [:content :content-id])}
                                         {:role "tool_call" :content {:id "compact-1"}}
                                         {:role "tool_call_output" :content {:id "compact-1"}}
                                         {:role "compact_marker" :content {:auto? false}}
                                         {:role "user" :content [{:type :text :text "The conversation was compacted/summarized, consider this summary:\nRenamed rfq to query"}]}
                                         {:role "user" :content [{:type :text :text "Continue with the task. The previous user request was:"}]}
                                         {:role "user" :content [{:type :text :text "Rename rfq to query"}]}
                                         {:role "assistant" :content [{:type :text :text "Done"}]}]}}
                    (:chats (h/db)))
            "the compaction prompt is tied to the /compact echo so rolling back to it undoes the compaction; the raw command never enters history")
        (is (match? (m/embeds [{:role :system :content {:type :text :text "Compacted chat"}}])
                    messages)
            "reported as a manual compaction"))
      (is (nil? (get-in (h/db) [:chats chat-id :steer-message])))
      (is (not (true? (get-in (h/db) [:chats chat-id :compacting?]))))
      (is (nil? (get-in (h/db) [:chats chat-id :auto-compacting?])))
      (is (nil? (get-in (h/db) [:chats chat-id :compact-done?])))
      (is (= :idle (get-in (h/db) [:chats chat-id :status]))))))

(deftest steered-compact-blocked-by-hook-test
  (testing "a preCompact hook blocking (exit 2) a steered /compact keeps the turn going without compacting"
    (h/reset-components!)
    (let [chat-id "steered-compact-blocked-chat"
          api-call-count* (atom 0)
          continuation* (atom nil)]
      (with-redefs [lifecycle/run-pre-compact-hooks! (constantly {:blocked? true
                                                                  :reason nil
                                                                  :hook-name "guard"
                                                                  :stop-turn? false})]
        (prompt!
         {:message "Rename rfq to query" :chat-id chat-id}
         {:all-tools-mock (constantly steered-compact-tools)
          :call-tool-mock steered-compact-call-tool-mock
          :api-mock
          (fn [{:keys [on-first-response-received on-message-received
                       on-prepare-tool-call on-tools-called]}]
            (swap! api-call-count* inc)
            (on-first-response-received {:type :text :text "Reading"})
            (on-message-received {:type :text :text "Reading"})
            (on-prepare-tool-call {:id "call-1" :full-name "eca__read_file" :arguments-text "{\"path\":\"/foo\"}"})
            (f.chat/prompt-steer {:chat-id chat-id :message "/compact Keep only the rename details"}
                                 (h/db*) (h/messenger) (h/config) (h/metrics))
            (reset! continuation* (on-tools-called [{:id "call-1" :full-name "eca__read_file" :arguments {"path" "/foo"}}]))
            (on-message-received {:type :text :text "Done"})
            (on-message-received {:type :finish}))}))
      (is (= 1 @api-call-count*) "no compaction prompt is sent")
      (is (match? {:tools steered-compact-tools
                   :new-messages (m/embeds [{:role "tool_call_output" :content {:id "call-1"}}])}
                  @continuation*)
          "the LLM loop continues normally with the tool results")
      (is (not-any? #(= "compact_marker" (:role %)) (get-in (h/db) [:chats chat-id :messages])))
      (is (not-any? #(and (= "user" (:role %))
                          (string/includes? (pr-str (:content %)) "/compact"))
                    (get-in (h/db) [:chats chat-id :messages]))
          "the blocked command is not injected as plain text either")
      (is (match? (m/embeds [{:role :user :content {:type :text :text "/compact Keep only the rename details\n"}}
                             {:role :system :content {:type :text :text "Compaction blocked by hook 'guard'."}}])
                  (:chat-content-received (h/messages))))
      (is (nil? (get-in (h/db) [:chats chat-id :steer-message])))
      (is (= :idle (get-in (h/db) [:chats chat-id :status]))))))

(deftest steered-plain-message-test
  (testing "a steered plain message is still injected as a user message at the tool-call boundary"
    (h/reset-components!)
    (let [chat-id "steered-plain-chat"
          continuation* (atom nil)]
      (prompt!
       {:message "Rename rfq to query" :chat-id chat-id}
       {:all-tools-mock (constantly steered-compact-tools)
        :call-tool-mock steered-compact-call-tool-mock
        :api-mock
        (fn [{:keys [on-first-response-received on-message-received
                     on-prepare-tool-call on-tools-called]}]
          (on-first-response-received {:type :text :text "Reading"})
          (on-message-received {:type :text :text "Reading"})
          (on-prepare-tool-call {:id "call-1" :full-name "eca__read_file" :arguments-text "{\"path\":\"/foo\"}"})
          (f.chat/prompt-steer {:chat-id chat-id :message "also check the tests"}
                               (h/db*) (h/messenger) (h/config) (h/metrics))
          (reset! continuation* (on-tools-called [{:id "call-1" :full-name "eca__read_file" :arguments {"path" "/foo"}}]))
          (on-message-received {:type :text :text "Done"})
          (on-message-received {:type :finish}))})
      (is (match? {:new-messages (m/embeds [{:role "tool_call_output" :content {:id "call-1"}}
                                            {:role "user" :content [{:type :text :text "also check the tests"}]}])}
                  @continuation*))
      (is (match? (m/embeds [{:role :user :content {:type :text :text "also check the tests\n" :content-id string?}}])
                  (:chat-content-received (h/messages))))
      (is (nil? (get-in (h/db) [:chats chat-id :steer-message])))
      (is (not-any? #(= "compact_marker" (:role %)) (get-in (h/db) [:chats chat-id :messages]))))))

(deftest steered-native-command-test
  (testing "a steer-safe native command steered while the turn runs is executed at the tool-call boundary instead of reaching the LLM as text, then the task resumes (#610)"
    (h/reset-components!)
    (let [chat-id "steered-native-command-chat"
          continuation* (atom nil)]
      (prompt!
       {:message "Rename rfq to query" :chat-id chat-id}
       {:all-tools-mock (constantly steered-compact-tools)
        :call-tool-mock steered-compact-call-tool-mock
        :api-mock
        (fn [{:keys [on-first-response-received on-message-received
                     on-prepare-tool-call on-tools-called]}]
          (on-first-response-received {:type :text :text "Reading"})
          (on-message-received {:type :text :text "Reading"})
          (on-prepare-tool-call {:id "call-1" :full-name "eca__read_file" :arguments-text "{\"path\":\"/foo\"}"})
          (is (some? (get-in (h/db) [:chats chat-id :prompt-cache])) "the prompt cache was built for this prompt")
          (f.chat/prompt-steer {:chat-id chat-id :message "/sync-system-prompt"}
                               (h/db*) (h/messenger) (h/config) (h/metrics))
          (is (= "/sync-system-prompt" (get-in (h/db) [:chats chat-id :steer-message])) "queued for the turn boundary")
          (reset! continuation* (on-tools-called [{:id "call-1" :full-name "eca__read_file" :arguments {"path" "/foo"}}]))
          (on-message-received {:type :text :text "Done"})
          (on-message-received {:type :finish}))})
      (is (match? {:tools steered-compact-tools
                   :new-messages (m/embeds [{:role "tool_call_output" :content {:id "call-1"}}])}
                  @continuation*)
          "the LLM loop continues normally with the tool results")
      (is (not-any? #(string/includes? (pr-str %) "/sync-system-prompt") (:new-messages @continuation*))
          "the command is not sent to the LLM")
      (is (not-any? #(string/includes? (pr-str (:content %)) "/sync-system-prompt")
                    (get-in (h/db) [:chats chat-id :messages]))
          "the command never enters history")
      (is (nil? (get-in (h/db) [:chats chat-id :prompt-cache])) "the command ran at the boundary")
      (is (match? (m/embeds [{:role :user :content {:type :text :text "/sync-system-prompt\n" :content-id string?}}
                             {:role "system" :content {:type :text :text #(string/includes? % "System prompt will be re-synced on the next message")}}])
                  (:chat-content-received (h/messages)))
          "the steer is echoed as the user message it was typed as, followed by the command output")
      (is (nil? (get-in (h/db) [:chats chat-id :steer-message])))
      (is (= :idle (get-in (h/db) [:chats chat-id :status]))))))

(deftest consume-steered-compact-test
  (let [consume! (fn [steer-message]
                   (h/reset-components!)
                   (swap! (h/db*) assoc-in [:chats "chat-1"]
                          (cond-> {:id "chat-1" :status :running}
                            steer-message (assoc :steer-message steer-message)))
                   (#'f.chat/consume-steered-compact! "chat-1" (h/db*)
                                                      {:chat-id "chat-1" :db* (h/db*) :messenger (h/messenger) :config (h/config)}))]
    (testing "a pending /compact without instructions is consumed with empty instructions"
      (is (match? {:custom-instructions "" :content-id string?}
                  (consume! "/compact")))
      (is (nil? (get-in (h/db) [:chats "chat-1" :steer-message])))
      (is (match? [{:role :user :content {:type :text :text "/compact\n" :content-id string?}}]
                  (:chat-content-received (h/messages)))))
    (testing "quoted instructions are joined like a regular /compact"
      (is (match? {:custom-instructions "keep the plan only"}
                  (consume! "/compact \"keep the plan\" only"))))
    (testing "other steers are left pending untouched"
      (is (nil? (consume! "/compacting is not a command")))
      (is (= "/compacting is not a command" (get-in (h/db) [:chats "chat-1" :steer-message])))
      (is (empty? (:chat-content-received (h/messages)))))
    (testing "no pending steer"
      (is (nil? (consume! nil))))))

(deftest consume-steered-native-command-test
  (let [consume! (fn [steer-message]
                   (h/reset-components!)
                   (swap! (h/db*) assoc-in [:chats "chat-1"]
                          (cond-> {:id "chat-1" :status :running :prompt-cache {:static "cached"}}
                            steer-message (assoc :steer-message steer-message)))
                   (#'f.chat/consume-steered-native-command! "chat-1" (h/db*)
                                                             {:chat-id "chat-1" :db* (h/db*) :messenger (h/messenger) :config (h/config)}))]
    (testing "a pending steer-safe native command is consumed, echoed and run with its output shown"
      (is (nil? (consume! "/sync-system-prompt")))
      (is (nil? (get-in (h/db) [:chats "chat-1" :steer-message])))
      (is (nil? (get-in (h/db) [:chats "chat-1" :prompt-cache])) "the command ran")
      (is (match? [{:role :user :content {:type :text :text "/sync-system-prompt\n" :content-id string?}}
                   {:role "system" :content {:type :text :text #(string/includes? % "System prompt will be re-synced on the next message")}}]
                  (:chat-content-received (h/messages)))))
    (testing "/compact, chat-changing commands and plain text are left pending untouched"
      (doseq [steer ["/compact keep the plan" "/model foo" "also check the tests"]]
        (is (nil? (consume! steer)))
        (is (= steer (get-in (h/db) [:chats "chat-1" :steer-message])))
        (is (= {:static "cached"} (get-in (h/db) [:chats "chat-1" :prompt-cache])))
        (is (empty? (:chat-content-received (h/messages))))))
    (testing "no pending steer"
      (is (nil? (consume! nil))))))

(deftest concurrent-tool-calls-test
  (testing "Running three calls simultaneously"
    (h/reset-components!)
    (let [{:keys [chat-id]}
          (prompt!
           {:message "Run 3 read-only tool calls simultaneously."}
           {:all-tools-mock (constantly [{:name "ro_tool_1" :full-name "eca__ro_tool_1" :server {:name "eca"}}
                                         {:name "ro_tool_2" :full-name "eca__ro_tool_2" :server {:name "eca"}}
                                         {:name "ro_tool_3" :full-name "eca__ro_tool_3" :server {:name "eca"}}])
            :api-mock
            (fn [{:keys [on-first-response-received
                         on-message-received
                         on-prepare-tool-call
                         on-tools-called]}]
              (on-first-response-received {:type :text :text "Ok,"})
              (on-message-received {:type :text :text "Ok,"})
              (on-message-received {:type :text :text " working on it"})
              (on-prepare-tool-call {:id "call-1" :full-name "eca__ro_tool_1" :arguments-text ""})
              (on-prepare-tool-call {:id "call-2" :full-name "eca__ro_tool_2" :arguments-text ""})
              (on-prepare-tool-call {:id "call-3" :full-name "eca__ro_tool_3" :arguments-text ""})
              (on-tools-called [{:id "call-1" :full-name "eca__ro_tool_1" :arguments {}}
                                {:id "call-2" :full-name "eca__ro_tool_2" :arguments {}}
                                {:id "call-3" :full-name "eca__ro_tool_3" :arguments {}}])
              (on-message-received {:type :text :text "The tool calls returned: \n"})
              (on-message-received {:type :text :text "something"})
              (on-message-received {:type :finish}))
            :call-tool-mock
            ;; Ensure that the tools complete in the 3-2-1 order by adjusting sleep times
            (fn [full-name & _others]
              ;; When this is called, we are already in a future.
              (case full-name

                "eca__ro_tool_1"
                (do (deep-sleep 900)
                    {:error false
                     :contents [{:type :text :text "RO tool call 1 result"}]})

                "eca__ro_tool_2"
                (do (deep-sleep 600)
                    {:error false
                     :contents [{:type :text :text "RO tool call 2 result"}]})

                "eca__ro_tool_3"
                (do (deep-sleep 100)
                    {:error false
                     :contents [{:type :text :text "RO tool call 3 result"}]})))})]

      (is (match?
           {chat-id {:id chat-id
                     :messages [{:role "user" :content [{:type :text :text "Run 3 read-only tool calls simultaneously."}]}
                                {:role "assistant" :content [{:type :text :text "Ok, working on it"}]}
                                {:role "tool_call" :content {:id "call-3" :full-name "eca__ro_tool_3" :arguments {}}}
                                {:role "tool_call_output" :content {:id "call-3"  :full-name "eca__ro_tool_3" :arguments {}
                                                                    :output {:error false
                                                                             :contents [{:type :text, :text "RO tool call 3 result"}]}}}
                                {:role "tool_call" :content {:id "call-2" :full-name "eca__ro_tool_2" :arguments {}}}
                                {:role "tool_call_output" :content {:id "call-2" :full-name "eca__ro_tool_2" :arguments {}
                                                                    :output {:error false
                                                                             :contents [{:type :text, :text "RO tool call 2 result"}]}}}
                                {:role "tool_call" :content {:id "call-1" :full-name "eca__ro_tool_1" :arguments {}}}
                                {:role "tool_call_output" :content {:id "call-1" :full-name "eca__ro_tool_1" :arguments {}
                                                                    :output {:error false
                                                                             :contents [{:type :text, :text "RO tool call 1 result"}]}}}
                                {:role "assistant" :content [{:type :text, :text "The tool calls returned: \nsomething"}]}]}}
           (:chats (h/db))))
      (is (match?
           {:chat-content-received
            [{:role :user :content {:type :text :text "Run 3 read-only tool calls simultaneously.\n"}}
             {:role :system :content {:type :progress :state :running, :text "Waiting model"}}
             {:role :system :content {:type :progress :state :running, :text "Generating"}}
             {:role :assistant :content {:type :text :text "Ok,"}}
             {:role :assistant :content {:type :text :text " working on it"}}
             {:role :assistant :content {:type :toolCallPrepare :id "call-1" :name "ro_tool_1" :arguments-text ""}}
             {:role :assistant :content {:type :toolCallPrepare :id "call-2" :name "ro_tool_2" :arguments-text ""}}
             {:role :assistant :content {:type :toolCallPrepare :id "call-3" :name "ro_tool_3" :arguments-text ""}}
             {:role :assistant :content {:type :toolCallRun :id "call-1" :name "ro_tool_1" :arguments {} :manual-approval false}}
             {:role :assistant :content {:type :toolCallRunning :id "call-1" :name "ro_tool_1" :arguments {}}}
             {:role :system :content {:type :progress :state :running, :text "Calling tool"}}
             {:role :assistant :content {:type :toolCallRun :id "call-2" :name "ro_tool_2" :arguments {} :manual-approval false}}
             {:role :assistant :content {:type :toolCallRunning :id "call-2" :name "ro_tool_2" :arguments {}}}
             {:role :system :content {:type :progress :state :running, :text "Calling tool"}}
             {:role :assistant :content {:type :toolCallRun :id "call-3" :name "ro_tool_3" :arguments {} :manual-approval false}}
             {:role :assistant :content {:type :toolCallRunning :id "call-3" :name "ro_tool_3" :arguments {}}}
             {:role :system :content {:type :progress :state :running, :text "Calling tool"}}
             {:role :assistant :content {:type :toolCalled :id "call-3" :name "ro_tool_3" :arguments {}
                                         :outputs [{:type :text :text "RO tool call 3 result"}]
                                         :error false}}
             {:role :system :content {:type :progress :state :running, :text "Generating"}}
             {:role :assistant :content {:type :toolCalled :id "call-2" :name "ro_tool_2" :arguments {}
                                         :outputs [{:type :text :text "RO tool call 2 result"}]
                                         :error false}}
             {:role :system :content {:type :progress :state :running, :text "Generating"}}
             {:role :assistant :content {:type :toolCalled :id "call-1" :name "ro_tool_1" :arguments {}
                                         :outputs [{:type :text :text "RO tool call 1 result"}]
                                         :error false}}
             {:role :system :content {:type :progress :state :running, :text "Generating"}}
             {:role :assistant :content {:type :text :text "The tool calls returned: \n"}}
             {:role :assistant :content {:type :text :text "something"}}
             {:role :system :content {:type :progress :state :finished}}]}
           (h/messages))))))

(deftest tool-calls-with-prompt-stop-test
  (testing "Three concurrent tool calls. Stopped before they all finished. Tool call 3 finishes. Calls 1,2 reject."
    (h/reset-components!)
    (let [wait-for-tool3 (promise)
          wait-for-tool2 (promise)
          wait-for-stop (promise)
          {:keys [chat-id]}
          (prompt!
           {:message "Run 3 read-only tool calls simultaneously."}
           {:all-tools-mock (constantly [{:name "ro_tool_1" :full-name "eca__ro_tool_1" :server {:name "eca"}}
                                         {:name "ro_tool_2" :full-name "eca__ro_tool_2" :server {:name "eca"}}
                                         {:name "ro_tool_3" :full-name "eca__ro_tool_3" :server {:name "eca"}}])
            :api-mock
            (fn [{:keys [on-first-response-received
                         on-prepare-tool-call
                         on-tools-called]}]
              (let [chat-id (first (keys (:chats (h/db))))]
                (on-first-response-received {:type :text :text "Ok,"})
                (on-prepare-tool-call {:id "call-1" :full-name "eca__ro_tool_1" :arguments-text ""})
                (on-prepare-tool-call {:id "call-2" :full-name "eca__ro_tool_2" :arguments-text ""})
                (on-prepare-tool-call {:id "call-3" :full-name "eca__ro_tool_3" :arguments-text ""})
                (future (Thread/sleep 400)
                        (when (= :timeout (deref wait-for-tool3 10000 :timeout))
                          (println "tool-calls-with-prompt-stop-test: deref in prompt stop future timed out"))
                        (Thread/sleep 50)
                        (f.chat/prompt-stop {:chat-id chat-id} (h/db*) (h/messenger) (h/config) (h/metrics) {})
                        (deliver wait-for-stop true))
                (on-tools-called [{:id "call-1" :full-name "eca__ro_tool_1" :arguments {}}
                                  {:id "call-2" :full-name "eca__ro_tool_2" :arguments {}}
                                  {:id "call-3" :full-name "eca__ro_tool_3" :arguments {}}])))
            :call-tool-mock
            (fn [full-name & _others]
              ;; When this is called, we are already in a future
              (case full-name

                "eca__ro_tool_1"
                (do (deep-sleep 1000)
                    (when (= :timeout (deref wait-for-tool2 10000 :timeout))
                      (println "tool-calls-with-prompt-stop-test: deref in tool 1 timed out"))
                    {:error false
                     :contents [{:type :text :text "RO tool call 1 result"}]})

                "eca__ro_tool_2"
                (do (deep-sleep 800)
                    (when (= :timeout (deref wait-for-stop 10000 :timeout))
                      (println "tool-calls-with-prompt-stop-test: deref in tool 2 timed out"))
                    (deliver wait-for-tool2 true)
                    {:error false
                     :contents [{:type :text :text "RO tool call 2 result"}]})

                "eca__ro_tool_3"
                (do (deep-sleep 200)
                    (deliver wait-for-tool3 true)
                    {:error false
                     :contents [{:type :text :text "RO tool call 3 result"}]})))})]
      (is (match? {chat-id
                   {:id chat-id
                    :messages [{:role "user" :content [{:type :text :text "Run 3 read-only tool calls simultaneously."}]}
                               {:role "tool_call" :content {:id "call-3" :full-name "eca__ro_tool_3" :arguments {}}}
                               {:role "tool_call_output" :content {:id "call-3" :full-name "eca__ro_tool_3" :arguments {}
                                                                   :output {:error false
                                                                            :contents [{:type :text, :text "RO tool call 3 result"}]}}}
                               {:role "tool_call" :content {:id "call-2" :full-name "eca__ro_tool_2" :arguments {}}}
                               {:role "tool_call_output" :content {:id "call-2" :full-name "eca__ro_tool_2" :arguments {}
                                                                   :output {:error false
                                                                            :contents [{:type :text, :text "RO tool call 2 result"}]}}}
                               {:role "tool_call" :content {:id "call-1" :full-name "eca__ro_tool_1" :arguments {}}}
                               {:role "tool_call_output" :content {:id "call-1" :full-name "eca__ro_tool_1" :arguments {}
                                                                   :output {:error false
                                                                            :contents [{:type :text, :text "RO tool call 1 result"}]}}}]}}
                  (:chats (h/db))))
      (is (match? {:chat-content-received
                   [{:role :user :content {:type :text :text "Run 3 read-only tool calls simultaneously.\n"}}
                    {:role :system :content {:type :progress :text "Waiting model"}}
                    {:role :system :content {:type :progress :text "Generating"}}
                    {:role :assistant :content {:type :toolCallPrepare :id "call-1" :name "ro_tool_1" :arguments-text ""}}
                    {:role :assistant :content {:type :toolCallPrepare :id "call-2" :name "ro_tool_2" :arguments-text ""}}
                    {:role :assistant :content {:type :toolCallPrepare :id "call-3" :name "ro_tool_3" :arguments-text ""}}
                    {:role :assistant :content {:type :toolCallRun :id "call-1" :name "ro_tool_1" :arguments {}}}
                    {:role :assistant :content {:type :toolCallRunning :id "call-1" :name "ro_tool_1" :arguments {}}}
                    {:role :system :content {:type :progress :state :running :text "Calling tool"}}
                    {:role :assistant :content {:type :toolCallRun :id "call-2" :name "ro_tool_2" :arguments {} :manual-approval false}}
                    {:role :assistant :content {:type :toolCallRunning :id "call-2" :name "ro_tool_2" :arguments {}}}
                    {:role :system :content {:type :progress :state :running :text "Calling tool"}}
                    {:role :assistant :content {:type :toolCallRun :id "call-3" :name "ro_tool_3" :arguments {} :manual-approval false}}
                    {:role :assistant :content {:type :toolCallRunning :id "call-3" :name "ro_tool_3" :arguments {}}}
                    {:role :system :content {:type :progress :state :running :text "Calling tool"}}
                    {:role :assistant :content {:type :toolCalled :id "call-3" :name "ro_tool_3" :arguments {}
                                                :outputs [{:type :text :text "RO tool call 3 result"}]}}
                    {:role :system :content {:type :progress :state :running :text "Generating"}}
                    {:role :system :content {:type :text :text "\nPrompt stopped\n"}}
                    {:role :system :content {:type :progress :state :finished}}
                    {:role :assistant :content {:type :toolCallRejected :id "call-2" :name "ro_tool_2" :arguments {} :reason :user}}
                    {:role :assistant :content {:type :toolCallRejected :id "call-1" :name "ro_tool_1" :arguments {} :reason :user}}]}
                  (h/messages))))))

(deftest send-mcp-prompt-test
  (testing "Argument mapping for send-mcp-prompt! should map arg values to prompt argument names"
    (let [test-arguments [{:name "foo"} {:name "bar"}]
          prompt-args (atom nil)
          test-chat-ctx {:db* (atom {})}
          invoked? (atom nil)]
      (with-redefs [f.mcp/all-prompts (fn [_]
                                        [{:name "awesome-prompt" :arguments test-arguments}])
                    f.prompt/get-prompt! (fn [_ args-map _]
                                           (reset! prompt-args args-map)
                                           {:messages [{:role :user :content "test"}]})
                    f.chat/prompt-messages! (fn [messages source-type ctx]
                                              (reset! invoked? [messages source-type ctx]))]
        (#'f.chat/send-mcp-prompt! {:prompt "awesome-prompt" :args [42 "yo"]} test-chat-ctx)
        (is (match?
             @prompt-args
             {"foo" 42 "bar" "yo"}))
        (is (match?
             @invoked?
             [[{:role :user :content "test"}] :mcp-prompt test-chat-ctx])))))

  (testing "shows error message and finishes chat when get-prompt! returns error-message"
    (let [test-chat-ctx {:db* (atom {})}
          sent-content (atom nil)
          finished-status (atom nil)]
      (with-redefs [f.mcp/all-prompts (fn [_]
                                        [{:name "failing-prompt" :arguments [{:name "arg1"}]}])
                    f.prompt/get-prompt! (fn [_ _ _]
                                           {:error-message "MCP error getting prompt: code=-32603 message=Invalid required argument: arg1"})
                    lifecycle/send-content! (fn [_ctx _role content]
                                              (reset! sent-content content))
                    lifecycle/finish-chat-prompt! (fn [status _ctx]
                                                    (reset! finished-status status))]
        (#'f.chat/send-mcp-prompt! {:prompt "failing-prompt" :args ["val1"]} test-chat-ctx)
        (is (= :text (:type @sent-content)))
        (is (string/includes? (:text @sent-content) "MCP error getting prompt"))
        (is (= :idle @finished-status)))))

  (testing "shows error message and finishes chat when get-prompt! returns nil"
    (let [test-chat-ctx {:db* (atom {})}
          sent-content (atom nil)
          finished-status (atom nil)]
      (with-redefs [f.mcp/all-prompts (fn [_]
                                        [{:name "nil-prompt" :arguments []}])
                    f.prompt/get-prompt! (fn [_ _ _] nil)
                    lifecycle/send-content! (fn [_ctx _role content]
                                              (reset! sent-content content))
                    lifecycle/finish-chat-prompt! (fn [status _ctx]
                                                    (reset! finished-status status))]
        (#'f.chat/send-mcp-prompt! {:prompt "nil-prompt" :args []} test-chat-ctx)
        (is (= :text (:type @sent-content)))
        (is (string/includes? (:text @sent-content) "No response from prompt"))
        (is (= :idle @finished-status))))))

(deftest delete-chat-command-test
  (testing "/delete-chat asks confirmation and /delete-chat confirm deletes the chat"
    (h/reset-components!)
    (let [{:keys [chat-id]}
          (prompt!
           {:message "hi"}
           {:all-tools-mock (constantly [])
            :api-mock
            (fn [{:keys [on-first-response-received on-message-received]}]
              (on-first-response-received {:type :text :text "hello"})
              (on-message-received {:type :text :text "hello"})
              (on-message-received {:type :finish}))})
          llm-mock (fn [& _] (throw (ex-info "commands should not call the LLM" {})))]
      (h/reset-messenger!)
      (prompt! {:message "/delete-chat" :chat-id chat-id}
               {:all-tools-mock (constantly [])
                :api-mock llm-mock})
      (is (match?
           {:chat-content-received
            (m/embeds [{:chat-id chat-id
                        :role "system"
                        :content {:type :text
                                  :text #(string/includes? % "Run `/delete-chat confirm` to proceed.")}}])}
           (h/messages)))
      (is (some? (get-in (h/db) [:chats chat-id]))
          "chat is kept until deletion is confirmed")
      (h/reset-messenger!)
      (prompt! {:message "/delete-chat confirm" :chat-id chat-id}
               {:all-tools-mock (constantly [])
                :api-mock llm-mock})
      (is (match? {:chat-deleted [{:chat-id chat-id}]}
                  (h/messages)))
      (is (nil? (get-in (h/db) [:chats chat-id])))
      (is (contains? (:deleted-chat-ids (h/db)) chat-id)))))

(deftest message->decision-test
  (testing "plain prompt message"
    (is (= {:type :prompt-message
            :message "Hello world"}
           (#'f.chat/message->decision "Hello world" {} {}))))
  (testing "message starting with a absolute path"
    (is (= {:type :prompt-message
            :message "/path/to/file check this out"}
           (#'f.chat/message->decision "/path/to/file check this out" {} {}))))
  (testing "ECA command without args"
    (is (= {:type :eca-command
            :command "doctor"
            :command-type :native
            :args []}
           (#'f.chat/message->decision "/doctor" {} {}))))
  (testing "ECA command with args"
    (is (= {:type :eca-command
            :command "login"
            :command-type :native
            :args ["foo" "bar"]}
           (#'f.chat/message->decision "/login foo bar" {} {}))))
  (testing "ECA command with args with spaces in quotes"
    (is (= {:type :eca-command
            :command "login"
            :command-type :native
            :args ["foo bar" "baz" "qux bla blow"]}
           (#'f.chat/message->decision "/login \"foo bar\" baz \"qux bla blow\"" {} {}))))
  (testing "quoted file arguments with spaces remain one token"
    (is (= ["review" "@/dir/My File.clj"]
           (#'f.chat/tokenize-args "review \"@/dir/My File.clj\""))))
  (with-redefs [f.mcp/all-prompts (constantly [{:name "prompt"
                                                :server "server"}])]
    (testing "MCP prompt without args"
      (is (= {:type :mcp-prompt
              :server "server"
              :prompt "prompt"
              :args []}
             (#'f.chat/message->decision "/server:prompt" {} {}))))
    (testing "MCP prompt with args"
      (is (= {:type :mcp-prompt
              :server "server"
              :prompt "prompt"
              :args ["arg1" "arg2"]}
             (#'f.chat/message->decision "/server:prompt arg1 arg2" {} {}))))))

(deftest rollback-persists-to-cache-test
  (testing "rollback flushes the trimmed history to disk so it survives an ECA restart"
    (h/reset-components!)
    (let [{:keys [chat-id]}
          (prompt!
           {:message "hi"}
           {:all-tools-mock (constantly [])
            :api-mock
            (fn [{:keys [on-first-response-received on-message-received]}]
              (on-first-response-received {:type :text :text "hello"})
              (on-message-received {:type :text :text "hello"})
              (on-message-received {:type :finish}))})
          first-content-id (get-in (h/db) [:chats chat-id :messages 0 :content-id])
          save-calls* (atom 0)]
      (with-redefs [db/save-chat! (fn [& _] (swap! save-calls* inc))]
        (f.chat/rollback-chat {:chat-id chat-id
                               :include ["messages" "tools"]
                               :content-id first-content-id}
                              (h/db*) (h/messenger) (h/metrics)))
      (is (pos? @save-calls*)
          "rollback-chat should persist the trimmed history immediately"))))

(deftest rollback-chat-test
  (testing "Rollback chat removes messages after content-id"
    (h/reset-components!)
    (let [{:keys [chat-id]}
          (prompt!
           {:message "Count with me: 1"}
           {:all-tools-mock (constantly [])
            :api-mock
            (fn [{:keys [on-first-response-received
                         on-message-received]}]
              (on-first-response-received {:type :text :text "2"})
              (on-message-received {:type :text :text "2"})
              (on-message-received {:type :finish}))})
          first-content-id (get-in (h/db) [:chats chat-id :messages 0 :content-id])
          _ (is (some? first-content-id) "first-content-id should exist")]
      ;; Verify initial state
      (is (match?
           {chat-id {:id chat-id
                     :messages [{:role "user" :content [{:type :text :text "Count with me: 1"}] :content-id first-content-id}
                                {:role "assistant" :content [{:type :text :text "2"}]}]}}
           (:chats (h/db))))

      ;; Add second message
      (h/reset-messenger!)
      (prompt!
       {:message "3"
        :chat-id chat-id}
       {:all-tools-mock (constantly [])
        :api-mock
        (fn [{:keys [on-first-response-received
                     on-message-received]}]
          (on-first-response-received {:type :text :text "4"})
          (on-message-received {:type :text :text "4"})
          (on-message-received {:type :finish}))})
      (let [second-content-id (get-in (h/db) [:chats chat-id :messages 2 :content-id])]

        ;; Verify we now have 4 messages
        (is (match?
             {chat-id {:id chat-id
                       :messages [{:role "user" :content [{:type :text :text "Count with me: 1"}] :content-id first-content-id}
                                  {:role "assistant" :content [{:type :text :text "2"}]}
                                  {:role "user" :content [{:type :text :text "3"}] :content-id second-content-id}
                                  {:role "assistant" :content [{:type :text :text "4"}]}]}}
             (:chats (h/db))))

        ;; Rollback to second message (keep first 2 messages, remove last 2)
        (h/reset-messenger!)
        (is (= {} (f.chat/rollback-chat
                   {:chat-id chat-id
                    :include ["messages" "tools"]
                    :content-id second-content-id} (h/db*) (h/messenger) (h/metrics))))

        ;; Verify messages after content-id are removed (keeps messages before content-id)
        (is (match?
             {chat-id {:id chat-id
                       :messages [{:role "user" :content [{:type :text :text "Count with me: 1"}] :content-id first-content-id}
                                  {:role "assistant" :content [{:type :text :text "2"}]}]}}
             (:chats (h/db))))

        ;; Verify messenger received chat-clear and then messages
        (is (match?
             {:chat-clear [{:chat-id chat-id :messages true}]
              :chat-content-received
              [{:chat-id chat-id
                :content {:type :text :text "\nCount with me: 1" :content-id first-content-id}
                :role "user"}
               {:chat-id chat-id
                :content {:type :text :text "\n2"}
                :role "assistant"}]}
             (h/messages)))))))

(deftest prompt-cache-agent-switch-test
  (testing "Static prompt cache is rebuilt when switching agents within the same chat"
    (h/reset-components!)
    (let [build-calls* (atom 0)
          real-build f.prompt/build-chat-instructions]
      (with-redefs [f.prompt/build-chat-instructions
                    (fn [& args]
                      (swap! build-calls* inc)
                      (apply real-build args))
                    ;; Test config doesn't populate :agent, so validate-agent-name
                    ;; would fall back to "code" for every input. Short-circuit it
                    ;; so the test can actually exercise an agent switch.
                    config/validate-agent-name (fn [agent-name _config] agent-name)]
        (let [mocks {:all-tools-mock (constantly [])
                     :api-mock (fn [{:keys [on-message-received]}]
                                 (on-message-received {:type :finish}))}
              {:keys [chat-id]} (prompt! {:message "Hi" :agent "code"} mocks)]
          (is (= 1 @build-calls*)
              "First prompt should build the static instructions once")
          (h/reset-messenger!)
          (prompt! {:message "Still code" :chat-id chat-id :agent "code"} mocks)
          (is (= 1 @build-calls*)
              "Second prompt with the same agent should reuse cached instructions")
          (h/reset-messenger!)
          (prompt! {:message "Switch to plan" :chat-id chat-id :agent "plan"} mocks)
          (is (= 2 @build-calls*)
              "Switching agent should invalidate the cache and rebuild instructions")
          (h/reset-messenger!)
          (prompt! {:message "Back to code" :chat-id chat-id :agent "code"} mocks)
          (is (= 3 @build-calls*)
              "Switching back to the first agent should also trigger a rebuild"))))))

(deftest prompt-cache-stable-context-change-test
  (testing "Static prompt cache is rebuilt when stable context content changes"
    (h/reset-components!)
    (h/config! {:chat {:autoSyncSystemPrompt true}})
    (let [build-calls* (atom 0)
          real-build f.prompt/build-chat-instructions]
      (with-redefs [f.context/agents-file-contexts (constantly [])
                    f.context/raw-contexts->refined (fn [contexts _db] contexts)
                    f.prompt/build-chat-instructions
                    (fn [& args]
                      (swap! build-calls* inc)
                      (apply real-build args))]
        (let [mocks {:all-tools-mock (constantly [])
                     :api-mock (fn [{:keys [on-message-received]}]
                                 (on-message-received {:type :finish}))}
              ctx-v1 {:type :file :path "foo.clj" :content "v1"}
              ctx-v2 {:type :file :path "foo.clj" :content "v2"}
              {:keys [chat-id]} (prompt! {:message "Hi" :contexts [ctx-v1]} mocks)]
          (is (= 1 @build-calls*))
          (h/reset-messenger!)
          (prompt! {:message "Again" :chat-id chat-id :contexts [ctx-v1]} mocks)
          (is (= 1 @build-calls*) "Same stable context should reuse cached static instructions")
          (h/reset-messenger!)
          (prompt! {:message "Changed" :chat-id chat-id :contexts [ctx-v2]} mocks)
          (is (= 2 @build-calls*) "Changed stable context should rebuild static instructions"))))))

(deftest prompt-cache-repo-map-content-intentionally-does-not-change-test
  (testing "Changing only repoMap content does not rebuild static instructions because repoMap is an expensive cached snapshot"
    (h/reset-components!)
    (h/config! {:chat {:autoSyncSystemPrompt true}})
    (let [build-calls* (atom 0)
          repo-map-calls* (atom 0)
          real-build f.prompt/build-chat-instructions]
      (with-redefs [f.context/agents-file-contexts (constantly [])
                    f.context/raw-contexts->refined (fn [contexts _db] contexts)
                    f.index/repo-map (fn [& _]
                                       (str "TREE-" (swap! repo-map-calls* inc)))
                    f.prompt/build-chat-instructions
                    (fn [& args]
                      (swap! build-calls* inc)
                      (apply real-build args))]
        (let [mocks {:all-tools-mock (constantly [])
                     :api-mock (fn [{:keys [on-message-received]}]
                                 (on-message-received {:type :finish}))}
              ctx {:type :repoMap}
              {:keys [chat-id]} (prompt! {:message "Hi" :contexts [ctx]} mocks)]
          (is (= 1 @build-calls*))
          (is (= 1 @repo-map-calls*) "Initial cache miss renders the repoMap snapshot")
          (h/reset-messenger!)
          (prompt! {:message "Again" :chat-id chat-id :contexts [ctx]} mocks)
          (is (= 1 @build-calls*) "repoMap presence, not repoMap content, is part of cache identity")
          (is (= 1 @repo-map-calls*) "Cache hit must not rebuild the expensive repoMap"))))))

(deftest prompt-cache-absolute-prompt-file-change-test
  (testing "Static prompt cache is rebuilt when an absolute systemPromptFile changes"
    (h/reset-components!)
    (let [build-calls* (atom 0)
          real-build f.prompt/build-chat-instructions
          prompt-file (fs/create-temp-file {:prefix "eca-prompt" :suffix ".md"})]
      (try
        (spit (str prompt-file) "Prompt v1")
        (h/config! {:chat {:autoSyncSystemPrompt true}
                    :prompts {:chat nil}
                    :agent {"code" {:prompts {:chat nil}
                                      :systemPromptFile (str prompt-file)}}})
        (with-redefs [f.prompt/build-chat-instructions
                      (fn [& args]
                        (swap! build-calls* inc)
                        (apply real-build args))]
          (let [mocks {:all-tools-mock (constantly [])
                       :api-mock (fn [{:keys [on-message-received]}]
                                   (on-message-received {:type :finish}))}
                {:keys [chat-id]} (prompt! {:message "Hi" :agent "code"} mocks)]
            (is (= 1 @build-calls*))
            (h/reset-messenger!)
            (prompt! {:message "Again" :chat-id chat-id :agent "code"} mocks)
            (is (= 1 @build-calls*) "Unchanged prompt file should reuse cached static instructions")
            (spit (str prompt-file) "Prompt v2")
            (h/reset-messenger!)
            (prompt! {:message "Changed" :chat-id chat-id :agent "code"} mocks)
            (is (= 2 @build-calls*) "Changed prompt file content should rebuild static instructions")))
        (finally
          (fs/delete-if-exists prompt-file))))))

(defn ^:private system-prompt-notices []
  (->> (h/messages)
       :chat-content-received
       (filter #(= :system (:role %)))
       (keep #(get-in % [:content :text]))
       (filter #(or (string/includes? % "System prompt changed")
                    (string/includes? % "Tools changed")))))

(deftest system-prompt-changed-notice-test
  (testing "With autoSyncSystemPrompt, a system notice is sent when the system prompt changes mid-chat"
    (h/reset-components!)
    (h/config! {:chat {:autoSyncSystemPrompt true}})
    (let [rules* (atom [])
          mocks {:all-tools-mock (constantly [])
                 :api-mock (fn [{:keys [on-message-received]}]
                             (on-message-received {:type :finish}))}]
      (with-redefs [f.rules/all-rules (fn [& _] {:static @rules* :path-scoped []})]
        (let [{:keys [chat-id]} (prompt! {:message "Hi"} mocks)]
          (is (empty? (system-prompt-notices)) "No notice on the first prompt")
          (h/reset-messenger!)
          (prompt! {:message "Again" :chat-id chat-id} mocks)
          (is (empty? (system-prompt-notices)) "No notice when nothing changed")
          (h/reset-messenger!)
          (reset! rules* [{:id "r1" :name "r1" :scope :project :content "Rule v1"}])
          (prompt! {:message "Changed" :chat-id chat-id} mocks)
          (is (= ["\nSystem prompt changed (rules), prompt cache invalidated.\n"]
                 (system-prompt-notices))
              "Notice names the changed category"))))))

(deftest skills-never-auto-sync-test
  (testing "Even with autoSyncSystemPrompt, skills changes keep the pinned system prompt"
    (h/reset-components!)
    (h/config! {:chat {:autoSyncSystemPrompt true}})
    (let [skills* (atom [])
          rules* (atom [])
          build-calls* (atom 0)
          real-build f.prompt/build-chat-instructions
          mocks {:all-tools-mock (constantly [])
                 :api-mock (fn [{:keys [on-message-received]}]
                             (on-message-received {:type :finish}))}]
      (with-redefs [f.skills/all (fn [& _] @skills*)
                    f.rules/all-rules (fn [& _] {:static @rules* :path-scoped []})
                    f.prompt/build-chat-instructions (fn [& args]
                                                       (swap! build-calls* inc)
                                                       (apply real-build args))]
        (let [{:keys [chat-id]} (prompt! {:message "Hi"} mocks)]
          (is (= 1 @build-calls*))
          (h/reset-messenger!)
          (reset! skills* [{:name "my-skill" :description "Does things"}])
          (prompt! {:message "Skill changed" :chat-id chat-id} mocks)
          (is (= 1 @build-calls*) "Skills-only change must not rebuild the system prompt")
          (is (= ["System prompt changed (skills), keeping current chat system prompt, changes will apply to new chats. Use /sync-system-prompt to apply now.\n"]
                 (system-prompt-notices)))
          (h/reset-messenger!)
          (prompt! {:message "Again" :chat-id chat-id} mocks)
          (is (empty? (system-prompt-notices)) "Same drift does not re-notify every turn")
          (h/reset-messenger!)
          (reset! rules* [{:id "r1" :name "r1" :scope :project :content "Rule v1"}])
          (prompt! {:message "Rules changed too" :chat-id chat-id} mocks)
          (is (= 2 @build-calls*) "A non-skills change still re-syncs, taking skills along")
          (is (= ["\nSystem prompt changed (rules, skills), prompt cache invalidated.\n"]
                 (system-prompt-notices))))))))

(deftest system-prompt-pinned-by-default-test
  (testing "Without autoSyncSystemPrompt (default), mid-chat changes keep the pinned system prompt"
    (h/reset-components!)
    (let [skills* (atom [])
          build-calls* (atom 0)
          real-build f.prompt/build-chat-instructions
          mocks {:all-tools-mock (constantly [])
                 :api-mock (fn [{:keys [on-message-received]}]
                             (on-message-received {:type :finish}))}]
      (with-redefs [f.skills/all (fn [& _] @skills*)
                    f.prompt/build-chat-instructions (fn [& args]
                                                       (swap! build-calls* inc)
                                                       (apply real-build args))]
        (let [{:keys [chat-id]} (prompt! {:message "Hi"} mocks)]
          (is (= 1 @build-calls*))
          (h/reset-messenger!)
          (reset! skills* [{:name "my-skill" :description "Does things"}])
          (prompt! {:message "Changed" :chat-id chat-id} mocks)
          (is (= 1 @build-calls*) "Changed skills must not rebuild the pinned system prompt")
          (is (= ["System prompt changed (skills), keeping current chat system prompt, changes will apply to new chats. Use /sync-system-prompt to apply now.\n"]
                 (system-prompt-notices))
              "Notice tells changes apply to new chats")
          (h/reset-messenger!)
          (prompt! {:message "Again" :chat-id chat-id} mocks)
          (is (= 1 @build-calls*))
          (is (empty? (system-prompt-notices)) "Same drift does not re-notify every turn")
          (h/reset-messenger!)
          (prompt! {:message "New chat"} mocks)
          (is (= 2 @build-calls*) "A new chat picks up the changed system prompt"))))))

(deftest tools-changed-notice-test
  (testing "Tools drift notice tells changes already apply to the current chat"
    (h/reset-components!)
    (let [tools* (atom [])
          skills* (atom [])
          build-calls* (atom 0)
          real-build f.prompt/build-chat-instructions
          mocks {:all-tools-mock (fn [& _] @tools*)
                 :api-mock (fn [{:keys [on-message-received]}]
                             (on-message-received {:type :finish}))}]
      (with-redefs [f.skills/all (fn [& _] @skills*)
                    f.prompt/build-chat-instructions (fn [& args]
                                                       (swap! build-calls* inc)
                                                       (apply real-build args))]
        (let [{:keys [chat-id]} (prompt! {:message "Hi"} mocks)]
          (is (= 1 @build-calls*))
          (h/reset-messenger!)
          (reset! tools* [{:full-name "eca__new_tool"}])
          (prompt! {:message "Tool appeared" :chat-id chat-id} mocks)
          (is (= 1 @build-calls*) "Tools drift must not rebuild the pinned system prompt")
          (is (= ["Tools changed and already apply to this chat, keeping current chat system prompt text.\n"]
                 (system-prompt-notices))
              "Tools-only drift explains tools already apply")
          (h/reset-messenger!)
          (reset! skills* [{:name "my-skill" :description "Does things"}])
          (prompt! {:message "Skill too" :chat-id chat-id} mocks)
          (is (= 1 @build-calls*))
          (is (= ["System prompt changed (skills, tools), keeping current chat system prompt, changes will apply to new chats (tools already apply). Use /sync-system-prompt to apply now.\n"]
                 (system-prompt-notices))
              "Mixed drift keeps the tools clarification"))))))

(deftest sync-system-prompt-command-test
  (testing "/sync-system-prompt forces the pinned system prompt to be rebuilt on the next message"
    (h/reset-components!)
    (let [skills* (atom [])
          build-calls* (atom 0)
          real-build f.prompt/build-chat-instructions
          mocks {:all-tools-mock (constantly [])
                 :api-mock (fn [{:keys [on-message-received]}]
                             (on-message-received {:type :finish}))}]
      (with-redefs [f.skills/all (fn [& _] @skills*)
                    f.prompt/build-chat-instructions (fn [& args]
                                                       (swap! build-calls* inc)
                                                       (apply real-build args))]
        (let [{:keys [chat-id]} (prompt! {:message "Hi"} mocks)]
          (is (= 1 @build-calls*))
          (reset! skills* [{:name "my-skill" :description "Does things"}])
          (prompt! {:message "Changed" :chat-id chat-id} mocks)
          (is (= 1 @build-calls*) "Pinned: no rebuild before the sync command")
          (prompt! {:message "/sync-system-prompt" :chat-id chat-id} mocks)
          (is (nil? (get-in (h/db) [:chats chat-id :prompt-cache]))
              "Command clears the prompt cache")
          (h/reset-messenger!)
          (prompt! {:message "After sync" :chat-id chat-id} mocks)
          (is (= 2 @build-calls*) "Next message rebuilds the system prompt")
          (is (empty? (system-prompt-notices)) "Explicit sync rebuild does not notify"))))))

(deftest clear-chat-resets-prompt-cache-test
  (testing "Clearing a chat drops the prompt cache so a model switch does not warn (#530)"
    (h/reset-components!)
    (let [mocks {:all-tools-mock (constantly [])
                 :api-mock (fn [{:keys [on-message-received]}]
                             (on-message-received {:type :finish}))}
          {:keys [chat-id]} (prompt! {:message "Hi" :model "openai/gpt-5.2"} mocks)]
      (is (some? (get-in (h/db) [:chats chat-id :prompt-cache])))
      (f.chat/clear-chat {:chat-id chat-id :messages true} (h/db*) (h/messenger) (h/metrics))
      (is (nil? (get-in (h/db) [:chats chat-id :prompt-cache]))
          "Clearing messages drops the prompt cache")
      (swap! (h/db*) update :models #(merge % {"anthropic/claude-opus-4" {:tools true}}))
      (h/reset-messenger!)
      (prompt! {:message "Fresh" :chat-id chat-id :model "anthropic/claude-opus-4"} mocks)
      (is (empty? (system-prompt-notices))
          "No prompt cache invalidation notice on an empty chat"))))

(deftest prompt-cache-key-includes-agent-test
  (testing "sync-or-async-prompt! receives prompt-cache-key scoped by active agent"
    (h/reset-components!)
    (with-redefs [config/validate-agent-name (fn [agent-name _config] agent-name)]
      (let [captured* (atom [])
            mocks {:all-tools-mock (constantly [])
                   :api-mock (fn [{:keys [on-message-received] :as params}]
                               (swap! captured* conj (:prompt-cache-key params))
                               (on-message-received {:type :finish}))}
            {:keys [chat-id]} (prompt! {:message "hi" :agent "code"} mocks)]
        (h/reset-messenger!)
        (prompt! {:message "hello" :chat-id chat-id :agent "plan"} mocks)
        (is (= 2 (count @captured*)))
        (is (every? some? @captured*))
        (is (string/ends-with? (first @captured*) "/code")
            "First prompt's cache key should be suffixed by /code")
        (is (string/ends-with? (second @captured*) "/plan")
            "Second prompt's cache key should be suffixed by /plan")))))

(defn ^:private capturing-api-mock
  "Returns an api-mock that writes the incoming sync-or-async-prompt! params
   to `captured*` and emits a minimal finish event."
  [captured*]
  (fn [{:keys [on-first-response-received on-message-received] :as params}]
    (reset! captured* params)
    (on-first-response-received {:type :text :text "x"})
    (on-message-received {:type :text :text "x"})
    (on-message-received {:type :finish})))

(deftest resume-preserves-stored-model-test
  (testing "Stored chat :model wins over default when no explicit model is sent (#417)"
    (h/reset-components!)
    (let [captured* (atom nil)
          chat-id "opus-chat"]
      (swap! (h/db*) update :models
             #(merge % {"anthropic/claude-opus-4" {:tools true}}))
      ;; Pre-seed a chat as if it were resumed — stored model is opus.
      (swap! (h/db*) assoc-in [:chats chat-id]
             {:id chat-id
              :model "anthropic/claude-opus-4"
              :messages [{:role "user" :content [{:type :text :text "prior"}]}
                         {:role "assistant" :content [{:type :text :text "ok"}]}]})
      (prompt! {:message "follow up" :chat-id chat-id}
               {:all-tools-mock (constantly [])
                :api-mock (capturing-api-mock captured*)})
      (is (= "anthropic" (:provider @captured*)))
      (is (= "claude-opus-4" (:model @captured*)))
      (is (= "anthropic/claude-opus-4" (get-in (h/db) [:chats chat-id :model])))))

  (testing "Explicit request :model overrides stored chat :model"
    (h/reset-components!)
    (let [captured* (atom nil)
          chat-id "opus-chat"]
      (swap! (h/db*) update :models
             #(merge % {"anthropic/claude-opus-4" {:tools true}}))
      (swap! (h/db*) assoc-in [:chats chat-id]
             {:id chat-id
              :model "anthropic/claude-opus-4"
              :messages [{:role "user" :content [{:type :text :text "prior"}]}]})
      (prompt! {:message "switch" :chat-id chat-id :model "openai/gpt-5.2"}
               {:all-tools-mock (constantly [])
                :api-mock (capturing-api-mock captured*)})
      (is (= "openai" (:provider @captured*)))
      (is (= "gpt-5.2" (:model @captured*)))))

  (testing "Stale stored :model (missing from (:models db)) falls through to default-model"
    (h/reset-components!)
    (let [captured* (atom nil)
          chat-id "opus-chat"]
      ;; Only gpt-5.2 is configured (by `prompt!` helper); opus is no longer
      ;; available (e.g. provider removed).
      (swap! (h/db*) assoc-in [:chats chat-id]
             {:id chat-id
              :model "anthropic/claude-opus-4"
              :messages [{:role "user" :content [{:type :text :text "prior"}]}]})
      (prompt! {:message "follow up" :chat-id chat-id}
               {:all-tools-mock (constantly [])
                :api-mock (capturing-api-mock captured*)})
      (is (= "openai" (:provider @captured*)))
      (is (= "gpt-5.2" (:model @captured*))))))

(deftest open-chat-restores-selected-model-test
  (testing "Opening a chat with a stored :model emits config/updated select-model (#417)"
    (h/reset-components!)
    (let [chat-id "resumed"]
      (swap! (h/db*) update :models
             #(merge % {"anthropic/claude-opus-4" {:tools true}}))
      (swap! (h/db*) assoc-in [:chats chat-id]
             {:id chat-id
              :title "My Opus thread"
              :model "anthropic/claude-opus-4"
              :messages [{:role "user" :content [{:type :text :text "hi"}]}]})
      (let [result (f.chat/open-chat! {:chat-id chat-id}
                                      (h/db*) (h/messenger) (h/config))]
        (is (match? {:found true
                     :chat-id chat-id
                     :title "My Opus thread"
                     :selection {:model "anthropic/claude-opus-4"
                                 :agent "code"
                                 :variant nil
                                 :variants []
                                 :trust false}}
                    result))
        (is (match? {:config-updated [{:chat-id chat-id
                                       :chat {:select-model "anthropic/claude-opus-4"
                                              :variants []
                                              :select-variant nil}}
                                      {:chat-id chat-id
                                       :chat {:select-trust false}}]}
                    (h/messages)))
        (is (= {} (:last-config-notified (h/db)))))))

  (testing "Opening a chat returns its persisted agent and valid variant atomically"
    (h/reset-components!)
    (h/config! {:providers {"anthropic" {:models {"claude-sonnet-4-5"
                                                  {:variants {"low" {:effort "low"}
                                                              "high" {:effort "high"}}}}}}
                :defaultAgent "code"
                :agent {"code" {} "plan" {}}})
    (let [chat-id "planned"]
      (swap! (h/db*) assoc
             :models {"anthropic/claude-sonnet-4-5" {:tools true}}
             :chats {chat-id {:id chat-id
                              :model "anthropic/claude-sonnet-4-5"
                              :agent "plan"
                              :variant "low"
                              :trust true
                              :messages []}})
      (let [result (f.chat/open-chat! {:chat-id chat-id}
                                      (h/db*) (h/messenger) (h/config))]
        (is (= {:model "anthropic/claude-sonnet-4-5"
                :agent "plan"
                :variant "low"
                :variants ["high" "low"]
                :trust true}
               (:selection result))))))

  (testing "Opening a chat returns the default agent's valid variant when the persisted one is stale"
    (h/reset-components!)
    (h/config! {:providers {"anthropic" {:models {"claude-sonnet-4-5"
                                                  {:variants {"low" {:effort "low"}
                                                              "high" {:effort "high"}}}}}}
                :agent {"code" {:variant "high"}}})
    (let [chat-id "stale-variant"]
      (swap! (h/db*) assoc
             :models {"anthropic/claude-sonnet-4-5" {:tools true}}
             :chats {chat-id {:id chat-id
                              :model "anthropic/claude-sonnet-4-5"
                              :variant "max"
                              :messages []}})
      (let [result (f.chat/open-chat! {:chat-id chat-id}
                                      (h/db*) (h/messenger) (h/config))]
        (is (= "high" (get-in result [:selection :variant]))))))

  (testing "Opening a chat with no stored :model only emits trust config/updated"
    (h/reset-components!)
    (let [chat-id "no-model"]
      (swap! (h/db*) assoc-in [:chats chat-id]
             {:id chat-id
              :messages [{:role "user" :content [{:type :text :text "hi"}]}]})
      (let [result (f.chat/open-chat! {:chat-id chat-id}
                                      (h/db*) (h/messenger) (h/config))]
        (is (= {:model nil
                :agent "code"
                :variant nil
                :variants []
                :trust false}
               (:selection result))))
      (is (match? {:config-updated [{:chat-id chat-id
                                     :chat {:select-trust false}}]}
                  (h/messages)))))

  (testing "Opening a chat with a stale stored :model only emits trust config/updated"
    (h/reset-components!)
    (let [chat-id "stale-model"]
      ;; Opus not in (:models db), so the UI dropdown must not jump to a ghost.
      (swap! (h/db*) assoc-in [:chats chat-id]
             {:id chat-id
              :model "anthropic/claude-opus-4"
              :messages [{:role "user" :content [{:type :text :text "hi"}]}]})
      (let [result (f.chat/open-chat! {:chat-id chat-id}
                                      (h/db*) (h/messenger) (h/config))]
        (is (nil? (get-in result [:selection :model])))
        (is (= [] (get-in result [:selection :variants])))
        (is (nil? (get-in result [:selection :variant]))))
      (is (match? {:config-updated [{:chat-id chat-id
                                     :chat {:select-trust false}}]}
                  (h/messages))))))

(deftest open-chat-marks-editor-open-test
  (testing "Resuming a chat lists it on the remote endpoint without a prompt"
    (h/reset-components!)
    (let [chat-id "resumed"]
      (swap! (h/db*) assoc-in [:chats chat-id]
             {:id chat-id
              :messages [{:role "user" :content [{:type :text :text "hi"}]}]})
      (is (not (contains? (:editor-open-chats @(h/db*)) chat-id)))
      (f.chat/open-chat! {:chat-id chat-id} (h/db*) (h/messenger) (h/config))
      (is (contains? (:editor-open-chats @(h/db*)) chat-id)))))

(deftest open-chat-restores-selected-trust-test
  (testing "Opening a trusted chat emits config/updated select-trust true (#426)"
    (h/reset-components!)
    (let [chat-id "trusted"]
      (swap! (h/db*) assoc-in [:chats chat-id]
             {:id chat-id
              :title "YOLO thread"
              :trust true
              :messages [{:role "user" :content [{:type :text :text "hi"}]}]})
      (f.chat/open-chat! {:chat-id chat-id} (h/db*) (h/messenger) (h/config))
      (is (match? {:config-updated [{:chat-id chat-id
                                     :chat {:select-trust true}}]}
                  (h/messages)))))

  (testing "Opening a non-trusted chat emits config/updated select-trust false (#426)"
    (h/reset-components!)
    (let [chat-id "secured"]
      ;; Pre-seed last-config-notified so the diff actually picks up false.
      (swap! (h/db*) assoc :last-config-notified {:chat {:select-trust true}})
      (swap! (h/db*) assoc-in [:chats chat-id]
             {:id chat-id
              :trust false
              :messages [{:role "user" :content [{:type :text :text "hi"}]}]})
      (f.chat/open-chat! {:chat-id chat-id} (h/db*) (h/messenger) (h/config))
      (is (match? {:config-updated [{:chat-id chat-id
                                     :chat {:select-trust false}}]}
                  (h/messages)))))

  (testing "Opening a chat with no :trust key normalizes to false (#426)"
    (h/reset-components!)
    (let [chat-id "legacy"]
      (swap! (h/db*) assoc :last-config-notified {:chat {:select-trust true}})
      (swap! (h/db*) assoc-in [:chats chat-id]
             {:id chat-id
              :messages [{:role "user" :content [{:type :text :text "hi"}]}]})
      (f.chat/open-chat! {:chat-id chat-id} (h/db*) (h/messenger) (h/config))
      (is (match? {:config-updated [{:chat-id chat-id
                                     :chat {:select-trust false}}]}
                  (h/messages))))))

(deftest fetch-history-test
  (let [text-msg (fn [text n] {:role "user" :content [{:type :text :text text}] :created-at n})
        seed! (fn [chat-id msgs]
                (h/reset-components!)
                (swap! (h/db*) assoc-in [:chats chat-id] {:id chat-id :messages msgs})
                chat-id)
        content-texts (fn [{:keys [contents]}]
                        (keep #(some-> (get-in % [:content :text]) string/trim) contents))]
    (testing "unknown chat returns an error"
      (h/reset-components!)
      (is (= "chat_not_found" (get-in (f.chat/fetch-history {:chat-id "nope"} (h/db*)) [:error :code]))))

    (testing "subagent chat is not fetchable"
      (h/reset-components!)
      (swap! (h/db*) assoc-in [:chats "sub"] {:id "sub" :subagent true :messages []})
      (is (= "chat_not_found" (get-in (f.chat/fetch-history {:chat-id "sub"} (h/db*)) [:error :code]))))

    (testing "returns transformed content items plus meta"
      (let [chat-id (seed! "c1" [(text-msg "m0" 1) (text-msg "m1" 2) (text-msg "m2" 3)])
            result (f.chat/fetch-history {:chat-id chat-id :limit 2} (h/db*))]
        (is (= ["m1" "m2"] (content-texts result)))
        (is (every? #(= chat-id (:chat-id %)) (:contents result)))
        (is (= 3 (get-in result [:meta :total])))
        (is (= 2 (get-in result [:meta :returned])))
        (is (some? (get-in result [:meta :before-cursor])))
        (is (nil? (get-in result [:meta :after-cursor])))))

    (testing "before cursor pages older"
      (let [chat-id (seed! "c1" [(text-msg "m0" 1) (text-msg "m1" 2) (text-msg "m2" 3)])
            before (get-in (f.chat/fetch-history {:chat-id chat-id :limit 2} (h/db*)) [:meta :before-cursor])
            result (f.chat/fetch-history {:chat-id chat-id :limit 2 :before before} (h/db*))]
        ;; first page was [m1 m2]; older-than-m1 is just [m0]
        (is (= ["m0"] (content-texts result)))))

    (testing "after=lastCompaction returns the active context with a compaction cursor"
      (let [chat-id (seed! "c1" [(text-msg "m0" 1)
                                 {:role "compact_marker" :content {:auto? false} :created-at 2}
                                 (text-msg "summary" 3)
                                 (text-msg "m3" 4)])
            result (f.chat/fetch-history {:chat-id chat-id :after "lastCompaction"} (h/db*))]
        (is (= ["summary" "m3"] (content-texts result)))
        (is (some? (get-in result [:meta :compaction-cursor])))))

    (testing "stale cursor returns cursor_expired"
      (let [chat-id (seed! "c1" [(text-msg "m0" 1)])]
        (is (= "cursor_expired"
               (get-in (f.chat/fetch-history {:chat-id chat-id :before "bogus"} (h/db*)) [:error :code])))))))

(deftest open-chat-windowed-test
  (let [msg (fn [t n] {:role "user" :content [{:type :text :text t}] :created-at n})
        replayed-texts (fn []
                         (->> (h/messages) :chat-content-received
                              (keep #(some-> (get-in % [:content :text]) string/trim))
                              vec))]
    (testing "no window params replays the full history with no meta"
      (h/reset-components!)
      (swap! (h/db*) assoc-in [:chats "full"]
             {:id "full" :messages [(msg "m0" 1) (msg "m1" 2) (msg "m2" 3)]})
      (let [result (f.chat/open-chat! {:chat-id "full"} (h/db*) (h/messenger) (h/config))]
        (is (nil? (:meta result)))
        (is (= ["m0" "m1" "m2"] (replayed-texts)))))

    (testing "limit replays only the window and returns meta"
      (h/reset-components!)
      (swap! (h/db*) assoc-in [:chats "win"]
             {:id "win" :messages [(msg "m0" 1) (msg "m1" 2) (msg "m2" 3)]})
      (let [result (f.chat/open-chat! {:chat-id "win" :limit 2} (h/db*) (h/messenger) (h/config))]
        (is (match? {:found true :chat-id "win"
                     :meta {:total 3 :returned 2 :after-cursor nil}}
                    result))
        (is (some? (get-in result [:meta :before-cursor])))
        (is (= ["m1" "m2"] (replayed-texts)))))

    (testing "expired cursor errors before any replay"
      (h/reset-components!)
      (swap! (h/db*) assoc-in [:chats "exp"]
             {:id "exp" :messages [(msg "m0" 1)]})
      (let [result (f.chat/open-chat! {:chat-id "exp" :before "bogus"} (h/db*) (h/messenger) (h/config))]
        (is (= "cursor_expired" (get-in result [:error :code])))
        (is (= {} (h/messages)) "no notifications emitted on expired cursor")))))

(deftest prompt-cache-agent-and-model-switch-test
  (testing "local static prompt cache is reused only when both agent and model match"
    (h/reset-components!)
    (h/config! {:env "test"
                :agent {"code" {:mode "primary"}
                        "plan" {:mode "primary"}}})
    (swap! (h/db*) update :models
           (fn [models]
             (merge {"openai/gpt-5.2" {:tools true}
                     "openai/gpt-4.1" {:tools true}}
                    (or models {}))))
    (let [build-calls* (atom 0)
          api-mock (fn [{:keys [on-first-response-received on-message-received]}]
                     (on-first-response-received {:type :text :text "ok"})
                     (on-message-received {:type :text :text "ok"})
                     (on-message-received {:type :finish}))
          call-prompt! (fn [params]
                         (with-redefs [llm-api/sync-or-async-prompt! api-mock
                                       llm-api/sync-prompt! (constantly nil)
                                       f.tools/call-tool! (constantly nil)
                                       f.tools/all-tools (constantly [])
                                       f.tools/approval (constantly :allow)
                                       config/await-plugins-resolved! (constantly true)
                                       f.prompt/build-chat-instructions (fn [& _]
                                                                          (swap! build-calls* inc)
                                                                          {:static (str "static-" @build-calls*)
                                                                           :dynamic "dynamic"})]
                           (f.chat/prompt params (h/db*) (h/messenger) (h/config) (h/metrics))))
          resp (call-prompt! {:message "Hello"
                              :agent "code"
                              :model "openai/gpt-5.2"})
          chat-id (:chat-id resp)]
      (is (match? {:chat-id string? :status :prompting} resp))
      (is (= 1 @build-calls*) "First call should build instructions")

      (h/reset-messenger!)
      (call-prompt! {:message "Hello again"
                     :chat-id chat-id
                     :agent "code"
                     :model "openai/gpt-5.2"})
      (is (= 1 @build-calls*) "Same agent and model should reuse cached static instructions")

      (h/reset-messenger!)
      (call-prompt! {:message "Switch agent"
                     :chat-id chat-id
                     :agent "plan"
                     :model "openai/gpt-5.2"})
      (is (= 2 @build-calls*) "Changing agent should rebuild instructions")

      (h/reset-messenger!)
      (call-prompt! {:message "Switch model"
                     :chat-id chat-id
                     :agent "plan"
                     :model "openai/gpt-4.1"})
      (is (= 3 @build-calls*) "Changing model should rebuild instructions")
      (let [sha? (m/pred #(re-matches #"[0-9a-f]{64}" %))]
        (is (match? {:static "static-3"
                     :static-signature {:prompt sha?
                                        :contexts sha?
                                        :rules sha?
                                        :skills sha?
                                        :tools sha?}
                     :agent "plan"
                     :model "openai/gpt-4.1"}
                    (get-in (h/db) [:chats chat-id :prompt-cache])))))))

(deftest message-content->chat-content-image-test
  (testing "image_generation_call role replays as a single :image ChatContent under assistant"
    ;; Mirrors the shape persisted by the `:image` branch of `:on-message-received`:
    ;; {:role "image_generation_call" :content {:id ... :media-type ... :base64 ...}}
    ;; (single-map content, similar to tool_call). This is the role used for
    ;; OpenAI-emitted images and is the canonical history shape for replay.
    (is (match?
         [{:role :assistant
           :content {:type :image
                     :media-type "image/png"
                     :base64 "AAA"}}]
         (#'f.chat/message-content->chat-content
          "image_generation_call"
          {:id "ig_1" :media-type "image/png" :base64 "AAA"}
          nil))))
  (testing "User-role :image content (e.g. attached via ImageContext) replays as a single :image ChatContent"
    ;; ImageContext attachments (clients without filesystem access) flow as
    ;; {:role "user" :content [{:type :image ...}]} and must still surface
    ;; as ChatImageContent on chat replay.
    (is (match?
         [{:role "user"
           :content {:type :image
                     :media-type "image/png"
                     :base64 "BBB"}}]
         (#'f.chat/message-content->chat-content
          "user"
          [{:type :image :media-type "image/png" :base64 "BBB"}]
          nil))))
  (testing "User-role mixed text + image history collapses text and emits one image content"
    (let [result (#'f.chat/message-content->chat-content
                  "user"
                  [{:type :text :text "look at this:"}
                   {:type :image :media-type "image/png" :base64 "CCC"}]
                  nil)]
      (is (= 2 (count result)))
      (is (= :text (get-in (first result) [:content :type])))
      (is (= :image (get-in (second result) [:content :type])))
      (is (= "CCC" (get-in (second result) [:content :base64])))))
  (testing "Multiple user-role images each emit their own ChatContent"
    (is (match?
         [{:role "user" :content {:type :image :base64 "X"}}
          {:role "user" :content {:type :image :base64 "Y"}}]
         (#'f.chat/message-content->chat-content
          "user"
          [{:type :image :media-type "image/png" :base64 "X"}
           {:type :image :media-type "image/png" :base64 "Y"}]
          nil))))
  (testing "Pure text history entry still produces one collapsed text ChatContent (no regression)"
    (is (match?
         [{:role "user"
           :content {:type :text}}]
         (#'f.chat/message-content->chat-content
          "user"
          [{:type :text :text "hello"}]
          nil)))))

(deftest prompt-with-client-supplied-chat-id-test
  (testing "Client-supplied unknown chat-id is accepted and emits chat/opened once"
    (h/reset-components!)
    (let [client-id "client-supplied-1234"
          {:keys [chat-id]}
          (prompt!
           {:message "Hey!" :chat-id client-id}
           {:all-tools-mock (constantly [])
            :api-mock
            (fn [{:keys [on-first-response-received on-message-received]}]
              (on-first-response-received {:type :text :text "Hey"})
              (on-message-received {:type :text :text "Hey"})
              (on-message-received {:type :finish}))})]
      (is (= client-id chat-id))
      (is (match? {client-id {:id client-id}} (:chats (h/db))))
      (is (match? [{:chat-id client-id}] (:chat-opened (h/messages))))))
  (testing "Second prompt on same client-supplied id does not re-emit chat/opened"
    (h/reset-messenger!)
    (let [client-id "client-supplied-1234"
          {:keys [chat-id]}
          (prompt!
           {:message "follow up" :chat-id client-id}
           {:all-tools-mock (constantly [])
            :api-mock
            (fn [{:keys [on-first-response-received on-message-received]}]
              (on-first-response-received {:type :text :text "ok"})
              (on-message-received {:type :text :text "ok"})
              (on-message-received {:type :finish}))})]
      (is (= client-id chat-id))
      (is (nil? (:chat-opened (h/messages)))))))

(deftest prompt-without-chat-id-does-not-emit-chat-opened-test
  (testing "Legacy null-id path mints a server-side id and does NOT emit chat/opened"
    (h/reset-components!)
    (let [{:keys [chat-id]}
          (prompt!
           {:message "Hey!"}
           {:all-tools-mock (constantly [])
            :api-mock
            (fn [{:keys [on-first-response-received on-message-received]}]
              (on-first-response-received {:type :text :text "Hey"})
              (on-message-received {:type :text :text "Hey"})
              (on-message-received {:type :finish}))})]
      (is (string? chat-id))
      (is (nil? (:chat-opened (h/messages)))))))

(deftest prompt-rejects-invalid-client-chat-id-test
  (testing "Blank chat-id is rejected without seeding state"
    (h/reset-components!)
    (let [resp (f.chat/prompt {:message "hi" :chat-id ""}
                              (h/db*) (h/messenger) (h/config) (h/metrics))]
      (is (match? {:status :error :model "error"} resp))
      (is (= {} (:chats (h/db))))
      (is (nil? (:chat-opened (h/messages))))))
  (testing "Reserved subagent- prefix is rejected"
    (h/reset-components!)
    (let [resp (f.chat/prompt {:message "hi" :chat-id "subagent-foo"}
                              (h/db*) (h/messenger) (h/config) (h/metrics))]
      (is (match? {:chat-id "subagent-foo" :status :error} resp))
      (is (= {} (:chats (h/db))))
      (is (nil? (:chat-opened (h/messages))))))
  (testing "Server-managed subagent chat-id is accepted"
    (h/reset-components!)
    (let [chat-id "subagent-foo"]
      (swap! (h/db*) assoc-in [:chats chat-id] {:id chat-id :subagent {:mode "subagent"}})
      (is (= {:chat-id chat-id}
             (prompt!
              {:message "hi" :chat-id chat-id}
              {:all-tools-mock (constantly [])
               :api-mock
               (fn [{:keys [on-first-response-received on-message-received]}]
                 (on-first-response-received {:type :text :text "ok"})
                 (on-message-received {:type :text :text "ok"})
                 (on-message-received {:type :finish}))})))))
  (testing "Excessively long chat-id is rejected"
    (h/reset-components!)
    (let [too-long (apply str (repeat 257 "a"))
          resp (f.chat/prompt {:message "hi" :chat-id too-long}
                              (h/db*) (h/messenger) (h/config) (h/metrics))]
      (is (match? {:status :error} resp))
      (is (= {} (:chats (h/db))))))
  (testing "Embedded whitespace and control characters are rejected"
    (doseq [bad ["abc\n" "a b" "a\tb" "a\u0000b" " leading" "trailing "]]
      (h/reset-components!)
      (let [resp (f.chat/prompt {:message "hi" :chat-id bad}
                                (h/db*) (h/messenger) (h/config) (h/metrics))]
        (is (match? {:status :error} resp)
            (str "expected " (pr-str bad) " to be rejected"))
        (is (= {} (:chats (h/db))))))))

(defn ^:private inline-prompt! [params mocks]
  (with-redefs [llm-api/sync-or-async-prompt! (:api-mock mocks)
                llm-api/sync-prompt! (constantly nil)
                f.tools/call-tool! (:call-tool-mock mocks)
                f.tools/all-tools (:all-tools-mock mocks)
                f.tools/approval (constantly :allow)
                config/await-plugins-resolved! (constantly true)]
    (h/config! {:env "test"})
    (swap! (h/db*) update :models
           (fn [models]
             (merge {"openai/gpt-5.2" {:tools true}}
                    (or models {}))))
    (f.chat/inline-prompt params (h/db*) (h/messenger) (h/config) (h/metrics))))

(def ^:private inline-finish-mock
  (fn [{:keys [on-first-response-received on-message-received]}]
    (on-first-response-received {:type :text :text "ok"})
    (on-message-received {:type :text :text "ok"})
    (on-message-received {:type :finish})))

(deftest inline-prompt-new-chat-test
  (testing "new inline chat is seeded with kind, title and emits chat/opened"
    (h/reset-components!)
    (let [resp (inline-prompt! {:chat-id "inline-1" :message "what is foo?"}
                               {:all-tools-mock (constantly [])
                                :api-mock inline-finish-mock})]
      (is (match? {:chat-id "inline-1" :status :prompting} resp))
      (is (match? {:kind :inline
                   :title "inline: what is foo?"}
                  (get-in (h/db) [:chats "inline-1"])))
      (is (match? [{:chat-id "inline-1" :title "inline: what is foo?"}]
                  (:chat-opened (h/messages))))))
  (testing "long messages are truncated in the title"
    (h/reset-components!)
    (let [message (apply str (repeat 30 "abc"))
          _ (inline-prompt! {:chat-id "inline-2" :message message}
                            {:all-tools-mock (constantly [])
                             :api-mock inline-finish-mock})]
      (is (= (str "inline: " (subs message 0 40) "...")
             (get-in (h/db) [:chats "inline-2" :title])))))
  (testing "follow-up on the same id does not re-seed nor re-emit chat/opened"
    (h/reset-components!)
    (let [_ (inline-prompt! {:chat-id "inline-3" :message "first"}
                            {:all-tools-mock (constantly [])
                             :api-mock inline-finish-mock})
          title-before (get-in (h/db) [:chats "inline-3" :title])
          _ (h/reset-messenger!)
          resp (inline-prompt! {:chat-id "inline-3" :message "second"}
                               {:all-tools-mock (constantly [])
                                :api-mock inline-finish-mock})]
      (is (match? {:chat-id "inline-3" :status :prompting} resp))
      (is (= title-before (get-in (h/db) [:chats "inline-3" :title])))
      (is (nil? (:chat-opened (h/messages))))
      (is (match? (m/embeds [{:role "user" :content [{:type :text :text "first"}]}
                             {:role "user" :content [{:type :text :text "second"}]}])
                  (get-in (h/db) [:chats "inline-3" :messages]))))))

(deftest inline-prompt-fork-test
  (testing "fork copies source history server-side without replaying it to the client"
    (h/reset-components!)
    (swap! (h/db*) assoc-in [:chats "source-1"]
           {:id "source-1"
            :model "openai/gpt-5.2"
            :variant "high"
            :trust true
            :messages [{:role "user" :content [{:type :text :text "original question"}]}
                       {:role "assistant" :content [{:type :text :text "original answer"}]}
                       ;; dangling tool call without output, as mid-run forks see
                       {:role "tool_call" :content {:id "tc-1" :name "shell"}}]})
    (let [resp (inline-prompt! {:chat-id "inline-fork-1"
                                :source-chat-id "source-1"
                                :message "btw what is bar?"}
                               {:all-tools-mock (constantly [])
                                :api-mock inline-finish-mock})]
      (is (match? {:chat-id "inline-fork-1" :status :prompting} resp))
      (is (match? {:kind :inline
                   :model "openai/gpt-5.2"
                   :variant "high"
                   :trust true}
                  (get-in (h/db) [:chats "inline-fork-1"])))
      (testing "copied history + new turn, dangling tool call dropped"
        (is (match? (m/embeds [{:role "user" :content [{:type :text :text "original question"}]}
                               {:role "assistant" :content [{:type :text :text "original answer"}]}
                               {:role "user" :content [{:type :text :text "btw what is bar?"}]}])
                    (get-in (h/db) [:chats "inline-fork-1" :messages])))
        (is (not-any? #(= "tool_call" (:role %))
                      (get-in (h/db) [:chats "inline-fork-1" :messages]))))
      (testing "source chat untouched by the new turn"
        (is (= 3 (count (get-in (h/db) [:chats "source-1" :messages])))))
      (testing "copied history is not replayed via chat/contentReceived"
        (is (not-any? (fn [{:keys [chat-id content]}]
                        (and (= "inline-fork-1" chat-id)
                             (= "original question" (:text content))))
                      (:chat-content-received (h/messages)))))
      (testing "copied trust emits per-chat select-trust"
        (is (match? {:config-updated (m/embeds [{:chat {:select-trust true}
                                                 :chat-id "inline-fork-1"}])}
                    (h/messages))))
      (testing "variant stays sticky on follow-ups"
        (inline-prompt! {:chat-id "inline-fork-1" :message "follow up"}
                        {:all-tools-mock (constantly [])
                         :api-mock inline-finish-mock})
        (is (= "high" (get-in (h/db) [:chats "inline-fork-1" :variant])))))))

(deftest inline-prompt-model-resolution-test
  (testing "chatInline.model config wins over the source chat's model"
    (h/reset-components!)
    (h/config! {:chatInline {:model "openai/inline-model"}})
    (swap! (h/db*) assoc-in [:models "openai/inline-model"] {:tools true})
    (swap! (h/db*) assoc-in [:chats "source-2"]
           {:id "source-2" :model "openai/gpt-5.2" :messages []})
    (let [resp (inline-prompt! {:chat-id "inline-model-1"
                                :source-chat-id "source-2"
                                :message "hi"}
                               {:all-tools-mock (constantly [])
                                :api-mock inline-finish-mock})]
      (is (match? {:model "openai/inline-model"} resp))))
  (testing "explicit model param wins over chatInline.model"
    (h/reset-components!)
    (h/config! {:chatInline {:model "openai/inline-model"}})
    (swap! (h/db*) assoc-in [:models "openai/explicit-model"] {:tools true})
    (let [resp (inline-prompt! {:chat-id "inline-model-2"
                                :message "hi"
                                :model "openai/explicit-model"}
                               {:all-tools-mock (constantly [])
                                :api-mock inline-finish-mock})]
      (is (match? {:model "openai/explicit-model"} resp))))
  (testing "fork keeps the source chat's model when no config is set"
    (h/reset-components!)
    (swap! (h/db*) assoc-in [:chats "source-3"]
           {:id "source-3" :model "openai/gpt-5.2" :messages []})
    (let [resp (inline-prompt! {:chat-id "inline-model-3"
                                :source-chat-id "source-3"
                                :message "hi"}
                               {:all-tools-mock (constantly [])
                                :api-mock inline-finish-mock})]
      (is (match? {:model "openai/gpt-5.2"} resp))))
  (testing "chatInline.agent default drives model via the agent's defaultModel"
    (h/reset-components!)
    (h/config! {:chatInline {:agent "helper"}
                :agent {"helper" {:defaultModel "openai/helper-model"}}})
    (swap! (h/db*) assoc-in [:models "openai/helper-model"] {:tools true})
    (let [resp (inline-prompt! {:chat-id "inline-agent-1" :message "hi"}
                               {:all-tools-mock (constantly [])
                                :api-mock inline-finish-mock})]
      (is (match? {:model "openai/helper-model"} resp))))
  (testing "chatInline defaults do not apply to regular chats"
    (h/reset-components!)
    (h/config! {:chatInline {:model "openai/inline-model"}})
    (swap! (h/db*) assoc-in [:chats "regular-1"] {:id "regular-1" :messages []})
    (let [resp (inline-prompt! {:chat-id "regular-1" :message "hi"}
                               {:all-tools-mock (constantly [])
                                :api-mock inline-finish-mock})]
      (is (match? {:model "openai/gpt-5.2"} resp))
      (is (nil? (get-in (h/db) [:chats "regular-1" :kind]))))))

(deftest inline-prompt-trust-test
  (testing "chat.defaultTrust seeds trust on new inline chats"
    (h/reset-components!)
    (h/config! {:chat {:defaultTrust true}})
    (let [_ (inline-prompt! {:chat-id "inline-trust-1" :message "hi"}
                            {:all-tools-mock (constantly [])
                             :api-mock inline-finish-mock})]
      (is (true? (get-in (h/db) [:chats "inline-trust-1" :trust])))
      (is (match? {:config-updated (m/embeds [{:chat {:select-trust true}
                                               :chat-id "inline-trust-1"}])}
                  (h/messages)))))
  (testing "explicit trust param wins over chat.defaultTrust"
    (h/reset-components!)
    (h/config! {:chat {:defaultTrust true}})
    (let [_ (inline-prompt! {:chat-id "inline-trust-2" :message "hi" :trust false}
                            {:all-tools-mock (constantly [])
                             :api-mock inline-finish-mock})]
      (is (false? (get-in (h/db) [:chats "inline-trust-2" :trust]))))))

(deftest inline-prompt-invalid-params-test
  (testing "missing chatId is rejected without seeding state"
    (h/reset-components!)
    (let [resp (f.chat/inline-prompt {:message "hi"}
                                     (h/db*) (h/messenger) (h/config) (h/metrics))]
      (is (match? {:status :error :model "error"} resp))
      (is (= {} (:chats (h/db))))
      (is (nil? (:chat-opened (h/messages))))))
  (testing "invalid chatId is rejected"
    (h/reset-components!)
    (let [resp (f.chat/inline-prompt {:chat-id "subagent-foo" :message "hi"}
                                     (h/db*) (h/messenger) (h/config) (h/metrics))]
      (is (match? {:chat-id "subagent-foo" :status :error} resp))
      (is (= {} (:chats (h/db))))))
  (testing "invalid sourceChatId is rejected"
    (h/reset-components!)
    (let [resp (f.chat/inline-prompt {:chat-id "inline-x" :source-chat-id "a b" :message "hi"}
                                     (h/db*) (h/messenger) (h/config) (h/metrics))]
      (is (match? {:status :error} resp))
      (is (= {} (:chats (h/db))))))
  (testing "blank message is rejected without seeding state"
    (h/reset-components!)
    (doseq [message [nil "" "   "]]
      (let [resp (f.chat/inline-prompt {:chat-id "inline-y" :message message}
                                       (h/db*) (h/messenger) (h/config) (h/metrics))]
        (is (match? {:status :error} resp))
        (is (= {} (:chats (h/db))))
        (is (nil? (:chat-opened (h/messages))))))))

(deftest inline-prompt-source-not-found-test
  (testing "nonexistent sourceChatId starts an empty inline chat"
    (h/reset-components!)
    (let [resp (inline-prompt! {:chat-id "inline-orphan"
                                :source-chat-id "gone-1"
                                :message "hi"}
                               {:all-tools-mock (constantly [])
                                :api-mock inline-finish-mock})]
      (is (match? {:chat-id "inline-orphan" :status :prompting} resp))
      (is (match? {:kind :inline} (get-in (h/db) [:chats "inline-orphan"])))
      ;; no copied history: the first message is the new question itself
      (is (match? {:role "user" :content [{:text "hi"}]}
                  (first (get-in (h/db) [:chats "inline-orphan" :messages])))))))

(deftest inline-prompt-variant-config-test
  (testing "chatInline.variant config seeds the new inline chat's variant"
    (h/reset-components!)
    (h/config! {:chatInline {:variant "high"}})
    (let [_ (inline-prompt! {:chat-id "inline-variant-1" :message "hi"}
                            {:all-tools-mock (constantly [])
                             :api-mock inline-finish-mock})]
      (is (= "high" (get-in (h/db) [:chats "inline-variant-1" :variant]))))))

(deftest fork-chat-response-test
  (h/reset-components!)
  (swap! (h/db*) assoc-in [:chats "c1"]
         {:id "c1"
          :title "Orig"
          :messages [{:role "user"
                      :content [{:type :text :text "q"}]
                      :content-id "m1"}]})
  (testing "returns the new chat id"
    (let [{:keys [chat-id]} (f.chat/fork-chat {:chat-id "c1" :content-id "m1"}
                                              (h/db*) (h/messenger) (h/metrics))]
      (is (string? chat-id))
      (is (some? (get-in (h/db) [:chats chat-id])))))
  (testing "unknown content-id returns an empty map"
    (is (= {} (f.chat/fork-chat {:chat-id "c1" :content-id "nope"}
                                (h/db*) (h/messenger) (h/metrics))))))

(deftest tool-call-approve-on-unknown-chat-is-noop-test
  (testing "Approving a tool-call against an unknown chat does not throw or mutate state"
    (h/reset-components!)
    (f.chat/tool-call-approve {:chat-id "unknown-chat" :tool-call-id "t1"}
                              (h/db*) (h/messenger) (h/config) (h/metrics))
    (is (= {} (:chats (h/db))))
    (is (= {} (h/messages)))))

(deftest tool-call-reject-on-unknown-chat-is-noop-test
  (testing "Rejecting a tool-call against an unknown chat does not throw or mutate state"
    (h/reset-components!)
    (f.chat/tool-call-reject {:chat-id "unknown-chat" :tool-call-id "t1"}
                             (h/db*) (h/messenger) (h/config) (h/metrics))
    (is (= {} (:chats (h/db))))
    (is (= {} (h/messages)))))

(deftest resolve-full-model-alias-test
  (let [db {:models {"company-litellm/big" {}
                     "company-litellm/explorer-small" {}
                     "github-copilot/explorer-small" {}
                     "anthropic/claude-sonnet-4-6" {}}
            :chats {"main" {:id "main" :model "company-litellm/big"}
                    "sub" {:id "sub" :parent-chat-id "main"}}}
        resolve-model (fn [requested chat-id agent-config]
                        (#'f.chat/resolve-full-model requested db chat-id agent-config {}))]
    (testing "explicit bare alias resolves against the chat's selected provider"
      (is (= "company-litellm/explorer-small"
             (resolve-model "explorer-small" "main" {}))))
    (testing "subagent resolves a bare alias against its parent chat's provider"
      (is (= "company-litellm/explorer-small"
             (resolve-model "explorer-small" "sub" {}))))
    (testing "agent defaultModel bare alias resolves via the selected provider"
      (is (= "company-litellm/explorer-small"
             (resolve-model nil "sub" {:defaultModel "explorer-small"}))))
    (testing "a literal full model id is returned as-is"
      (is (= "anthropic/claude-sonnet-4-6"
             (resolve-model "anthropic/claude-sonnet-4-6" "main" {}))))
    (testing "an unknown explicit model id is kept verbatim (back-compat)"
      (is (= "totally-unknown"
             (resolve-model "totally-unknown" "main" {}))))
    (testing "a stored full model still wins over the agent defaultModel"
      (is (= "company-litellm/big"
             (resolve-model nil "main" {:defaultModel "explorer-small"}))))))

