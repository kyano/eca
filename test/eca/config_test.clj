(ns eca.config-test
  (:require
   [babashka.fs :as fs]
   [cheshire.core :as json]
   [clojure.java.io :as io]
   [clojure.string :as string]
   [clojure.test :refer [deftest is testing]]
   [eca.config :as config]
   [eca.interpolation :as interpolation]
   [eca.logger :as logger]
   [eca.secrets :as secrets]
   [eca.shared :as shared]
   [eca.test-helper :as h]
   [matcher-combinators.matchers :as m]
   [matcher-combinators.test :refer [match?]]))

(h/reset-components-before-test)

(deftest all-cache-test
  (testing "resolves once for different db values sharing the same workspace-folders"
    (let [calls* (atom 0)
          folders [{:uri "file:///ws"}]]
      (config/clear-cache!)
      (try
        (with-redefs-fn {#'config/all* (fn [db]
                                         (swap! calls* inc)
                                         {:resolved-for (:workspace-folders db)})}
          (fn []
            (is (= {:resolved-for folders}
                   (config/all {:workspace-folders folders :chats {"a" {}}})))
            (is (= {:resolved-for folders}
                   (config/all {:workspace-folders folders :chats {"b" {}} :config-hash 1})))
            (is (= 1 @calls*))
            (config/all {:workspace-folders [{:uri "file:///other"}]})
            (is (= 2 @calls*))
            (config/clear-cache!)
            (config/all {:workspace-folders folders})
            (is (= 3 @calls*))))
        (finally
          (config/clear-cache!))))))

(deftest all-test
  (testing "Default config"
    (reset! config/initialization-config* {:pureConfig true})
    (is (match?
         {:pureConfig true
          :providers {"github-copilot" {:key nil
                                        :models (m/equals {})}}}
         (#'config/all* {}))))
  (testing "deep merging initializationOptions with initial config"
    (reset! config/initialization-config* {:pureConfig true
                                           :providers {"githubCopilot" {:key "123"}}})
    (is (match?
         {:pureConfig true
          :providers {"github-copilot" {:key "123"
                                        :models (m/equals {})}}}
         (#'config/all* {}))))
  (testing "providers and models are updated correctly"
    (reset! config/initialization-config* {:pureConfig true
                                           :providers {"customProvider" {:key "123"
                                                                         :models {:gpt-5 {}}}
                                                       "openrouter" {:models {"openai/o4-mini" {}}}}})
    (is (match?
         {:pureConfig true
          :providers {"custom-provider" {:key "123"
                                         :models {"gpt-5" {}}}
                      "openrouter" {:models {"openai/o4-mini" {}}}}}
         (#'config/all* {})))))

(deftest default-trust-config-test
  (testing "chat.defaultTrust defaults to false"
    (reset! config/initialization-config* {:pureConfig true})
    (is (match? {:chat {:defaultTrust false}}
                (#'config/all* {}))))
  (testing "chat.defaultTrust can be enabled via config"
    (reset! config/initialization-config* {:pureConfig true
                                           :chat {:defaultTrust true}})
    (is (match? {:chat {:defaultTrust true}}
                (#'config/all* {})))))

(deftest extra-configs-test
  (testing "extraConfigs is deep merged last, overriding earlier sources"
    (let [extra (fs/file (fs/create-temp-dir) "extra.json")]
      (spit extra (json/generate-string {:defaultAgent "from-extra"
                                         :chat {:defaultTrust true}}))
      (reset! config/initialization-config* {:pureConfig true
                                             :defaultAgent "plan"
                                             :chat {:defaultTrust false}
                                             :extraConfigs [(str extra)]})
      (let [result (#'config/all* {})]
        (is (= "from-extra" (:defaultAgent result)))
        (is (true? (get-in result [:chat :defaultTrust]))))))

  (testing "later extraConfigs entries win over earlier ones"
    (let [dir (fs/create-temp-dir)
          first-file (fs/file dir "first.json")
          second-file (fs/file dir "second.json")]
      (spit first-file (json/generate-string {:defaultAgent "first"}))
      (spit second-file (json/generate-string {:defaultAgent "second"}))
      (reset! config/initialization-config* {:pureConfig true
                                             :extraConfigs [(str first-file) (str second-file)]})
      (is (= "second" (:defaultAgent (#'config/all* {}))))))

  (testing "missing extraConfigs paths are skipped without throwing"
    (reset! config/initialization-config* {:pureConfig true
                                           :defaultAgent "plan"
                                           :extraConfigs ["/this/path/does/not/exist.json"]})
    (is (= "plan" (:defaultAgent (#'config/all* {})))))

  (testing "relative extraConfigs paths resolve against the workspace root"
    (let [dir (fs/create-temp-dir)]
      (spit (fs/file dir "rel.json") (json/generate-string {:defaultAgent "from-rel"}))
      (reset! config/initialization-config* {:pureConfig true
                                             :defaultAgent "plan"
                                             :extraConfigs ["rel.json"]})
      (is (= "from-rel"
             (:defaultAgent (#'config/all* {:workspace-folders [{:uri (shared/filename->uri (str dir))}]})))))))

(defn ^:private plugin-config-flow
  [{:keys [global roots init env custom extras pure?]}]
  (let [dir (fs/create-temp-dir)]
    (try
      (let [write-config! (fn [path config]
                            (io/make-parents path)
                            (spit path (json/generate-string config))
                            path)
            global-file (write-config! (fs/file dir "global.json") global)
            custom-file (when custom (write-config! (fs/file dir "custom.json") custom))
            folders (mapv (fn [i layer]
                            (let [root (fs/file dir (str "root-" i))]
                              (write-config! (fs/file root ".eca" "config.json") layer)
                              {:uri (shared/filename->uri (str root))}))
                          (range) roots)
            extra-files (mapv (fn [i layer]
                                (str (write-config! (fs/file dir (str "extra-" i ".json")) layer)))
                              (range) extras)]
        (with-redefs-fn {#'config/initialization-config* (atom (cond-> (or init {})
                                                               pure? (assoc :pureConfig true)
                                                               (seq extras) (assoc :extraConfigs extra-files)))
                         #'config/custom-config-file-path* (atom (some-> custom-file str))
                         #'config/plugin-components* (atom nil)
                         #'config/global-config-file (constantly global-file)
                         #'config/config-from-envvar (constantly env)
                         ;; Read the actual custom file without its separate TTL cache.
                         #'config/config-from-custom #'config/config-from-custom*}
          (fn []
            (config/clear-cache!)
            {:all (config/all {:workspace-folders folders})
             :files (config/read-file-configs)})))
      (finally
        (config/clear-cache!)
        (fs/delete-tree dir)))))

(deftest plugin-install-config-flow-test
  (let [install #(get-in % [:all :plugins "install"])
        p (fn [refs] {:plugins {:install refs}})]
    (testing "global and multiple roots append; exact duplicates move to their last position"
      (let [result (:all (plugin-config-flow
                         {:global {:plugins {:company {:source "https://example.com/company.git"}
                                             :install ["a" "b" "a" "same@company"]}
                                   :disabledTools ["global"]
                                   :chat {:global true}}
                          :roots [{:plugins {:first {:source "https://example.com/first.git"}
                                             :install ["c" "a"]}
                                   :disabledTools ["first"]
                                   :chat {:first true}}
                                  {:plugins {:install ["b" "same" "same@other" "d" "d"]}
                                   :disabledTools ["last"]
                                   :chat {:last true}}]}))]
        (is (= ["same@company" "c" "a" "b" "same" "same@other" "d"]
               (get-in result [:plugins "install"])))
        (is (= "https://example.com/company.git" (get-in result [:plugins "company" :source])))
        (is (= "https://example.com/first.git" (get-in result [:plugins "first" :source])))
        (is (some? (get-in result [:plugins "eca" :source])))
        (is (= ["last"] (:disabledTools result)))
        (is (true? (get-in result [:chat :global])))
        (is (true? (get-in result [:chat :last])))
        (is (nil? (get-in result [:chat :first])))))
    (testing "empty append retains inherited plugins"
      (is (= ["global"] (install (plugin-config-flow {:global (p ["global"])
                                                     :roots [(p []) (p [])]})))))
    (testing "replace with empty or populated list resets earlier layers, not sources"
      (doseq [refs [[] ["new" "new"]]]
        (let [result (:all (plugin-config-flow
                           {:global {:plugins {:company {:source "https://example.com/company.git"}
                                               :install ["global"]}}
                            :roots [(assoc-in (p refs) [:plugins :installMode] "replace")]}))]
          (is (= (vec (distinct refs)) (get-in result [:plugins "install"])))
          (is (some? (get-in result [:plugins "company" :source]))))))
    (testing "a reset survives aggregation, but its mode does not apply to later roots"
      (doseq [refs [[] ["reset"]]]
        (is (= (conj refs "later")
               (install (plugin-config-flow
                         {:global (p ["global"])
                          :roots [(assoc-in (p refs) [:plugins :installMode] "replace")
                                  (p ["later"])]}))))))
    (testing "mode without an install list does not reset or stick"
      (is (= ["global" "later"] (install (plugin-config-flow
                                         {:global (p ["global"])
                                          :roots [{:plugins {:installMode "replace"}} (p ["later"])]})))))
    (testing "existing order is initial config, init, env, global, roots, then extras"
      (is (= ["init" "env" "global" "root" "extra-1" "extra-2"]
             (install (plugin-config-flow {:init (p ["init"])
                                           :env {"plugins" {"install" ["env"]}}
                                           :global (p ["global"])
                                           :roots [(p ["root"])]
                                           :extras [(p ["extra-1"]) (p ["extra-2"])]})))))
    (testing "extra resets apply to all earlier layers and later extras append"
      (doseq [refs [[] ["reset"]]]
        (is (= (conj refs "later")
               (install (plugin-config-flow
                         {:init (p ["init"])
                          :global (p ["global"])
                          :roots [(p ["root"])]
                          :extras [(assoc-in (p refs) [:plugins :installMode] "replace")
                                   (p ["later"])]})))))
      (is (= [] (install (plugin-config-flow
                         {:global (p ["global"])
                          :extras [(assoc-in (p []) [:plugins :installMode] "replace")]})))))
    (testing "custom file selection and layer-local resets, including early file config"
      (doseq [[layers expected files]
              [[{:env (p ["env"]) :custom (p ["custom"]) :extras [(p ["extra"])]}
                ["init" "env" "custom" "extra"] ["env" "custom"]]
               [{:env {"plugins" {"installMode" "replace" "install" ["env"]}}}
                ["env" "global" "root"] ["env" "global"]]
               [{:env (p ["env"])
                 :custom (assoc-in (p []) [:plugins :installMode] "replace")
                 :extras [(p ["extra"])]}
                ["extra"] []]]]
        (let [result (plugin-config-flow
                      (merge {:init (p ["init"]) :global (p ["global"]) :roots [(p ["root"])]}
                             layers))]
          (is (= expected (install result)))
          (is (= files (get-in result [:files :plugins "install"]))))))
    (testing "explicit append after replace, with unrelated extra config behavior unchanged"
      (let [hook {:type "command" :command "echo extra"}
            result (:all (plugin-config-flow
                          {:global {:plugins {:install ["global"]}
                                    :chat {:retained true}
                                    :hooks {:preToolCall [{:type "command" :command "echo global"}]}
                                    :agent {"parent" {:disabledTools ["parent"]}
                                            "child" {:inherit "parent" :disabledTools ["child"]}}}
                           :extras [{:plugins {:installMode "replace" :install ["reset"]}
                                     :chat false
                                     :disabledTools ["first"]}
                                    {:plugins {:installMode "append" :install ["extra"]}
                                     :chat {:added true}
                                     :disabledTools ["last"]
                                     :hooks {:preToolCall [hook]}}]}))]
        (is (= ["reset" "extra"] (get-in result [:plugins "install"])))
        ;; Extras were already combined before merging with the global map.
        (is (true? (get-in result [:chat :retained])))
        (is (true? (get-in result [:chat :added])))
        (is (= ["last"] (:disabledTools result)))
        (is (= [hook] (get-in result [:hooks :preToolCall])))
        (is (= ["child"] (get-in result [:agent "child" :disabledTools])))))
    (testing "pureConfig still skips env and file layers, but applies extraConfigs"
      (is (= ["init" "extra"] (install (plugin-config-flow
                                       {:pure? true
                                        :init (assoc-in (p ["init"]) [:plugins :installMode] "replace")
                                        :env (p ["env"])
                                        :global (p ["global"])
                                        :roots [(p ["root"])]
                                        :extras [(p ["extra"])]})))))))

(deftest deep-merge-test
  (testing "basic merge"
    (is (match?
         {:a 1
          :b 4
          :c 3
          :d 1}
         (#'config/deep-merge {:a 1}
                              {:b 2}
                              {:c 3}
                              {:b 4 :d 1}))))
  (testing "deep merging"
    (is (match?
         {:a 1
          :b {:c {:d 3
                  :e 4}}}
         (#'config/deep-merge {:a 1
                               :b {:c {:d 3}}}
                              {:b {:c {:e 4}}}))))
  (testing "deep merging maps with other keys"
    (is (match?
         {:a 1
          :b {:c {:e 3
                  :f 4}
              :d 2}}
         (#'config/deep-merge {:a 1
                               :b {:c {:e 3}
                                   :d 2}}
                              {:b {:c {:f 4}}})))
    (is (match?
         {:pureConfig true
          :providers {"github-copilot" {:models {"gpt-5" {}}
                                        :key "123"}}}
         (#'config/deep-merge {:providers {"github-copilot" {:models {"gpt-5" {}}}}}
                              {:pureConfig true
                               :providers {"github-copilot" {:key "123"}}})))))

(deftest normalize-fields-test
  (testing "stringfy only passed rules"
    (is (match?
         {:pureConfig true
          :providers {"custom-provider" {:key "123"
                                         :models {"gpt-5" {}}}
                      "openrouter" {:models {"openai/o4-mini" {}}}}}
         (#'config/normalize-fields
          {:stringfy-key
           [[:providers]
            [:providers :ANY :models]]}
          {"pureConfig" true
           "providers" {"custom-provider" {"key" "123"
                                           "models" {"gpt-5" {}}}
                        "openrouter" {"models" {"openai/o4-mini" {}}}}}))))
  (testing "kebab-case only passed rules"
    (is (match?
         {:pureConfig true
          :providers {"custom-provider" {:key "123"
                                         :models {"gpt-5" {}}}
                      "open-router" {:models {"openAi/o4-mini" {}}}}}
         (#'config/normalize-fields
          {:stringfy-key
           [[:providers]
            [:providers :ANY :models]]
           :kebab-case-key
           [[:providers]]}
          {"pureConfig" true
           "providers" {"customProvider" {"key" "123"
                                          "models" {"gpt-5" {}}}
                        "open-router" {"models" {"openAi/o4-mini" {}}}}}))))
  (testing "keywordize-vals"
    (is (match?
         {:pureConfig true
          :providers {"custom-provider" {:key "123"
                                         :models {"gpt-5" {}}
                                         :httpClient {:version :http1.1}}}}
         (#'config/normalize-fields
          {:stringfy-key
           [[:providers]
            [:providers :ANY :models]]
           :kebab-case-key
           [[:providers]]
           :keywordize-val
           [[:providers :ANY :httpClient]]}
          {"pureConfig" true
           "providers" {"customProvider" {"key" "123"
                                          "models" {"gpt-5" {}}
                                          "httpClient" {"version" "http1.1"}}}})))))

(deftest validate-agent-test
  (testing "valid agent returns as-is"
    (let [config {:agent {"code" {} "plan" {} "custom" {}}}]
      (is (= "code" (config/validate-agent-name "code" config)))
      (is (= "plan" (config/validate-agent-name "plan" config)))
      (is (= "custom" (config/validate-agent-name "custom" config)))))

  (testing "nil agent returns fallback"
    (let [config {:agent {"code" {} "plan" {}}}]
      (is (= "code" (config/validate-agent-name nil config)))))

  (testing "empty string agent returns fallback"
    (let [config {:agent {"code" {} "plan" {}}}]
      (is (= "code" (config/validate-agent-name "" config)))))

  (testing "unknown agent returns fallback"
    (let [config {:agent {"code" {} "plan" {}}}]
      (with-redefs [logger/warn (fn [_ _] nil)]
        (is (= "code" (config/validate-agent-name "nonexistent" config))))))

  (testing "agent validation with various configs"
    ;; Config with only code agent
    (let [config {:agent {"code" {}}}]
      (is (= "code" (config/validate-agent-name "plan" config))))

    ;; Config with custom agents only
    (let [config {:agent {"custom1" {} "custom2" {}}}]
      (with-redefs [logger/warn (fn [_ _] nil)]
        (is (= "code" (config/validate-agent-name "plan" config)))
        (is (= "custom1" (config/validate-agent-name "custom1" config)))
        (is (= "custom2" (config/validate-agent-name "custom2" config)))))

    ;; Empty agent config
    (let [config {:agent {}}]
      (with-redefs [logger/warn (fn [_ _] nil)]
        (is (= "code" (config/validate-agent-name "anything" config)))))))

(deftest resolve-agent-inheritance-test
  (testing "basic inheritance copies parent fields to child"
    (let [agents {"plan" {:mode "primary"
                          :disabledTools ["edit_file" "write_file"]
                          :toolCall {:approval {:byDefault "ask"}}}
                  "my-plan" {:inherit "plan"
                             :description "custom plan"}}]
      (is (match?
           {"plan" {:mode "primary"
                    :disabledTools ["edit_file" "write_file"]}
            "my-plan" {:mode "primary"
                       :disabledTools ["edit_file" "write_file"]
                       :toolCall {:approval {:byDefault "ask"}}
                       :description "custom plan"}}
           (#'config/resolve-agent-inheritance agents)))))

  (testing "spawnableBy follows normal inheritance and child override behavior"
    (let [agents {"worker" {:mode "subagent"
                             :spawnableBy ["duel"]}
                  "inherited-worker" {:inherit "worker"
                                      :description "inherits restriction"}
                  "overridden-worker" {:inherit "worker"
                                       :spawnableBy ["other"]}}
          resolved (#'config/resolve-agent-inheritance agents)]
      (is (= ["duel"] (get-in resolved ["inherited-worker" :spawnableBy])))
      (is (= ["other"] (get-in resolved ["overridden-worker" :spawnableBy])))))

  (testing "variant follows normal inheritance and child override behavior"
    (let [agents {"worker" {:mode "subagent"
                             :variant "medium"}
                  "inherited-worker" {:inherit "worker"}
                  "overridden-worker" {:inherit "worker"
                                       :variant "high"}}
          resolved (#'config/resolve-agent-inheritance agents)]
      (is (= "medium" (get-in resolved ["inherited-worker" :variant])))
      (is (= "high" (get-in resolved ["overridden-worker" :variant])))))

  (testing "child values override parent values"
    (let [agents {"code" {:mode "primary"
                          :disabledTools ["preview_file_change"]
                          :defaultModel "anthropic/claude-sonnet-4-6"}
                  "my-code" {:inherit "code"
                             :defaultModel "openai/gpt-5"}}]
      (is (match?
           {"my-code" {:mode "primary"
                       :disabledTools ["preview_file_change"]
                       :defaultModel "openai/gpt-5"}}
           (#'config/resolve-agent-inheritance agents)))))

  (testing "missing parent is skipped with warning"
    (let [agents {"child" {:inherit "nonexistent"
                           :mode "primary"}}]
      (is (match?
           {"child" {:mode "primary"}}
           (#'config/resolve-agent-inheritance agents)))
      (is (not (contains? (get (#'config/resolve-agent-inheritance agents) "child") :inherit)))))

  (testing "self-inheritance is skipped"
    (let [agents {"self" {:inherit "self"
                          :mode "primary"}}]
      (is (match?
           {"self" {:mode "primary"}}
           (#'config/resolve-agent-inheritance agents)))
      (is (not (contains? (get (#'config/resolve-agent-inheritance agents) "self") :inherit)))))

  (testing "agent without inherit is unchanged"
    (let [agents {"code" {:mode "primary"
                          :disabledTools ["preview_file_change"]}}]
      (is (match?
           {"code" {:mode "primary"
                    :disabledTools ["preview_file_change"]}}
           (#'config/resolve-agent-inheritance agents)))))

  (testing "inherit key is stripped from resolved config"
    (let [agents {"plan" {:mode "primary"}
                  "child" {:inherit "plan"
                           :description "my child"}}
          resolved (#'config/resolve-agent-inheritance agents)]
      (is (not (contains? (get resolved "child") :inherit))))))

(deftest resolve-provider-inheritance-test
  (testing "child gets parent config without its credentials and keeps inherit"
    (let [providers {"anthropic" {:api "anthropic"
                                  :url "https://api.anthropic.com"
                                  :key "parent-key"
                                  :keyRc "parent@api.anthropic.com"
                                  :keyEnv "PARENT_KEY"
                                  :requiresAuth? true
                                  :models {"claude-opus-5" {}}}
                     "anthropic-work" {:inherit "anthropic"
                                       :models {"claude-sonnet-5" {}}}}
          resolved (#'config/resolve-provider-inheritance providers)]
      (is (= (get providers "anthropic") (get resolved "anthropic")))
      (is (match? {:api "anthropic"
                   :url "https://api.anthropic.com"
                   :requiresAuth? true
                   :inherit "anthropic"
                   :models {"claude-opus-5" {}
                            "claude-sonnet-5" {}}}
                  (get resolved "anthropic-work")))
      (is (not-any? #(contains? (get resolved "anthropic-work") %) [:key :keyRc :keyEnv]))))

  (testing "child values and credentials win"
    (let [resolved (#'config/resolve-provider-inheritance
                    {"openai" {:api "openai-responses" :url "https://api.openai.com" :key "parent-key"}
                     "openai-work" {:inherit "openai" :url "https://proxy.example.com" :key "work-key"}})]
      (is (match? {:api "openai-responses" :url "https://proxy.example.com" :key "work-key" :inherit "openai"}
                  (get resolved "openai-work")))))

  (testing "inherit chains resolve through the parent"
    (let [resolved (#'config/resolve-provider-inheritance
                    {"anthropic" {:api "anthropic" :url "https://api.anthropic.com"}
                     "anthropic-work" {:inherit "anthropic" :cacheRetention "long"}
                     "anthropic-work-2" {:inherit "anthropic-work"}})]
      (is (match? {:api "anthropic" :url "https://api.anthropic.com" :cacheRetention "long" :inherit "anthropic-work"}
                  (get resolved "anthropic-work-2")))
      (is (= "anthropic" (config/provider-base "anthropic-work-2" {:providers resolved})))))

  (testing "parent name is normalized like provider ids"
    (is (match? {"anthropic-work" {:api "anthropic" :inherit "nubank-anthropic"}}
                (#'config/resolve-provider-inheritance
                 {"nubank-anthropic" {:api "anthropic"}
                  "anthropic-work" {:inherit "nubankAnthropic"}}))))

  (testing "self, unknown and circular parents are ignored"
    (with-redefs [logger/warn (fn [& _] nil)]
      (let [resolved (#'config/resolve-provider-inheritance
                      {"self" {:inherit "self" :api "anthropic"}
                       "orphan" {:inherit "nonexistent" :api "openai-chat"}
                       "a" {:inherit "b" :url "a"}
                       "b" {:inherit "a" :url "b"}})]
        (is (= {:api "anthropic"} (get resolved "self")))
        (is (= {:api "openai-chat"} (get resolved "orphan")))
        (is (= {:url "a"} (get resolved "a")))
        (is (= {:url "b"} (get resolved "b"))))))

  (testing "providers without inherit are returned untouched"
    (let [providers {"anthropic" {:api "anthropic"}}]
      (is (identical? providers (#'config/resolve-provider-inheritance providers))))))

(deftest provider-base-test
  (let [config {:providers {"anthropic" {:api "anthropic"}
                            "anthropic-work" {:api "anthropic" :inherit "anthropic"}}}]
    (is (= "anthropic" (config/provider-base "anthropic-work" config)))
    (is (= "anthropic" (config/provider-base "anthropic" config)))
    (is (= "custom" (config/provider-base "custom" config)))
    (is (nil? (config/provider-base nil config)))))

(deftest provider-inherit-all-test
  (testing "an inheriting provider gets the built-in defaults but not the built-in key"
    (reset! config/initialization-config* {:pureConfig true
                                           :providers {"anthropicWork" {:inherit "anthropic"}}})
    (let [providers (:providers (#'config/all* {}))]
      (is (match? {:api "anthropic"
                   :url string?
                   :requiresAuth? true
                   :inherit "anthropic"
                   :models {"claude-opus-5" {}}}
                  (get providers "anthropic-work")))
      (is (contains? (get providers "anthropic") :key))
      (is (not (contains? (get providers "anthropic-work") :key))))))

(deftest diff-keeping-vectors-test
  (testing "like clojure.data/diff"
    (is (= {:b 3}
           (#'config/diff-keeping-vectors {:a 1
                                           :b 2}
                                          {:a 1
                                           :b 3})))
    (is (= nil
           (#'config/diff-keeping-vectors {:a {:b 2}
                                           :c 3}
                                          {:a {:b 2}
                                           :c 3})))
    (is (= {:a {:b 3}}
           (#'config/diff-keeping-vectors {:a {:b 2}
                                           :c 3}
                                          {:a {:b 3}
                                           :c 3}))))
  (testing "if a vector value changed, we keep vector from b"
    (is (= {:b [:bar :foo]}
           (#'config/diff-keeping-vectors {:a 1
                                           :c 3
                                           :b [:bar]}
                                          {:c 3
                                           :b [:bar :foo]})))
    (is (= {:b [:bar]}
           (#'config/diff-keeping-vectors {:a 1
                                           :c 3
                                           :b [:bar :foo]}
                                          {:c 3
                                           :b [:bar]})))
    (is (= {:b [:bar]}
           (#'config/diff-keeping-vectors {:a 1
                                           :c 3
                                           :b []}
                                          {:c 3
                                           :b [:bar]})))
    (is (= {:b []}
           (#'config/diff-keeping-vectors {:a 1
                                           :c 3
                                           :b [:bar]}
                                          {:c 3
                                           :b []})))))

(deftest parse-dynamic-string-test
  (testing "returns nil for nil input"
    (is (nil? (interpolation/replace-dynamic-strings nil "/tmp" {}))))

  (testing "returns string unchanged when no patterns"
    (is (= "hello world" (interpolation/replace-dynamic-strings "hello world" "/tmp" {}))))

  (testing "replaces environment variable patterns"
    (with-redefs [interpolation/get-env (fn [env-var]
                                   (case env-var
                                     "TEST_VAR" "test-value"
                                     "ANOTHER_VAR" "another-value"
                                     nil))]
      (is (= "test-value" (interpolation/replace-dynamic-strings "${env:TEST_VAR}" "/tmp" {})))
      (is (= "prefix test-value suffix" (interpolation/replace-dynamic-strings "prefix ${env:TEST_VAR} suffix" "/tmp" {})))
      (is (= "test-value and another-value" (interpolation/replace-dynamic-strings "${env:TEST_VAR} and ${env:ANOTHER_VAR}" "/tmp" {})))))

  (testing "replaces undefined env var with empty string"
    (with-redefs [interpolation/get-env (constantly nil)]
      (is (= "" (interpolation/replace-dynamic-strings "${env:UNDEFINED_VAR}" "/tmp" {})))
      (is (= "prefix  suffix" (interpolation/replace-dynamic-strings "prefix ${env:UNDEFINED_VAR} suffix" "/tmp" {})))))

  (testing "replaces undefined env var with default value"
    (with-redefs [interpolation/get-env (constantly nil)]
      (is (= "default-value" (interpolation/replace-dynamic-strings "${env:UNDEFINED_VAR:default-value}" "/tmp" {})))
      (is (= "http://localhost:11434" (interpolation/replace-dynamic-strings "${env:OLLAMA_API_URL:http://localhost:11434}" "/tmp" {})))
      (is (= "prefix default-value suffix" (interpolation/replace-dynamic-strings "prefix ${env:UNDEFINED_VAR:default-value} suffix" "/tmp" {})))))

  (testing "uses env var value when set, ignoring default"
    (with-redefs [interpolation/get-env (fn [env-var]
                                   (case env-var
                                     "TEST_VAR" "actual-value"
                                     "OLLAMA_API_URL" "http://custom:8080"
                                     nil))]
      (is (= "actual-value" (interpolation/replace-dynamic-strings "${env:TEST_VAR:default-value}" "/tmp" {})))
      (is (= "http://custom:8080" (interpolation/replace-dynamic-strings "${env:OLLAMA_API_URL:http://localhost:11434}" "/tmp" {})))))

  (testing "handles default values with special characters"
    (with-redefs [interpolation/get-env (constantly nil)]
      (is (= "http://localhost:11434/api" (interpolation/replace-dynamic-strings "${env:API_URL:http://localhost:11434/api}" "/tmp" {})))
      (is (= "value-with-dashes" (interpolation/replace-dynamic-strings "${env:VAR:value-with-dashes}" "/tmp" {})))
      (is (= "value_with_underscores" (interpolation/replace-dynamic-strings "${env:VAR:value_with_underscores}" "/tmp" {})))
      (is (= "/path/to/file" (interpolation/replace-dynamic-strings "${env:VAR:/path/to/file}" "/tmp" {})))))

  (testing "handles empty default value"
    (with-redefs [interpolation/get-env (constantly nil)]
      (is (= "" (interpolation/replace-dynamic-strings "${env:UNDEFINED_VAR:}" "/tmp" {})))
      (is (= "prefix  suffix" (interpolation/replace-dynamic-strings "prefix ${env:UNDEFINED_VAR:} suffix" "/tmp" {})))))

  (testing "handles multiple env vars with mixed default values"
    (with-redefs [interpolation/get-env (fn [env-var]
                                   (case env-var
                                     "DEFINED_VAR" "defined"
                                     nil))]
      (is (= "defined and default-value"
             (interpolation/replace-dynamic-strings "${env:DEFINED_VAR:fallback1} and ${env:UNDEFINED_VAR:default-value}" "/tmp" {})))
      (is (= "defined and "
             (interpolation/replace-dynamic-strings "${env:DEFINED_VAR} and ${env:UNDEFINED_VAR}" "/tmp" {})))))

  (testing "replaces file pattern with file content - absolute path"
    (with-redefs [fs/absolute? (fn [path] (= path "/absolute/file.txt"))
                  fs/expand-home identity
                  slurp (fn [path]
                          (if (= (str path) "/absolute/file.txt")
                            "test file content"
                            (throw (ex-info "File not found" {}))))]
      (is (= "test file content" (interpolation/replace-dynamic-strings "${file:/absolute/file.txt}" "/tmp" {})))))

  (testing "replaces file pattern with file content - relative path"
    (with-redefs [fs/absolute? (fn [_] false)
                  fs/path (fn [cwd file-path] (str cwd "/" file-path))
                  fs/expand-home identity
                  slurp (fn [path]
                          (if (= path "/tmp/test.txt")
                            "relative file content"
                            (throw (ex-info "File not found" {}))))]
      (is (= "relative file content" (interpolation/replace-dynamic-strings "${file:test.txt}" "/tmp" {})))))

  (testing "replaces file pattern with file content - path with ~"
    (with-redefs [fs/absolute? (fn [_] true)
                  fs/path (fn [cwd file-path] (str cwd "/" file-path))
                  fs/expand-home (fn [f]
                                   (string/replace (str f) "~" "/home/user"))
                  slurp (fn [path]
                          (if (= path "/home/user/foo/test.txt")
                            "relative file content"
                            (throw (ex-info "File not found" {}))))]
      (is (= "relative file content" (interpolation/replace-dynamic-strings "${file:~/foo/test.txt}" "/tmp" {})))))

  (testing "replaces file pattern with empty string when file not found"
    (with-redefs [logger/warn (fn [& _] nil)
                  fs/absolute? (fn [_] true)
                  slurp (fn [_] (throw (ex-info "File not found" {})))]
      (is (= "" (interpolation/replace-dynamic-strings "${file:/nonexistent/file.txt}" "/tmp" {})))
      (is (= "prefix  suffix" (interpolation/replace-dynamic-strings "prefix ${file:/nonexistent/file.txt} suffix" "/tmp" {})))))

  (testing "handles multiple file patterns"
    (with-redefs [fs/absolute? (fn [_] true)
                  fs/expand-home identity
                  slurp (fn [path]
                          (case (str path)
                            "/file1.txt" "content1"
                            "/file2.txt" "content2"
                            (throw (ex-info "File not found" {}))))]
      (is (= "content1 and content2"
             (interpolation/replace-dynamic-strings "${file:/file1.txt} and ${file:/file2.txt}" "/tmp" {})))))

  (testing "handles mixed env and file patterns"
    (with-redefs [interpolation/get-env (fn [env-var]
                                   (when (= env-var "TEST_VAR") "env-value"))
                  fs/expand-home identity
                  fs/absolute? (fn [_] true)
                  slurp (fn [path]
                          (if (= (str path) "/file.txt")
                            "file-value"
                            (throw (ex-info "File not found" {}))))]
      (is (= "env-value and file-value"
             (interpolation/replace-dynamic-strings "${env:TEST_VAR} and ${file:/file.txt}" "/tmp" {})))))

  (testing "handles patterns within longer strings"
    (with-redefs [interpolation/get-env (fn [env-var]
                                   (when (= env-var "API_KEY") "secret123"))]
      (is (= "Bearer secret123" (interpolation/replace-dynamic-strings "Bearer ${env:API_KEY}" "/tmp" {})))))

  (testing "handles empty string input"
    (is (= "" (interpolation/replace-dynamic-strings "" "/tmp" {}))))

  (testing "preserves content with escaped-like patterns that don't match"
    (is (= "${notenv:VAR}" (interpolation/replace-dynamic-strings "${notenv:VAR}" "/tmp" {})))
    (is (= "${env:}" (interpolation/replace-dynamic-strings "${env:}" "/tmp" {}))))

  (testing "replaces classpath pattern with resource content"
    ;; ECA_VERSION is a real resource file
    (let [version-content (interpolation/replace-dynamic-strings "${classpath:ECA_VERSION}" "/tmp" {})]
      (is (string? version-content))
      (is (seq version-content))))

  (testing "replaces classpath pattern with empty string when resource not found"
    (with-redefs [logger/warn (fn [& _] nil)]
      (is (= "" (interpolation/replace-dynamic-strings "${classpath:nonexistent/resource.txt}" "/tmp" {})))
      (is (= "prefix  suffix" (interpolation/replace-dynamic-strings "prefix ${classpath:nonexistent/resource.txt} suffix" "/tmp" {})))))

  (testing "handles multiple classpath patterns"
    (with-redefs [io/resource (fn [path]
                                (case path
                                  "resource1.txt" (java.io.ByteArrayInputStream. (.getBytes "content1" "UTF-8"))
                                  "resource2.txt" (java.io.ByteArrayInputStream. (.getBytes "content2" "UTF-8"))
                                  nil))]
      (is (= "content1 and content2"
             (interpolation/replace-dynamic-strings "${classpath:resource1.txt} and ${classpath:resource2.txt}" "/tmp" {})))))

  (testing "handles classpath patterns within longer strings"
    (with-redefs [io/resource (fn [path]
                                (when (= path "config/prompt.md")
                                  (java.io.ByteArrayInputStream. (.getBytes "# System Prompt\nYou are helpful." "UTF-8"))))]
      (is (= "# System Prompt\nYou are helpful."
             (interpolation/replace-dynamic-strings "${classpath:config/prompt.md}" "/tmp" {})))))

  (testing "handles exception when reading classpath resource throws NullPointerException"
    (with-redefs [logger/warn (fn [& _] nil)
                  io/resource (constantly nil)]
      (is (= "" (interpolation/replace-dynamic-strings "${classpath:error/resource.txt}" "/tmp" {})))
      (is (= "prefix  suffix" (interpolation/replace-dynamic-strings "prefix ${classpath:error/resource.txt} suffix" "/tmp" {})))))

  (testing "replaces netrc pattern with credential password"
    (with-redefs [secrets/get-credential (fn [key-rc _]
                                           (when (= key-rc "api.openai.com")
                                             "secret-password-123"))]
      (is (= "secret-password-123" (interpolation/replace-dynamic-strings "${netrc:api.openai.com}" "/tmp" {})))))
  (testing "replaces netrc pattern with credential password with a custom netrcFile"
    (with-redefs [secrets/get-credential (fn [key-rc netrc-file]
                                           (when (and (= key-rc "api.openai.com")
                                                      (= netrc-file "/tmp/my-file"))
                                             "secret-password-123"))]
      (is (= "secret-password-123" (interpolation/replace-dynamic-strings "${netrc:api.openai.com}" "/tmp" {"netrcFile" "/tmp/my-file"})))))

  (testing "replaces netrc pattern with empty string when credential not found"
    (with-redefs [secrets/get-credential (constantly nil)]
      (is (= "" (interpolation/replace-dynamic-strings "${netrc:nonexistent.com}" "/tmp" {})))
      (is (= "prefix  suffix" (interpolation/replace-dynamic-strings "prefix ${netrc:nonexistent.com} suffix" "/tmp" {})))))

  (testing "handles netrc pattern with login and port"
    (with-redefs [secrets/get-credential (fn [key-rc _]
                                           (case key-rc
                                             "user@api.example.com" "password1"
                                             "api.example.com:8080" "password2"
                                             "user@api.example.com:443" "password3"
                                             nil))]
      (is (= "password1" (interpolation/replace-dynamic-strings "${netrc:user@api.example.com}" "/tmp" {})))
      (is (= "password2" (interpolation/replace-dynamic-strings "${netrc:api.example.com:8080}" "/tmp" {})))
      (is (= "password3" (interpolation/replace-dynamic-strings "${netrc:user@api.example.com:443}" "/tmp" {})))))

  (testing "handles multiple netrc patterns"
    (with-redefs [secrets/get-credential (fn [key-rc _]
                                           (case key-rc
                                             "api1.example.com" "password1"
                                             "api2.example.com" "password2"
                                             nil))]
      (is (= "password1 and password2"
             (interpolation/replace-dynamic-strings "${netrc:api1.example.com} and ${netrc:api2.example.com}" "/tmp" {})))))

  (testing "handles mixed env, file, classpath, netrc and cmd patterns"
    (with-redefs [interpolation/get-env (fn [env-var]
                                   (when (= env-var "TEST_VAR") "env-value"))
                  fs/expand-home identity
                  fs/absolute? (fn [_] true)
                  slurp (fn [path]
                          (cond
                            (string? path)
                            (if (= path "/file.txt")
                              "file-value"
                              (throw (ex-info "File not found" {})))
                            :else "classpath-value"))
                  io/resource (fn [_] (java.io.ByteArrayInputStream. (.getBytes "classpath-value" "UTF-8")))
                  secrets/get-credential (fn [key-rc _]
                                           (when (= key-rc "api.example.com")
                                             "netrc-password"))
                  interpolation/resolve-cmd (fn [cmd-string]
                                              (when (= cmd-string "echo cmd-value") "cmd-value"))
                  logger/warn (fn [& _] nil)]
      (is (= "env-value and file-value and classpath-value and netrc-password and cmd-value"
             (interpolation/replace-dynamic-strings "${env:TEST_VAR} and ${file:/file.txt} and ${classpath:resource.txt} and ${netrc:api.example.com} and ${cmd:echo cmd-value}" "/tmp" {})))))

  (testing "handles netrc pattern within longer strings"
    (with-redefs [secrets/get-credential (fn [key-rc _]
                                           (when (= key-rc "api.openai.com")
                                             "sk-abc123"))]
      (is (= "Bearer sk-abc123" (interpolation/replace-dynamic-strings "Bearer ${netrc:api.openai.com}" "/tmp" {})))))

  (testing "handles exception when reading netrc credential fails"
    (with-redefs [logger/warn (fn [& _] nil)
                  secrets/get-credential (fn [_] (throw (ex-info "Netrc error" {})))]
      (is (= "" (interpolation/replace-dynamic-strings "${netrc:api.example.com}" "/tmp" {})))
      (is (= "prefix  suffix" (interpolation/replace-dynamic-strings "prefix ${netrc:api.example.com} suffix" "/tmp" {})))))

  (testing "handles netrc pattern with special characters in key-rc"
    (with-redefs [secrets/get-credential (fn [key-rc _]
                                           (case key-rc
                                             "api-gateway.example-corp.com" "password1"
                                             "api_service.example.com" "password2"
                                             nil))]
      (is (= "password1" (interpolation/replace-dynamic-strings "${netrc:api-gateway.example-corp.com}" "/tmp" {})))
      (is (= "password2" (interpolation/replace-dynamic-strings "${netrc:api_service.example.com}" "/tmp" {})))))

  (testing "replaces cmd pattern with the resolver result"
    (with-redefs [interpolation/resolve-cmd (fn [cmd-string]
                                              (case cmd-string
                                                "pass show eca/api-key" "sk-abc123"
                                                "echo hello" "hello"
                                                ""))]
      (is (= "sk-abc123" (interpolation/replace-dynamic-strings "${cmd:pass show eca/api-key}" "/tmp" {})))
      (is (= "Bearer sk-abc123" (interpolation/replace-dynamic-strings "Bearer ${cmd:pass show eca/api-key}" "/tmp" {})))))

  (testing "cmd pattern supports values with `://` (1Password op references)"
    (with-redefs [interpolation/resolve-cmd (fn [cmd-string]
                                              (when (= cmd-string "op read op://vault/Item/credential")
                                                "op-secret"))]
      (is (= "op-secret"
             (interpolation/replace-dynamic-strings "${cmd:op read op://vault/Item/credential}" "/tmp" {})))))

  (testing "cmd pattern returns empty string when resolver returns nil/empty"
    (with-redefs [interpolation/resolve-cmd (constantly "")]
      (is (= "" (interpolation/replace-dynamic-strings "${cmd:false}" "/tmp" {})))
      (is (= "prefix  suffix"
             (interpolation/replace-dynamic-strings "prefix ${cmd:false} suffix" "/tmp" {})))))

  (testing "cmd pattern returns empty string when resolver throws"
    (with-redefs [logger/warn (fn [& _] nil)
                  interpolation/resolve-cmd (fn [_] (throw (ex-info "boom" {})))]
      (is (= "" (interpolation/replace-dynamic-strings "${cmd:explode}" "/tmp" {})))
      (is (= "prefix  suffix"
             (interpolation/replace-dynamic-strings "prefix ${cmd:explode} suffix" "/tmp" {})))))

  (testing "handles multiple cmd patterns in one string"
    (with-redefs [interpolation/resolve-cmd (fn [cmd-string]
                                              (case cmd-string
                                                "echo a" "value-a"
                                                "echo b" "value-b"
                                                ""))]
      (is (= "value-a and value-b"
             (interpolation/replace-dynamic-strings "${cmd:echo a} and ${cmd:echo b}" "/tmp" {}))))))

(deftest config-schema-test
  (testing "docs/config.json is a valid JSON schema"
    (let [schema (json/parse-string (slurp (io/file "docs" "config.json")))]
      (is (= "http://json-schema.org/draft-07/schema#" (get schema "$schema")))
      (is (= "https://eca.dev/config.json" (get schema "$id")))
      (is (= "ECA Configuration" (get schema "title")))
      (is (map? (get schema "properties")))
      (is (map? (get schema "definitions")))))

  (testing "update-global-config! includes $schema in written config"
    (let [temp-dir (fs/create-temp-dir)
          config-file (io/file (str temp-dir) "config.json")]
      (try
        (with-redefs [config/global-config-file (constantly config-file)]
          (config/update-global-config! {:defaultModel "anthropic/claude-sonnet-4-6"})
          (let [written-config (json/parse-string (slurp config-file))]
            (is (= "https://eca.dev/config.json" (get written-config "$schema")))
            (is (= "anthropic/claude-sonnet-4-6" (get written-config "defaultModel")))))
        (finally
          (fs/delete-tree temp-dir))))))

(deftest effective-model-variants-test
  (let [anthropic-variants {"low" {:output_config {:effort "low"} :thinking {:type "adaptive"}}
                            "medium" {:output_config {:effort "medium"} :thinking {:type "adaptive"}}
                            "high" {:output_config {:effort "high"} :thinking {:type "adaptive"}}
                            "max" {:output_config {:effort "max"} :thinking {:type "adaptive"}}}
        openai-variants {"none" {:reasoning {:effort "none" :summary "auto"}}
                         "low" {:reasoning {:effort "low" :summary "auto"}}
                         "medium" {:reasoning {:effort "medium" :summary "auto"}}
                         "high" {:reasoning {:effort "high" :summary "auto"}}
                         "xhigh" {:reasoning {:effort "xhigh" :summary "auto"}}}
        config {:variantsByModel {".*sonnet[-._]4[-._]6|opus[-._]4[-._][56]"
                                  {:variants anthropic-variants}
                                  ".*gpt[-._]5(?:[-._](?:2|4)(?!\\d)|[-._]3[-._]codex)"
                                  {:variants openai-variants
                                   :excludeProviders ["github-copilot"]}}}]

    (testing "Returns built-in variants for matching anthropic model"
      (is (= anthropic-variants
             (config/effective-model-variants config "anthropic" "claude-sonnet-4-6" nil))))

    (testing "Returns built-in variants for opus models"
      (is (= anthropic-variants
             (config/effective-model-variants config "anthropic" "claude-opus-4-5" nil)))
      (is (= anthropic-variants
             (config/effective-model-variants config "anthropic" "claude-opus-4-6" nil))))

    (testing "Returns built-in variants for matching openai model"
      (is (= openai-variants
             (config/effective-model-variants config "openai" "gpt-5.2" nil)))
      (is (= openai-variants
             (config/effective-model-variants config "openai" "gpt-5.3-codex" nil))))

    (testing "Returns nil for non-matching model without user variants"
      (is (nil? (config/effective-model-variants config "openai" "gpt-4.1" nil)))
      (is (nil? (config/effective-model-variants config "ollama" "llama3" nil))))

    (testing "Custom provider with matching model gets built-in variants"
      (is (= anthropic-variants
             (config/effective-model-variants config "my-proxy" "claude-opus-4-6" nil))))

    (testing "excludeProviders prevents built-in variants for that provider"
      (is (nil? (config/effective-model-variants config "github-copilot" "gpt-5.2" nil))))

    (testing "User variants override built-in on name clash"
      (is (= (merge anthropic-variants {"high" {:custom "payload"}})
             (config/effective-model-variants config "anthropic" "claude-sonnet-4-6"
                                             {"high" {:custom "payload"}}))))

    (testing "Discovered variants replace built-ins and user variants have final priority"
      (is (= {"low" {:source "endpoint"}
              "high" {:source "user"}}
             (config/effective-model-variants
              config "openai" "gpt-5.2"
              {:api :openai-responses
               :variants {"low" {:source "endpoint"}}}
              {"high" {:source "user"}}))))

    (testing "Effort variants from /models are the last fallback"
      (let [effort-variants {"low" {:reasoning_effort "low"}
                             "high" {:reasoning_effort "high"}}
            caps {:api :openai-chat :effort-variants effort-variants}]
        (is (= effort-variants
               (config/effective-model-variants {} "synthetic" "qwen3" caps nil)))
        (is (= anthropic-variants
               (config/effective-model-variants config "my-proxy" "claude-opus-4-6"
                                                (assoc caps :api :anthropic) nil)))
        (is (= {"low" {:reasoning_effort "low"}}
               (config/effective-model-variants {} "synthetic" "qwen3" caps
                                                {"high" {}})))))

    (testing "User variant set to {} removes a discovered variant"
      (is (= {"low" {:source "endpoint"}}
             (config/effective-model-variants
              {} "github-copilot" "future-model"
              {:api :openai-chat
               :variants {"low" {:source "endpoint"}
                          "high" {:source "endpoint"}}}
              {"high" {}}))))

    (testing "Discovered API participates in built-in API filtering"
      (let [api-config {:variantsByModel {".*claude.*" {:api ["anthropic" "bedrock"]
                                                         :variants anthropic-variants}}}]
        (is (= anthropic-variants
               (config/effective-model-variants api-config "github-copilot" "claude-opus-4-6"
                                                {:api :anthropic} nil)))
        (is (nil? (config/effective-model-variants api-config "github-copilot" "claude-opus-4-6"
                                                   {:api :openai-chat} nil)))))

    (testing "Default config: Copilot Claude models get no built-in variants without discovered metadata"
      ;; Intentional: Copilot variants come only from /models discovery; built-in
      ;; Claude variants are restricted to the anthropic/bedrock APIs (see
      ;; :variantsByModel :api and docs/config/variants.md).
      (let [default-config (config/initial-config)]
        (is (nil? (config/effective-model-variants default-config "github-copilot" "claude-sonnet-4-6" nil nil)))
        (is (nil? (config/effective-model-variants default-config "github-copilot" "claude-sonnet-5" nil nil)))
        (is (= anthropic-variants
               (config/effective-model-variants default-config "anthropic" "claude-sonnet-4-6" nil nil)))
        (is (= {"default" {:thinking {:type "adaptive" :display "summarized"}}
                "low" {:output_config {:effort "low"} :thinking {:type "adaptive" :display "summarized"}}
                "medium" {:output_config {:effort "medium"} :thinking {:type "adaptive" :display "summarized"}}
                "high" {:output_config {:effort "high"} :thinking {:type "adaptive" :display "summarized"}}
                "xhigh" {:output_config {:effort "xhigh"} :thinking {:type "adaptive" :display "summarized"}}
                "max" {:output_config {:effort "max"} :thinking {:type "adaptive" :display "summarized"}}}
               (config/effective-model-variants default-config "anthropic" "claude-opus-5-5" nil nil)))))

    (testing "Default config: Claude models on openai-chat providers (e.g. OpenRouter) get verbosity-based variants"
      (let [default-config (assoc-in (config/initial-config) [:providers "openrouter" :api] "openai-chat")]
        (is (= {"low" {:verbosity "low" :reasoning {:enabled true}}
                "medium" {:verbosity "medium" :reasoning {:enabled true}}
                "high" {:verbosity "high" :reasoning {:enabled true}}
                "max" {:verbosity "max" :reasoning {:enabled true}}}
               (config/effective-model-variants default-config "openrouter" "anthropic/claude-opus-4.5" nil nil)))
        (is (= {"low" {:verbosity "low"}
                "medium" {:verbosity "medium"}
                "high" {:verbosity "high"}
                "xhigh" {:verbosity "xhigh"}
                "max" {:verbosity "max"}}
               (config/effective-model-variants default-config "openrouter" "anthropic/claude-opus-4.7" nil nil)))
        (is (= {"low" {:verbosity "low"}
                "medium" {:verbosity "medium"}
                "high" {:verbosity "high"}
                "xhigh" {:verbosity "xhigh"}
                "max" {:verbosity "max"}}
               (config/effective-model-variants default-config "openrouter" "anthropic/claude-fable-5" nil nil)))
        (is (= {"low" {:verbosity "low"}
                "medium" {:verbosity "medium"}
                "high" {:verbosity "high"}
                "xhigh" {:verbosity "xhigh"}
                "max" {:verbosity "max"}}
               (config/effective-model-variants default-config "openrouter" "anthropic/claude-mythos-5-1" nil nil)))
        (is (= {"low" {:verbosity "low"}
                "medium" {:verbosity "medium"}
                "high" {:verbosity "high"}
                "xhigh" {:verbosity "xhigh"}
                "max" {:verbosity "max"}}
               (config/effective-model-variants default-config "openrouter" "anthropic/claude-opus-5.5" nil nil)))
        ;; Copilot Claude models on the chat API keep discovery-only behavior
        (is (nil? (config/effective-model-variants default-config "github-copilot" "claude-opus-4.5"
                                                   {:api :openai-chat} nil)))))

    (testing "Default config: gpt-6 models get effort variants without none"
      (let [default-config (assoc-in (config/initial-config) [:providers "custom" :api] "openai-responses")
            gpt-6-variants {"low" {:reasoning {:effort "low" :summary "auto"}}
                            "medium" {:reasoning {:effort "medium" :summary "auto"}}
                            "high" {:reasoning {:effort "high" :summary "auto"}}
                            "xhigh" {:reasoning {:effort "xhigh" :summary "auto"}}
                            "max" {:reasoning {:effort "max" :summary "auto"}}}]
        (is (= gpt-6-variants
               (config/effective-model-variants default-config "openai" "gpt-6-astra" nil nil)))
        (is (= gpt-6-variants
               (config/effective-model-variants default-config "custom" "gpt_6_astra" nil nil)))
        ;; gpt-5.6 keeps its own set, including none
        (is (contains? (config/effective-model-variants default-config "openai" "gpt-5.6-sol" nil nil)
                       "none"))
        ;; point releases and Copilot are not matched
        (is (nil? (config/effective-model-variants default-config "openai" "gpt-6.1" nil nil)))
        (is (nil? (config/effective-model-variants default-config "openai" "gpt-60" nil nil)))
        (is (nil? (config/effective-model-variants default-config "github-copilot" "gpt-6-astra" nil nil)))))

    (testing "Default config: GPT models on openai-chat providers (e.g. LiteLLM/Azure gateways) get reasoning_effort variants (#609)"
      (let [default-config (-> (config/initial-config)
                               (assoc-in [:providers "gateway" :api] "openai-chat")
                               (assoc-in [:providers "legacy" :api] "openai"))]
        (is (= {"low" {:reasoning_effort "low"}
                "medium" {:reasoning_effort "medium"}
                "high" {:reasoning_effort "high"}
                "xhigh" {:reasoning_effort "xhigh"}
                "max" {:reasoning_effort "max"}}
               (config/effective-model-variants default-config "gateway" "openai/gpt-6-astra" nil nil)))
        (is (= {"none" {:reasoning_effort "none"}
                "low" {:reasoning_effort "low"}
                "medium" {:reasoning_effort "medium"}
                "high" {:reasoning_effort "high"}
                "xhigh" {:reasoning_effort "xhigh"}
                "max" {:reasoning_effort "max"}}
               (config/effective-model-variants default-config "gateway" "gpt-5.6-luna" nil nil)))
        (is (= {"none" {:reasoning_effort "none"}
                "low" {:reasoning_effort "low"}
                "medium" {:reasoning_effort "medium"}
                "high" {:reasoning_effort "high"}
                "xhigh" {:reasoning_effort "xhigh"}}
               (config/effective-model-variants default-config "gateway" "gpt-5.5" nil nil)))
        ;; the Responses shape stays on openai-responses providers, including the legacy `openai` api alias
        (is (= {:effort "medium" :summary "auto"}
               (get-in (config/effective-model-variants default-config "legacy" "gpt-5.5" nil nil)
                       ["medium" :reasoning])))
        ;; a discovered API wins over the provider config
        (is (= {:reasoning_effort "medium"}
               (get (config/effective-model-variants default-config "legacy" "gpt-5.5" {:api :openai-chat} nil)
                    "medium")))
        ;; Copilot GPT models keep discovery-only behavior
        (is (nil? (config/effective-model-variants default-config "github-copilot" "gpt-5.5" {:api :openai-chat} nil)))))

    (testing "User variant set to {} removes it from result"
      (is (= (dissoc anthropic-variants "high" "max")
             (config/effective-model-variants config "anthropic" "claude-sonnet-4-6"
                                             {"high" {} "max" {}}))))

    (testing "User-only variants pass through when no regex match"
      (is (= {"fast" {:speed "turbo"}}
             (config/effective-model-variants config "ollama" "llama3"
                                             {"fast" {:speed "turbo"}}))))

    (testing "User-only variant set to {} is removed"
      (is (nil? (config/effective-model-variants config "ollama" "llama3"
                                                 {"fast" {}}))))

    (testing "Matches model name variations with different separators"
      (is (= anthropic-variants
             (config/effective-model-variants config "custom" "sonnet.4.6" nil)))
      (is (= anthropic-variants
             (config/effective-model-variants config "custom" "opus_4_5" nil)))
      (is (= openai-variants
             (config/effective-model-variants config "openai" "gpt-5.2" nil))))

    (testing "Does not match partial model versions"
      (is (nil? (config/effective-model-variants config "openai" "gpt-5.23" nil))))

    (testing "Nil model-name returns only user variants"
      (is (nil? (config/effective-model-variants config "openai" nil nil)))
      (is (= {"fast" {:a 1}}
             (config/effective-model-variants config "openai" nil {"fast" {:a 1}}))))

    (testing "Missing variantsByModel key in config returns only user variants"
      (is (nil? (config/effective-model-variants {} "openai" "gpt-5.2" nil)))
      (is (= {"fast" {:a 1}}
             (config/effective-model-variants {} "openai" "gpt-5.2" {"fast" {:a 1}}))))

    (testing "Invalid regex pattern in variantsByModel is skipped gracefully"
      (let [bad-config {:variantsByModel {"[invalid" {:variants {"low" {:a 1}}}}}]
        (is (nil? (config/effective-model-variants bad-config "openai" "gpt-5.2" nil)))
        (is (= {"fast" {:a 1}}
               (config/effective-model-variants bad-config "openai" "gpt-5.2" {"fast" {:a 1}})))))))

(deftest notify-selected-model-changed-test
  (testing "Emits config/updated with the given model, its variants and selected variant"
    (h/reset-components!)
    (h/config! {:providers {"anthropic" {:models {"claude-sonnet-4-5"
                                                  {:variants {"low" {:a 1}
                                                              "medium" {:a 2}
                                                              "high" {:a 3}}}}}}
                :defaultAgent "code"
                :agent {"code" {:variant "medium"}}})
    (swap! (h/db*) update :models
           #(merge % {"anthropic/claude-sonnet-4-5" {:tools true}}))
    (config/notify-selected-model-changed! "anthropic/claude-sonnet-4-5"
                                           (h/db*) (h/messenger) (h/config))
    (is (match? {:config-updated [{:chat {:select-model "anthropic/claude-sonnet-4-5"
                                          :variants ["high" "low" "medium"]
                                          :select-variant "medium"}}]}
                (h/messages))))

  (testing "Advertises variants discovered from provider model metadata"
    (h/reset-components!)
    (h/config! {:providers {"github-copilot" {:models {}}}
                :defaultAgent "code"
                :agent {"code" {:variant "high"}}})
    (swap! (h/db*) update :models
           #(merge % {"github-copilot/future-model"
                      {:api :openai-chat
                       :variants {"low" {:reasoning_effort "low"}
                                  "high" {:reasoning_effort "high"}}}}))
    (config/notify-selected-model-changed! "github-copilot/future-model"
                                           (h/db*) (h/messenger) (h/config))
    (is (match? {:config-updated [{:chat {:select-model "github-copilot/future-model"
                                          :variants ["high" "low"]
                                          :select-variant "high"}}]}
                (h/messages))))

  (testing "No-op when model is missing from (:models db*) (stale persisted model)"
    (h/reset-components!)
    (config/notify-selected-model-changed! "anthropic/claude-opus-4"
                                           (h/db*) (h/messenger) (h/config))
    (is (nil? (:config-updated (h/messages)))))

  (testing "No-op when model is nil"
    (h/reset-components!)
    (swap! (h/db*) update :models
           #(merge % {"anthropic/claude-opus-4" {:tools true}}))
    (config/notify-selected-model-changed! nil (h/db*) (h/messenger) (h/config))
    (is (nil? (:config-updated (h/messages)))))

  (testing "Provider without matching variants emits empty variants list"
    (h/reset-components!)
    (swap! (h/db*) update :models
           #(merge % {"custom-provider/plain-model" {:tools true}}))
    (config/notify-selected-model-changed! "custom-provider/plain-model"
                                           (h/db*) (h/messenger) (h/config))
    (is (match? {:config-updated [{:chat {:select-model "custom-provider/plain-model"
                                          :variants []
                                          :select-variant nil}}]}
                (h/messages))))

  (testing "5-arity: chat-variant valid for the model is preserved over the agent's variant
            (so resume restores the variant the user last saw on the chat)"
    (h/reset-components!)
    (h/config! {:providers {"anthropic" {:models {"claude-sonnet-4-5"
                                                  {:variants {"low" {:a 1}
                                                              "medium" {:a 2}
                                                              "high" {:a 3}}}}}}
                :defaultAgent "code"
                :agent {"code" {:variant "high"}}})
    (swap! (h/db*) update :models
           #(merge % {"anthropic/claude-sonnet-4-5" {:tools true}}))
    (config/notify-selected-model-changed! "anthropic/claude-sonnet-4-5"
                                           (h/db*) (h/messenger) (h/config)
                                           "low")
    (is (match? {:config-updated [{:chat {:select-model "anthropic/claude-sonnet-4-5"
                                          :select-variant "low"}}]}
                (h/messages))))

  (testing "5-arity: chat-variant not supported by the model falls back to agent variant"
    (h/reset-components!)
    (h/config! {:providers {"anthropic" {:models {"claude-sonnet-4-5"
                                                  {:variants {"low" {:a 1}
                                                              "high" {:a 2}}}}}}
                :defaultAgent "code"
                :agent {"code" {:variant "high"}}})
    (swap! (h/db*) update :models
           #(merge % {"anthropic/claude-sonnet-4-5" {:tools true}}))
    (config/notify-selected-model-changed! "anthropic/claude-sonnet-4-5"
                                           (h/db*) (h/messenger) (h/config)
                                           "max")
    (is (match? {:config-updated [{:chat {:select-variant "high"}}]}
                (h/messages))))

  (testing "5-arity: nil chat-variant behaves like the 4-arity (uses agent variant)"
    (h/reset-components!)
    (h/config! {:providers {"anthropic" {:models {"claude-sonnet-4-5"
                                                  {:variants {"low" {:a 1}
                                                              "high" {:a 2}}}}}}
                :defaultAgent "code"
                :agent {"code" {:variant "high"}}})
    (swap! (h/db*) update :models
           #(merge % {"anthropic/claude-sonnet-4-5" {:tools true}}))
    (config/notify-selected-model-changed! "anthropic/claude-sonnet-4-5"
                                           (h/db*) (h/messenger) (h/config)
                                           nil)
    (is (match? {:config-updated [{:chat {:select-variant "high"}}]}
                (h/messages))))

  (testing "6-arity: chat-id scopes the model and variant without changing session defaults"
    (h/reset-components!)
    (h/config! {:providers {"anthropic" {:models {"claude-sonnet-4-5"
                                                  {:variants {"low" {:a 1}
                                                              "high" {:a 2}}}}}}
                :defaultAgent "code"
                :agent {"code" {:variant "high"}}})
    (swap! (h/db*) assoc
           :models {"anthropic/claude-sonnet-4-5" {:tools true}}
           :last-config-notified {:chat {:select-model "openai/gpt-5.2"
                                         :select-variant "medium"}})
    (let [session-defaults (:last-config-notified (h/db))
          selection (config/notify-selected-model-changed!
                     "anthropic/claude-sonnet-4-5"
                     (h/db*) (h/messenger) (h/config)
                     "low" "chat-a")]
      (is (= {:model "anthropic/claude-sonnet-4-5"
              :variants ["high" "low"]
              :variant "low"}
             selection))
      (is (match? {:config-updated [{:chat-id "chat-a"
                                     :chat {:select-model "anthropic/claude-sonnet-4-5"
                                            :variants ["high" "low"]
                                            :select-variant "low"}}]}
                  (h/messages)))
      (is (= session-defaults (:last-config-notified (h/db))))))

  (testing "Model with defaultVariant selected when agent and chat have no variant set"
    (h/reset-components!)
    (h/config! {:providers {"anthropic" {:models {"claude-sonnet-4-5"
                                                  {:defaultVariant "low"
                                                   :variants {"low" {:a 1}
                                                              "medium" {:a 2}
                                                              "high" {:a 3}}}}}}
                :defaultAgent "code"
                :agent {"code" {}}})
    (swap! (h/db*) update :models
           #(merge % {"anthropic/claude-sonnet-4-5" {:tools true}}))
    (config/notify-selected-model-changed! "anthropic/claude-sonnet-4-5"
                                           (h/db*) (h/messenger) (h/config))
    (is (match? {:config-updated [{:chat {:select-model "anthropic/claude-sonnet-4-5"
                                          :variants ["high" "low" "medium"]
                                          :select-variant "low"}}]}
                (h/messages))))

  (testing "Agent configured variant overrides model's defaultVariant"
    (h/reset-components!)
    (h/config! {:providers {"anthropic" {:models {"claude-sonnet-4-5"
                                                  {:defaultVariant "low"
                                                   :variants {"low" {:a 1}
                                                              "medium" {:a 2}
                                                              "high" {:a 3}}}}}}
                :defaultAgent "code"
                :agent {"code" {:variant "medium"}}})
    (swap! (h/db*) update :models
           #(merge % {"anthropic/claude-sonnet-4-5" {:tools true}}))
    (config/notify-selected-model-changed! "anthropic/claude-sonnet-4-5"
                                           (h/db*) (h/messenger) (h/config))
    (is (match? {:config-updated [{:chat {:select-variant "medium"}}]}
                (h/messages))))

  (testing "Chat's explicit or persisted variant overrides model's defaultVariant"
    (h/reset-components!)
    (h/config! {:providers {"anthropic" {:models {"claude-sonnet-4-5"
                                                  {:defaultVariant "low"
                                                   :variants {"low" {:a 1}
                                                              "medium" {:a 2}
                                                              "high" {:a 3}}}}}}
                :defaultAgent "code"
                :agent {"code" {}}})
    (swap! (h/db*) update :models
           #(merge % {"anthropic/claude-sonnet-4-5" {:tools true}}))
    (config/notify-selected-model-changed! "anthropic/claude-sonnet-4-5"
                                           (h/db*) (h/messenger) (h/config)
                                           "high")
    (is (match? {:config-updated [{:chat {:select-variant "high"}}]}
                (h/messages))))

  (testing "Model's defaultVariant not among available variants is ignored (falls back to nil)"
    (h/reset-components!)
    (h/config! {:providers {"anthropic" {:models {"claude-sonnet-4-5"
                                                  {:defaultVariant "invalid-variant"
                                                   :variants {"low" {:a 1}
                                                              "medium" {:a 2}
                                                              "high" {:a 3}}}}}}
                :defaultAgent "code"
                :agent {"code" {}}})
    (swap! (h/db*) update :models
           #(merge % {"anthropic/claude-sonnet-4-5" {:tools true}}))
    (config/notify-selected-model-changed! "anthropic/claude-sonnet-4-5"
                                           (h/db*) (h/messenger) (h/config))
    (is (match? {:config-updated [{:chat {:select-variant nil}}]}
                (h/messages))))

  (testing "Model with kebab-case :default-variant selected when agent and chat have no variant set"
    (h/reset-components!)
    (h/config! {:providers {"anthropic" {:models {"claude-sonnet-4-5"
                                                  {:default-variant "low"
                                                   :variants {"low" {:a 1}
                                                              "high" {:a 2}}}}}}
                :defaultAgent "code"
                :agent {"code" {}}})
    (swap! (h/db*) update :models
           #(merge % {"anthropic/claude-sonnet-4-5" {:tools true}}))
    (config/notify-selected-model-changed! "anthropic/claude-sonnet-4-5"
                                           (h/db*) (h/messenger) (h/config))
    (is (match? {:config-updated [{:chat {:select-variant "low"}}]}
                (h/messages)))))

(deftest notify-selected-trust-changed-test
  (testing "4-arity: chat-id scopes trust without changing session defaults"
    (h/reset-components!)
    (swap! (h/db*) assoc :last-config-notified {:chat {:select-trust false}})
    (let [session-defaults (:last-config-notified (h/db))
          selection (config/notify-selected-trust-changed!
                     true (h/db*) (h/messenger) "chat-a")]
      (is (true? selection))
      (is (= [{:chat-id "chat-a" :chat {:select-trust true}}]
             (:config-updated (h/messages))))
      (is (= session-defaults (:last-config-notified (h/db)))))))

(deftest notify-selected-agent-changed-test
  (testing "scopes the agent to chat-id, even when it matches the session defaults"
    (h/reset-components!)
    (swap! (h/db*) assoc :last-config-notified {:chat {:select-agent "plan"}})
    (let [session-defaults (:last-config-notified (h/db))]
      (config/notify-selected-agent-changed! "plan" (h/db*) (h/messenger) "chat-a")
      (is (= [{:chat-id "chat-a" :chat {:select-agent "plan"}}]
             (:config-updated (h/messages))))
      (is (= session-defaults (:last-config-notified (h/db))))))

  (testing "no-op without agent or chat-id"
    (h/reset-components!)
    (config/notify-selected-agent-changed! nil (h/db*) (h/messenger) "chat-a")
    (config/notify-selected-agent-changed! "plan" (h/db*) (h/messenger) nil)
    (is (empty? (:config-updated (h/messages))))))

(deftest agent-modes-test
  (testing "scalar string :mode is honored"
    (is (= #{"primary"} (config/agent-modes {:mode "primary"})))
    (is (= #{"subagent"} (config/agent-modes {:mode "subagent"}))))
  (testing "list :mode is honored"
    (is (= #{"primary"} (config/agent-modes {:mode ["primary"]})))
    (is (= #{"subagent"} (config/agent-modes {:mode ["subagent"]})))
    (is (= #{"primary" "subagent"}
           (config/agent-modes {:mode ["primary" "subagent"]}))))
  (testing "duplicates in list :mode collapse via set semantics"
    (is (= #{"primary"} (config/agent-modes {:mode ["primary" "primary"]}))))
  (testing "absent, nil, or empty :mode defaults to both primary and subagent"
    (is (= #{"primary" "subagent"} (config/agent-modes {})))
    (is (= #{"primary" "subagent"} (config/agent-modes {:mode nil})))
    (is (= #{"primary" "subagent"} (config/agent-modes {:mode []})))))

(deftest subagent-availability-test
  (let [unrestricted {:mode "subagent"}
        restricted-string {:mode "subagent" :spawnableBy "duel"}
        restricted-collection {:mode ["primary" "subagent"]
                               :spawnableBy ["duel" "another"]}
        primary-only {:mode "primary" :spawnableBy "duel"}]
    (testing "unrestricted subagents are available to every or no parent"
      (is (config/subagent-available? unrestricted "code"))
      (is (config/subagent-available? unrestricted nil)))
    (testing "restricted subagents require an exact allowed parent id"
      (is (config/subagent-available? restricted-string "duel"))
      (is (config/subagent-available? restricted-collection "another"))
      (is (not (config/subagent-available? restricted-string "Duel")))
      (is (not (config/subagent-available? restricted-string "code")))
      (is (not (config/subagent-available? restricted-string nil))))
    (testing "spawnableBy does not make primary-only agents into subagents"
      (is (not (config/subagent-available? primary-only "duel"))))))

(deftest primary-agent-names-test
  (testing "includes agents whose effective modes contain 'primary'"
    (let [config {:agent {"a-primary" {:mode "primary"}
                          "a-subagent-only" {:mode "subagent"}
                          "a-list-subagent-only" {:mode ["subagent"]}
                          "a-list-both" {:mode ["primary" "subagent"]
                                         :spawnableBy ["orchestrator"]}
                          "a-unspecified" {:description "no mode set"}}}
          names (set (config/primary-agent-names config))]
      (is (contains? names "a-primary"))
      (is (contains? names "a-list-both"))
      (is (contains? names "a-unspecified"))
      (is (not (contains? names "a-subagent-only")))
      (is (not (contains? names "a-list-subagent-only")))))
  (testing "empty agent map returns empty"
    (is (empty? (config/primary-agent-names {:agent {}})))))
