(ns eca.llm-api-test
  (:require
   [clojure.test :refer [deftest is testing]]
   [cheshire.core :as json]
   [hato.client :as http]
   [eca.client-test-helpers :refer [with-client-proxied *http-client-captures*]]
   [eca.config :as config]
   [eca.llm-api :as llm-api]
   [eca.llm-providers.anthropic :as llm-providers.anthropic]
   [eca.llm-providers.gemini :as llm-providers.gemini]
   [eca.llm-providers.ollama :as llm-providers.ollama]
   [eca.llm-providers.openai :as llm-providers.openai]
   [eca.llm-providers.openai-chat :as llm-providers.openai-chat]
   [eca.secrets :as secrets]
   [eca.test-helper :as h]))

(h/reset-components-before-test)

(deftest sanitize-past-messages-for-api-test
  (testing "drops anthropic-origin reason when target is :openai-chat"
    (let [past [{:role "user" :content [{:type :text :text "hi"}]}
                {:role "reason" :content {:id "r1" :external-id "sig-xyz" :text "thinking"
                                          :api :anthropic}}
                {:role "assistant" :content [{:type :text :text "ok"}]}]
          {:keys [messages dropped-count dropped-apis]}
          (llm-api/sanitize-past-messages-for-api :openai-chat past)]
      (is (= 1 dropped-count))
      (is (= #{:anthropic} dropped-apis))
      (is (= 2 (count messages)))
      (is (= ["user" "assistant"] (mapv :role messages)))))

  (testing "drops openai-responses-origin reason when target is :anthropic"
    (let [past [{:role "reason" :content {:id "rs_abc" :external-id "encrypted-blob" :text "thinking"
                                          :api :openai-responses}}
                {:role "user" :content [{:type :text :text "hi"}]}]
          {:keys [messages dropped-count dropped-apis]}
          (llm-api/sanitize-past-messages-for-api :anthropic past)]
      (is (= 1 dropped-count))
      (is (= #{:openai-responses} dropped-apis))
      (is (= 1 (count messages)))
      (is (= "user" (:role (first messages))))))

  (testing "drops tool_call/tool_call_output pairs whose :api differs from target"
    (let [past [{:role "user" :content [{:type :text :text "hi"}]}
                {:role "tool_call" :content {:id "toolu_aaa" :full-name "read"
                                             :api :anthropic}}
                {:role "tool_call_output" :content {:id "toolu_aaa" :output {:contents []}
                                                    :api :anthropic}}
                {:role "user" :content [{:type :text :text "next"}]}]
          {:keys [messages dropped-count]}
          (llm-api/sanitize-past-messages-for-api :openai-chat past)]
      (is (= 2 dropped-count) "tool_call and tool_call_output both removed")
      (is (= 2 (count messages)))
      (is (every? #(= "user" (:role %)) messages))))

  (testing "drops anthropic server_tool_use and server_tool_result on switch away"
    (let [past [{:role "server_tool_use" :content {:id "stu_1" :name "web_search"
                                                   :api :anthropic}}
                {:role "server_tool_result" :content {:tool-use-id "stu_1" :raw-content {}
                                                      :api :anthropic}}
                {:role "user" :content [{:type :text :text "k"}]}]
          {:keys [messages dropped-count]}
          (llm-api/sanitize-past-messages-for-api :openai-responses past)]
      (is (= 2 dropped-count))
      (is (= [{:role "user" :content [{:type :text :text "k"}]}] messages))))

  (testing "same-api round-trip preserves all entries"
    (let [past [{:role "user" :content [{:type :text :text "hi"}]}
                {:role "reason" :content {:id "r1" :external-id "sig" :text "t"
                                          :api :anthropic}}
                {:role "tool_call" :content {:id "toolu_a" :full-name "read"
                                             :api :anthropic}}
                {:role "tool_call_output" :content {:id "toolu_a" :output {:contents []}
                                                    :api :anthropic}}]
          {:keys [messages dropped-count dropped-apis]}
          (llm-api/sanitize-past-messages-for-api :anthropic past)]
      (is (zero? dropped-count))
      (is (empty? dropped-apis))
      (is (= past messages))))

  (testing "untagged (legacy) entries are preserved as-is"
    (let [past [{:role "user" :content [{:type :text :text "hi"}]}
                {:role "reason" :content {:id "r1" :external-id "sig" :text "t"}}
                {:role "tool_call" :content {:id "toolu_a" :full-name "read"}}]
          {:keys [messages dropped-count]}
          (llm-api/sanitize-past-messages-for-api :openai-chat past)]
      (is (zero? dropped-count))
      (is (= past messages))))

  (testing "internal-only top-level message fields are stripped before provider serialization"
    (let [past [{:role "user"
                 :content [{:type :text :text "hi"}]
                 :created-at 123
                 :content-id "c1"}
                {:role "assistant"
                 :content [{:type :text :text "ok"}]
                 :created-at 456
                 :content-id "c2"}]
          {:keys [messages dropped-count]}
          (llm-api/sanitize-past-messages-for-api :openai-chat past)]
      (is (zero? dropped-count))
      (is (= [{:role "user" :content [{:type :text :text "hi"}]}
              {:role "assistant" :content [{:type :text :text "ok"}]}]
             messages))
      (is (every? #(not (contains? % :created-at)) messages))
      (is (every? #(not (contains? % :content-id)) messages))))

  (testing "mixed history: tagged foreign entries dropped, untagged + matching kept"
    (let [past [{:role "user" :content [{:type :text :text "u1"}]}
                {:role "reason" :content {:id "r0" :text "legacy"}}                            ; untagged → kept
                {:role "reason" :content {:id "r1" :text "t" :api :anthropic}}                 ; foreign → dropped
                {:role "reason" :content {:id "r2" :text "t" :api :openai-chat}}               ; matching → kept
                {:role "assistant" :content [{:type :text :text "a"}] :created-at 999}]
          {:keys [messages dropped-count dropped-apis]}
          (llm-api/sanitize-past-messages-for-api :openai-chat past)]
      (is (= 1 dropped-count))
      (is (= #{:anthropic} dropped-apis))
      (is (= 4 (count messages)))
      (is (= ["user" "reason" "reason" "assistant"] (mapv :role messages)))
      (is (= ["r0" "r2"]
             (->> messages (filter #(= "reason" (:role %))) (map #(get-in % [:content :id])))))
      (is (not (contains? (last messages) :created-at))))))

(deftest default-model-test
  (testing "Custom provider defaultModel present"
    (with-redefs [config/get-env (constantly nil)
                  secrets/credential-file-paths (constantly [])]
      (let [db {:models {"my-provider/my-model" {}}}
            config {:defaultModel "my-provider/my-model"}]
        (is (= "my-provider/my-model" (llm-api/default-model db config))))))

  (testing "Ollama running model present"
    (with-redefs [config/get-env (constantly nil)
                  secrets/credential-file-paths (constantly [])]
      (let [db {:models {"ollama/foo" {:tools true}
                         "gpt-4.1" {:tools true}
                         "other-model" {:tools true}}}
            config {}]
        (is (= "ollama/foo" (llm-api/default-model db config))))))

  (testing "Anthropic API key present in config"
    (with-redefs [config/get-env (constantly nil)
                  secrets/credential-file-paths (constantly [])]
      (let [db {:models {"anthropic/claude-sonnet-4.5" {}}}
            config {:providers {"anthropic" {:key "something"}}}]
        (is (= "anthropic/claude-sonnet-4.5" (llm-api/default-model db config))))))

  (testing "Anthropic API key present in ENV"
    (with-redefs [config/get-env (fn [k] (when (= k "ANTHROPIC_API_KEY") "env-anthropic"))
                  secrets/credential-file-paths (constantly [])]
      (let [db {:models {"anthropic/claude-sonnet-4.5" {}}}
            config {:providers {"anthropic" {:keyEnv "ANTHROPIC_API_KEY"}}}]
        (is (= "anthropic/claude-sonnet-4.5" (llm-api/default-model db config))))))

  (testing "OpenAI API key present in config"
    (with-redefs [config/get-env (constantly nil)
                  secrets/credential-file-paths (constantly [])]
      (let [db {:models {"openai/gpt-5.2" {}}}
            config {:providers {"openai" {:key "yes!"}}}]
        (is (= "openai/gpt-5.2" (llm-api/default-model db config))))))

  (testing "OpenAI API key present in ENV"
    (with-redefs [config/get-env (fn [k] (when (= k "OPENAI_API_KEY") "env-openai"))
                  secrets/credential-file-paths (constantly [])]
      (let [db {:models {"openai/gpt-5.2" {}}}
            config {:providers {"anthropic" {:key nil :keyEnv nil :keyRc nil}
                                "openai" {:keyEnv "OPENAI_API_KEY"}}}]
        (is (= "openai/gpt-5.2" (llm-api/default-model db config))))))

  (testing "Fallback default (no keys anywhere)"
    (with-redefs [config/get-env (constantly nil)
                  secrets/credential-file-paths (constantly [])]
      (let [db {:models {"anthropic/claude-sonnet-4.5" {}
                         "openai/gpt-5.2" {}}}
            config {}]
        (is (= "anthropic/claude-sonnet-4.5" (llm-api/default-model db config))))))

  (testing "Returns nil when no models are available"
    (with-redefs [config/get-env (constantly nil)
                  secrets/credential-file-paths (constantly [])]
      (let [db {:models {}}
            config {}]
        (is (nil? (llm-api/default-model db config))))))

  (testing "Missing configured defaultModel falls back to deterministic first available model"
    (with-redefs [config/get-env (constantly nil)
                  secrets/credential-file-paths (constantly [])]
      (let [db {:models {"z-model" {}
                         "a-model" {}}
                :auth {}}
            config {:defaultModel "missing-model"}]
        (is (= "a-model" (llm-api/default-model db config))))))

  (testing "When key-based default is unavailable, falls back to deterministic first available model"
    (with-redefs [config/get-env (constantly nil)
                  secrets/credential-file-paths (constantly [])]
      (let [db {:models {"openai/gpt-4.1" {}
                         "custom/zeta" {}}
                :auth {}}
            config {:providers {"anthropic" {:key "something"}}}]
        (is (= "custom/zeta" (llm-api/default-model db config)))))))

(deftest provider->api-handler-copilot-metadata-test
  (let [config {:providers {"github-copilot" {:api "openai-chat"}}}]
    (testing "Discovered endpoint overrides the old model-name fallback"
      (is (= :openai-responses
             (:api (llm-api/provider->api-handler
                    "github-copilot" "future-model" {:api :openai-responses} config))))
      (is (= :anthropic
             (:api (llm-api/provider->api-handler
                    "github-copilot" "claude-future" {:api :anthropic} config))))
      (is (= :openai-chat
             (:api (llm-api/provider->api-handler
                    "github-copilot" "gpt-5.5" {:api :openai-chat} config))))
      (is (= :anthropic
             (:api (llm-api/provider->api-handler
                    "github-copilot" "claude-future" {:api "anthropic"} config)))))

    (testing "Existing hardcoded routing remains the fallback without endpoint metadata"
      (is (= :openai-responses
             (:api (llm-api/provider->api-handler "github-copilot" "gpt-5.5" config))))
      (is (= :openai-chat
             (:api (llm-api/provider->api-handler "github-copilot" "gpt-5" config))))
      (is (= :openai-chat
             (:api (llm-api/provider->api-handler "github-copilot" "unknown-model" config)))))))

(deftest prompt-forwards-max-output-tokens-to-ollama-test
  (let [captured* (atom nil)]
    (with-redefs [llm-providers.ollama/chat!
                  (fn [opts _callbacks]
                    (reset! captured* opts)
                    {:output-text "ok"})]
      (#'eca.llm-api/prompt!
       {:provider "ollama"
        :model "test-model"
        :model-capabilities {:tools false
                             :reason? false
                             :model-name "test-model"
                             :max-output-tokens 512}
        :user-messages [{:role "user" :content [{:type :text :text "hello"}]}]
        :past-messages []
        :config {:providers {"ollama" {:url "http://localhost:11434"}}}
        :sync? true}))
    (is (= 512 (:max-output-tokens @captured*)))))

(deftest prompt-test
  (testing "Custom OpenAI provider behavior and proper passing of httpClient options to the Hato client"
    (let [req* (atom nil)]
      (with-client-proxied {}

        (fn handler [req]
          (reset! req* req)
          {:status 200
           :body {:usage {:prompt_tokens 5 :completion_tokens 2}
                  :choices [{:message {:content "hi"
                                       :reasoning_content "think more"}}]}})

        (let [response (#'eca.llm-api/prompt!
                        {:config {:providers {"lmstudio"
                                              {:api "openai-chat",
                                               :url "http://localhost:1234",
                                               :completionUrlRelativePath "/v1/chat/completions",
                                               :httpClient {:version :http-1.1},
                                               :models {"ibm/granite-4-h-tiny" {}}}}}

                         :provider "lmstudio"
                         :model "ibm/granite-4-h-tiny"

                         :model-capabilities {:tools false,
                                              :reason? false,
                                              :web-search false,
                                              :model-name "ibm/granite-4-h-tiny"}
                         :sync? true})]
          (is (= {:method "POST",
                  :uri "/v1/chat/completions"}
                 (select-keys @req* [:method :uri])))
          ;; Verify that a single Hato HTTP client request occurred and used HTTP/1.1
          (is (= [{:version :http-1.1}] (map #(dissoc % :proxy) @*http-client-captures*)))
          (is (= {:usage {:input-tokens 5, :output-tokens 2, :input-cache-read-tokens nil},
                  :tools-to-call (),
                  :reason-text "think more",
                  :reasoning-content "think more",
                  :output-text "hi"}
                 (select-keys response [:usage :tools-to-call :reason-text :reasoning-content :output-text])) response)))))

  (testing "Custom provider allows dynamically discovered models even when not present in provider :models config"
    (let [req* (atom nil)]
      (with-client-proxied {}

        (fn handler [req]
          (reset! req* req)
          {:status 200
           :body {:usage {:prompt_tokens 5 :completion_tokens 2}
                  :choices [{:message {:content "hi"}}]}})

        (let [response (#'eca.llm-api/prompt!
                        {:config {:providers {"synthetic"
                                              {:api "openai-chat"
                                               :url "http://localhost:1234"
                                               :completionUrlRelativePath "/v1/chat/completions"
                                               :httpClient {:version :http-1.1}
                                               :models {}}}}

                         :provider "synthetic"
                         :model "qwen-3-235b-instruct"

                         :model-capabilities {:tools false
                                              :reason? false
                                              :web-search false
                                              :model-name "hf:Qwen/Qwen3-235B-A22B-Instruct-2507"}
                         :sync? true})]
          (is (= {:method "POST"
                  :uri "/v1/chat/completions"}
                 (select-keys @req* [:method :uri])))
          (is (= "hi" (:output-text response))))))))

(deftest prompt-uses-copilot-discovered-variants-test
  (let [base-opts {:provider "github-copilot"
                   :user-messages [{:role "user" :content [{:type :text :text "hi"}]}]
                   :past-messages []
                   :tools []
                   :variant "high"
                   :provider-auth {:api-key "copilot-token"
                                   :api-url "https://api.githubcopilot.com"
                                   :type :auth/oauth}
                   :config {:providers {"github-copilot" {:api "openai-chat"
                                                           :url "https://api.githubcopilot.com"
                                                           :models {}}}}
                   :sync? false}]
    (testing "Chat models receive reasoning_effort"
      (let [captured* (atom nil)]
        (with-redefs [llm-providers.openai-chat/chat-completion!
                      (fn [opts _callbacks] (reset! captured* opts) :ok)]
          (#'eca.llm-api/prompt!
           (assoc base-opts
                  :model "gpt-chat"
                  :model-capabilities {:api :openai-chat
                                       :tools true
                                       :reason? true
                                       :web-search false
                                       :model-name "gpt-chat"
                                       :variants {"high" {:reasoning_effort "high"}}})))
        (is (= "high" (get-in @captured* [:extra-payload :reasoning_effort])))
        (is (nil? (get-in @captured* [:extra-payload :reasoning])))))

    (testing "Responses models receive nested reasoning config"
      (let [captured* (atom nil)]
        (with-redefs [llm-providers.openai/create-response!
                      (fn [opts _callbacks] (reset! captured* opts) :ok)]
          (#'eca.llm-api/prompt!
           (assoc base-opts
                  :model "gpt-responses"
                  :model-capabilities {:api :openai-responses
                                       :tools true
                                       :reason? true
                                       :web-search false
                                       :model-name "gpt-responses"
                                       :variants {"high" {:reasoning {:effort "high" :summary "auto"}}}})))
        (is (= {:effort "high" :summary "auto"}
               (get-in @captured* [:extra-payload :reasoning])))))

    (testing "Messages models receive Anthropic payload and headers"
      (let [captured* (atom nil)]
        (with-redefs [llm-providers.anthropic/chat!
                      (fn [opts _callbacks] (reset! captured* opts) :ok)]
          (#'eca.llm-api/prompt!
           (assoc base-opts
                  :model "claude-adaptive"
                  :model-capabilities {:api :anthropic
                                       :tools true
                                       :reason? true
                                       :web-search false
                                       :model-name "claude-adaptive"
                                       :variants {"high" {:thinking {:type "adaptive"}
                                                          :output_config {:effort "high"}}}})))
        (is (= {:type "adaptive"} (get-in @captured* [:extra-payload :thinking])))
        (is (= {:effort "high"} (get-in @captured* [:extra-payload :output_config])))
        (let [headers ((:extra-headers @captured*) {:body {:messages [{:role "user"}]}})]
          (is (= "interleaved-thinking-2025-05-14" (get headers "anthropic-beta")))
          (is (= "user" (get headers "x-initiator"))))))

    (testing "Messages models with no selected variant fall back to discovered default (#528)"
      (let [captured* (atom nil)]
        (with-redefs [llm-providers.anthropic/chat!
                      (fn [opts _callbacks] (reset! captured* opts) :ok)]
          (#'eca.llm-api/prompt!
           (assoc base-opts
                  :variant nil
                  :model "claude-adaptive"
                  :model-capabilities {:api :anthropic
                                       :tools true
                                       :reason? true
                                       :web-search false
                                       :model-name "claude-adaptive"
                                       :variants {"default" {:thinking {:type "adaptive"}}
                                                  "high" {:thinking {:type "adaptive"}
                                                          :output_config {:effort "high"}}}})))
        (is (= {:type "adaptive"} (get-in @captured* [:extra-payload :thinking])))
        (is (nil? (get-in @captured* [:extra-payload :output_config])))))))

(deftest prompt-passes-image-generation-to-openai-handler-test
  (testing "openai branch forwards :image-generation true to create-response! when capability is on"
    (let [captured* (atom nil)]
      (with-redefs [llm-providers.openai/create-response!
                    (fn [opts _callbacks] (reset! captured* opts) :ok)]
        (#'eca.llm-api/prompt!
         {:provider "openai"
          :model "gpt-5.2"
          :model-capabilities {:tools true
                               :reason? false
                               :web-search false
                               :image-generation? true
                               :model-name "gpt-5.2"}
          :user-messages [{:role "user" :content [{:type :text :text "hi"}]}]
          :past-messages []
          :tools []
          :provider-auth {:api-key "test-key"}
          :config {:providers {"openai" {:url "https://api.openai.com" :key "test-key"}}}
          :sync? false}))
      (is (= true (:image-generation @captured*))
          "openai handler should receive :image-generation true")))

  (testing "openai branch strips internal top-level message fields before reaching handler"
    (let [captured* (atom nil)]
      (with-redefs [llm-providers.openai/create-response!
                    (fn [opts _callbacks] (reset! captured* opts) :ok)]
        (#'eca.llm-api/prompt!
         {:provider "openai"
          :model "gpt-5.2"
          :model-capabilities {:tools true
                               :reason? false
                               :web-search false
                               :image-generation? false
                               :model-name "gpt-5.2"}
          :user-messages [{:role "user"
                           :content [{:type :text :text "hi"}]
                           :created-at 111
                           :content-id "user-1"}]
          :past-messages [{:role "assistant"
                           :content [{:type :text :text "ok"}]
                           :created-at 222
                           :content-id "past-1"}]
          :tools []
          :provider-auth {:api-key "test-key"}
          :config {:providers {"openai" {:url "https://api.openai.com" :key "test-key"}}}
          :sync? false}))
      (is (= [{:role "user" :content [{:type :text :text "hi"}]}]
             (:user-messages @captured*)))
      (is (= [{:role "assistant" :content [{:type :text :text "ok"}]}]
             (:past-messages @captured*)))))

  (testing "openai tool-call replay strips internal top-level message fields before follow-up request"
    (let [seen-bodies* (atom [])]
      (with-redefs [eca.llm-providers.openai/normalize-messages
                    (fn [messages _supports-image?] (vec messages))
                    eca.llm-providers.openai/base-responses-request!
                    (fn [{:keys [body on-stream]}]
                      (swap! seen-bodies* conj body)
                      (if (= 1 (count @seen-bodies*))
                        (on-stream "response.completed"
                                   {:response {:status "completed"
                                               :usage {:input_tokens 1 :output_tokens 1}
                                               :output [{:type "function_call"
                                                         :id "item_1"
                                                         :call_id "call_1"
                                                         :name "demo/tool"
                                                         :arguments "{}"}]}})
                        (on-stream "response.completed"
                                   {:response {:status "completed"
                                               :usage {:input_tokens 1 :output_tokens 1}
                                               :output []}}))
                      :ok)]
        (llm-providers.openai/create-response!
         {:model "gpt-5.2"
          :instructions nil
          :reason? false
          :supports-image? false
          :api-key "test-key"
          :api-url "https://api.openai.com"
          :past-messages []
          :user-messages [{:role "user" :content [{:type :text :text "hi"}]}]
          :tools [{:full-name "demo/tool" :description "d" :parameters {}}]
          :web-search false
          :image-generation false}
         {:on-message-received identity
          :on-error identity
          :on-prepare-tool-call identity
          :on-reason identity
          :on-usage-updated identity
          :on-server-web-search identity
          :on-server-image-generation identity
          :on-tools-called (fn [_]
                             {:new-messages [{:role "assistant"
                                              :content [{:type :text :text "after tool"}]
                                              :created-at 333
                                              :content-id "replay-1"}]
                              :tools []})}))
      (is (= 2 (count @seen-bodies*)))
      (is (= [{:role "assistant"
               :content [{:type :text :text "after tool"}]}]
             (:input (second @seen-bodies*))))))

  (testing "openai branch forwards :image-generation false (or nil) when capability is off"
    (let [captured* (atom nil)]
      (with-redefs [llm-providers.openai/create-response!
                    (fn [opts _callbacks] (reset! captured* opts) :ok)]
        (#'eca.llm-api/prompt!
         {:provider "openai"
          :model "gpt-4-legacy"
          :model-capabilities {:tools true
                               :reason? false
                               :web-search false
                               :image-generation? false
                               :model-name "gpt-4-legacy"}
          :user-messages [{:role "user" :content [{:type :text :text "hi"}]}]
          :past-messages []
          :tools []
          :provider-auth {:api-key "test-key"}
          :config {:providers {"openai" {:url "https://api.openai.com" :key "test-key"}}}
          :sync? false}))
      (is (not (true? (:image-generation @captured*)))
          "openai handler should NOT receive :image-generation true when capability is off"))))

(deftest prompt-forwards-codex-decision-inputs-only-for-openai-test
  (let [base-opts {:model "gpt-5.6-sol"
                   :model-capabilities {:api :openai-responses
                                        :tools true
                                        :reason? true
                                        :web-search false
                                        :model-name "gpt-5.6-sol"
                                        :provider-data {:responses-lite? true
                                                        :default-reasoning-effort "low"}}
                   :instructions "test"
                   :user-messages [{:role "user" :content [{:type :text :text "hi"}]}]
                   :past-messages []
                   :tools []
                   :sync? false}]
    (testing "the openai handler receives provider, auth-type and opaque provider-data"
      (let [captured* (atom nil)]
        (with-redefs [llm-providers.openai/create-response!
                      (fn [opts _callbacks] (reset! captured* opts) :ok)]
          (#'eca.llm-api/prompt!
           (merge base-opts
                  {:provider "openai"
                   :provider-auth {:api-key "oauth-token"
                                   :type :auth/oauth
                                   :account-id "account-1"}
                   :config {:providers {"openai" {:api "openai-responses"
                                                  :url "https://api.openai.com"
                                                  :models {}}}}})))
        (is (= "openai" (:provider @captured*)))
        (is (= :auth/oauth (:auth-type @captured*)))
        (is (= {:responses-lite? true
                :default-reasoning-effort "low"}
               (:provider-data @captured*)))))

    (testing "OpenAI API keys forward their auth-type so no Codex behavior applies"
      (let [captured* (atom nil)]
        (with-redefs [llm-providers.openai/create-response!
                      (fn [opts _callbacks] (reset! captured* opts) :ok)]
          (#'eca.llm-api/prompt!
           (merge base-opts
                  {:provider "openai"
                   :provider-auth {:api-key "sk-test" :type :auth/api-key}
                   :config {:providers {"openai" {:api "openai-responses"
                                                  :url "https://api.openai.com"
                                                  :models {}}}}})))
        (is (= "openai" (:provider @captured*)))
        (is (= :auth/api-key (:auth-type @captured*)))))

    (testing "Copilot and custom Responses providers receive no provider identity"
      (doseq [[provider provider-auth provider-config]
              [["github-copilot"
                {:api-key "copilot-token"
                 :api-url "https://api.githubcopilot.com"
                 :type :auth/oauth}
                {:api "openai-chat"
                 :url "https://api.githubcopilot.com"
                 :models {}}]
               ["gateway"
                {:api-key "gateway-token" :type :auth/api-key}
                {:api "openai-responses"
                 :url "https://gateway.example.com"
                 :models {"gpt-5.6-sol" {}}}]]]
        (let [captured* (atom nil)]
          (with-redefs [llm-providers.openai/create-response!
                        (fn [opts _callbacks] (reset! captured* opts) :ok)]
            (#'eca.llm-api/prompt!
             (merge base-opts
                    {:provider provider
                     :provider-auth provider-auth
                     :config {:providers {provider provider-config}}})))
          (is (nil? (:provider @captured*))
              (str provider " must not inherit ChatGPT Codex transport"))
          (is (nil? (:auth-type @captured*))))))))

(deftest oauth-gpt-5-6-lite-retries-post-tool-request-test
  (testing "GPT-5.6 Lite keeps routing state and executes the tool once across overload retry"
    (let [requests* (atom [])
          tools-called* (atom 0)
          messages* (atom [])
          terminal-errors* (atom [])
          stream (fn [text]
                   (java.io.ByteArrayInputStream.
                    (.getBytes ^String text java.nio.charset.StandardCharsets/UTF_8)))
          tool-stream (str
                       "event: response.completed\n"
                       "data: {\"type\":\"response.completed\",\"response\":{\"status\":\"completed\",\"output\":[{\"type\":\"function_call\",\"id\":\"item-1\",\"call_id\":\"call-1\",\"name\":\"eca__directory_tree\",\"arguments\":\"{}\"}],\"usage\":{\"input_tokens\":1,\"output_tokens\":1}}}\n\n")
          overloaded-stream (str
                             "event: error\n"
                             "data: {\"type\":\"service_unavailable_error\",\"code\":\"server_is_overloaded\",\"message\":\"Our servers are currently overloaded.\"}\n\n")
          final-stream (str
                        "event: response.output_text.delta\n"
                        "data: {\"type\":\"response.output_text.delta\",\"delta\":\"done\"}\n\n"
                        "event: response.completed\n"
                        "data: {\"type\":\"response.completed\",\"response\":{\"status\":\"completed\",\"output\":[],\"usage\":{\"input_tokens\":1,\"output_tokens\":1}}}\n\n")]
      (with-redefs [http/post
                    (fn [url opts]
                      (let [request-number (inc (count @requests*))
                            body (json/parse-string (:body opts) true)]
                        (swap! requests* conj {:url url
                                              :headers (:headers opts)
                                              :body body})
                        {:status 200
                         :headers {"x-codex-turn-state"
                                   (if (= 1 request-number) "state-1" "state-2")}
                         :body (stream (case request-number
                                         1 tool-stream
                                         2 overloaded-stream
                                         final-stream))}))
                    eca.llm-api/sleep-with-cancel (fn [_ _] true)]
        (llm-api/sync-or-async-prompt!
         {:provider "openai"
          :model "gpt-5.6-sol"
          :model-capabilities {:api :openai-responses
                               :model-name "gpt-5.6-sol"
                               :tools true
                               :reason? true
                               :web-search true
                               :image-generation? true
                               :provider-data {:responses-lite? true
                                               :default-reasoning-effort "low"}}
          :instructions "test"
          :user-messages [{:role "user" :content [{:type :text :text "hello"}]}]
          :past-messages []
          :tools [{:full-name "eca__directory_tree"
                   :description "list"
                   :parameters {:type "object"}}]
          :config {:providers {"openai" {:api "openai-responses"
                                         :url "https://api.openai.com"
                                         :models {"gpt-5.6-sol" {}}}}}
          :provider-auth {:api-key "oauth-token"
                          :account-id "account-1"
                          :type :auth/oauth}
          :on-message-received #(swap! messages* conj %)
          :on-error #(swap! terminal-errors* conj %)
          :on-tools-called (fn [_]
                             (swap! tools-called* inc)
                             {:new-messages [{:role "tool_call_output"
                                              :content {:id "call-1"
                                                        :output {:contents [{:type :text
                                                                            :text "result"}]}}}]
                              :tools []})})

        (is (= 3 (count @requests*)))
        (is (= 1 @tools-called*))
        (is (empty? @terminal-errors*))
        (is (some #(= {:type :text :text "done"} %) @messages*))
        (doseq [{:keys [body]} @requests*]
          (is (= "additional_tools" (get-in body [:input 0 :type])))
          (is (nil? (:instructions body)))
          (is (nil? (:tools body)))
          (is (false? (:parallel_tool_calls body))))
        (is (nil? (get-in (first @requests*) [:headers "x-codex-turn-state"])))
        (is (= ["state-1" "state-1"]
               (mapv #(get-in % [:headers "x-codex-turn-state"])
                     (rest @requests*))))
        (let [session-ids (mapv #(get-in % [:headers "Session-ID"]) @requests*)]
          (is (every? string? session-ids))
          (is (= 1 (count (distinct session-ids)))
              "every request in the turn shares one Session-ID"))))))

(deftest prompt-forwards-stream-idle-timeout-and-cache-retention-to-anthropic-handler-test
  (testing "custom provider with :api anthropic forwards :stream-idle-timeout-seconds and :cache-retention to chat!"
    (let [captured* (atom nil)]
      (with-redefs [llm-providers.anthropic/chat!
                    (fn [opts _callbacks] (reset! captured* opts) :ok)]
        (#'eca.llm-api/prompt!
         {:provider "my-proxy"
          :model "claude-sonnet-4-6"
          :model-capabilities {:tools true
                               :reason? false
                               :web-search false
                               :model-name "claude-sonnet-4-6"}
          :user-messages [{:role "user" :content [{:type :text :text "hi"}]}]
          :past-messages []
          :tools []
          :provider-auth {:api-key "test-key"}
          :config {:streamIdleTimeoutSeconds 300
                   :providers {"my-proxy" {:api "anthropic"
                                           :url "https://my-proxy.example.com/v1"
                                           :key "test-key"
                                           :cacheRetention "long"
                                           :models {"claude-sonnet-4-6" {}}}}}
          :sync? false}))
      (is (= "long" (:cache-retention @captured*))
          "anthropic handler should receive :cache-retention from provider-config")
      (is (= 300 (:stream-idle-timeout-seconds @captured*))
          "anthropic handler should receive :stream-idle-timeout-seconds from top-level config"))))

(deftest prompt-routes-to-gemini-handler-test
  (testing "custom provider with :api gemini routes to llm-providers.gemini/chat!"
    (let [captured* (atom nil)]
      (with-redefs [llm-providers.gemini/chat!
                    (fn [opts _callbacks] (reset! captured* opts) :ok)]
        (#'eca.llm-api/prompt!
         {:provider "my-gemini"
          :model "gemini-3-pro"
          :model-capabilities {:tools true
                               :reason? true
                               :model-name "gemini-3-pro"}
          :user-messages [{:role "user" :content [{:type :text :text "hi"}]}]
          :past-messages []
          :tools []
          :provider-auth {:api-key "test-key"}
          :config {:streamIdleTimeoutSeconds 180
                   :providers {"my-gemini" {:api "gemini"
                                            :url "https://my-gemini.example.com"
                                            :key "test-key"
                                            :completionUrlRelativePath "/v1beta/custom/{model}:streamGenerateContent"
                                            :extraHeaders {"X-Custom" "header-val"}
                                            :models {"gemini-3-pro" {:extraPayload {:generationConfig {:temperature 0.7}}}}}}}
          :sync? false}))
      (is (some? @captured*))
      (is (= "gemini-3-pro" (:model @captured*)))
      (is (= "https://my-gemini.example.com" (:api-url @captured*)))
      (is (= "/v1beta/custom/{model}:streamGenerateContent" (:url-relative-path @captured*)))
      (is (= {"X-Custom" "header-val"} (:extra-headers @captured*)))
      (is (= {:generationConfig {:temperature 0.7}} (:extra-payload @captured*)))
      (is (= 180 (:stream-idle-timeout-seconds @captured*))))))

(deftest prompt-merges-provider-and-model-extra-headers-test
  (testing "provider-level extraHeaders are sent and model-level ones win on conflicts"
    (let [captured* (atom nil)]
      (with-redefs [llm-providers.openai/create-response!
                    (fn [opts _callbacks] (reset! captured* opts) :ok)]
        (#'eca.llm-api/prompt!
         {:provider "openai"
          :model "gpt-5.2"
          :model-capabilities {:tools true
                               :reason? false
                               :web-search false
                               :model-name "gpt-5.2"}
          :user-messages [{:role "user" :content [{:type :text :text "hi"}]}]
          :past-messages []
          :tools []
          :provider-auth {:api-key "test-key"}
          :config {:providers {"openai" {:url "https://api.openai.com"
                                         :key "test-key"
                                         :extraHeaders {"Ocp-Apim-Subscription-Key" "prov-secret"
                                                        "X-Shared" "provider"}
                                         :models {"gpt-5.2" {:extraHeaders {"X-Shared" "model"}}}}}}
          :sync? false}))
      (is (= {"Ocp-Apim-Subscription-Key" "prov-secret"
              "X-Shared" "model"}
             (:extra-headers @captured*))))))

(deftest retry-delay-ms-test
  ;; Formula: (quot capped 2) + rand(0, capped)
  ;; Range: [capped/2, capped/2 + capped) = [capped/2, capped*3/2)
  (testing "exponential backoff with jitter stays within bounds"
    (dotimes [_ 50]
      (let [d0 (#'llm-api/retry-delay-ms 0)]
        (is (<= 1000 d0 3000) "attempt 0: base 2s")))
    (dotimes [_ 50]
      (let [d1 (#'llm-api/retry-delay-ms 1)]
        (is (<= 2000 d1 6000) "attempt 1: base 4s")))
    (dotimes [_ 50]
      (let [d2 (#'llm-api/retry-delay-ms 2)]
        (is (<= 4000 d2 12000) "attempt 2: base 8s"))))

  (testing "capped at max-delay-ms for high attempts"
    (dotimes [_ 50]
      (let [d9 (#'llm-api/retry-delay-ms 9)]
        (is (<= 30000 d9 90000) "attempt 9: capped at 60s base"))))

  (testing "uses configured base, multiplier, and cap"
    (with-redefs [clojure.core/rand (constantly 0)]
      (let [policy {:base-delay-ms 100
                    :backoff-multiplier 3.0
                    :max-delay-ms 250}]
        (is (= 50 (#'llm-api/retry-delay-ms 0 policy)))
        (is (= 125 (#'llm-api/retry-delay-ms 1 policy)))
        (is (= 125 (#'llm-api/retry-delay-ms 2 policy)))))))

(deftest retry-policy-test
  (testing "defaults preserve existing retry behavior"
    (is (= {:max-retries 10
            :base-delay-ms 2000
            :backoff-multiplier 2.0
            :max-delay-ms 60000}
           (#'llm-api/retry-policy {} :overloaded)))
    (is (= 3 (:max-retries (#'llm-api/retry-policy {} :premature-stop)))))

  (testing "provider retry configuration overrides the defaults"
    (let [provider-config {:retry {:maxRetries 6
                                   :prematureStopMaxRetries 4
                                   :baseDelayMs 500
                                   :backoffMultiplier 1.5
                                   :maxDelayMs 10000}}]
      (is (= {:max-retries 6
              :base-delay-ms 500
              :backoff-multiplier 1.5
              :max-delay-ms 10000}
             (#'llm-api/retry-policy provider-config :overloaded)))
      (is (= 4 (:max-retries (#'llm-api/retry-policy provider-config :premature-stop))))))

  (testing "invalid values fall back to safe defaults"
    (let [policy (#'llm-api/retry-policy
                  {:retry {:maxRetries -1
                           :baseDelayMs -2
                           :backoffMultiplier 0
                           :maxDelayMs -3}}
                  :overloaded)]
      (is (= 10 (:max-retries policy)))
      (is (= 2000 (:base-delay-ms policy)))
      (is (= 2.0 (:backoff-multiplier policy)))
      (is (= 60000 (:max-delay-ms policy))))))

(deftest sleep-with-cancel-test
  (testing "completes when not cancelled"
    (is (true? (#'llm-api/sleep-with-cancel 50 (constantly false)))))

  (testing "returns false when already cancelled"
    (is (false? (#'llm-api/sleep-with-cancel 1000 (constantly true)))))

  (testing "returns false when cancelled during sleep"
    (let [cancelled* (atom false)
          result (future (#'llm-api/sleep-with-cancel 5000 #(deref cancelled*)))]
      (Thread/sleep 200)
      (reset! cancelled* true)
      (is (false? (deref result 2000 :timeout))))))

(defn- make-prompt-opts
  "Creates minimal sync-or-async-prompt! opts for testing retry behavior.
   Pass :stream false in overrides for sync mode, defaults to async (stream true)."
  [overrides]
  (let [stream (get overrides :stream true)]
    (merge {:provider "anthropic"
            :model "claude-sonnet-4-6"
            :model-capabilities {:tools false :reason? false :web-search false}
            :instructions "test"
            :user-messages [{:role "user" :content [{:type :text :text "hello"}]}]
            :past-messages []
            :tools []
            :config {:providers {"anthropic" {:key "test-key"
                                              :url "http://test"
                                              :models {"claude-sonnet-4-6" {:extraPayload {:stream stream}}}}}}
            :provider-auth {:api-key "test-key"}}
           (dissoc overrides :stream))))

(deftest refresh-provider-auth-fn-used-for-initial-and-retry-test
  (testing "refresh-provider-auth-fn supplies a fresh provider-auth on each prompt! call"
    ;; Previously provider-auth was captured once and reused across retries,
    ;; so expired tokens caused all retries to fail.
    (let [refresh-calls* (atom 0)
          seen-api-keys* (atom [])]
      (with-redefs [eca.llm-api/prompt! (fn [{:keys [provider-auth]}]
                                          (swap! seen-api-keys* conj (:api-key provider-auth))
                                          (let [attempt (count @seen-api-keys*)]
                                            (if (= 1 attempt)
                                              {:error {:status 429
                                                       :body "Rate limit exceeded"
                                                       :message "LLM response status: 429"}}
                                              {:output-text "success"
                                               :usage {:input-tokens 1 :output-tokens 1}})))
                    eca.llm-api/sleep-with-cancel (fn [_ cancelled?] (not (cancelled?)))]
        (llm-api/sync-or-async-prompt!
         (make-prompt-opts
          {:stream false
           :provider-auth {:api-key "stale-token"}
           :refresh-provider-auth-fn (fn []
                                       (let [n (swap! refresh-calls* inc)]
                                         {:api-key (str "fresh-token-" n)}))
           :on-error identity
           :on-message-received identity})))
      (is (= 2 @refresh-calls*)
          "refresh-provider-auth-fn is called once per prompt! attempt (initial + retry)")
      (is (= ["fresh-token-1" "fresh-token-2"] @seen-api-keys*)
          "each prompt! invocation (including the retry) must see a freshly read api-key")))

  (testing "falls back to captured provider-auth when refresh-provider-auth-fn throws"
    (let [seen-api-keys* (atom [])]
      (with-redefs [eca.llm-api/prompt! (fn [{:keys [provider-auth]}]
                                          (swap! seen-api-keys* conj (:api-key provider-auth))
                                          {:output-text "ok"
                                           :usage {:input-tokens 1 :output-tokens 1}})
                    eca.llm-api/sleep-with-cancel (fn [_ _] true)]
        (llm-api/sync-or-async-prompt!
         (make-prompt-opts
          {:stream false
           :provider-auth {:api-key "fallback-token"}
           :refresh-provider-auth-fn (fn [] (throw (ex-info "boom" {})))
           :on-error identity
           :on-message-received identity})))
      (is (= ["fallback-token"] @seen-api-keys*)
          "when refresh-provider-auth-fn throws, prompt! keeps running with the statically captured provider-auth"))))

(deftest sync-retry-on-rate-limited-test
  (testing "retries on 429 and succeeds on subsequent attempt"
    (let [attempt* (atom 0)
          retry-events* (atom [])
          on-error-called* (atom false)]
      (with-redefs [eca.llm-api/prompt! (fn [_opts]
                                          (let [attempt (swap! attempt* inc)]
                                            (if (= 1 attempt)
                                              {:error {:status 429
                                                       :body "Rate limit exceeded"
                                                       :message "LLM response status: 429"}}
                                              {:output-text "success"
                                               :usage {:input-tokens 10 :output-tokens 5}})))
                    eca.llm-api/sleep-with-cancel (fn [_ cancelled?] (not (cancelled?)))]
        (llm-api/sync-or-async-prompt!
         (make-prompt-opts
          {:stream false
           :on-retry (fn [event] (swap! retry-events* conj event))
           :on-error (fn [_] (reset! on-error-called* true))
           :on-message-received identity})))
      (is (= 2 @attempt*))
      (is (= 1 (count @retry-events*)))
      (is (= 1 (:attempt (first @retry-events*))))
      (is (false? @on-error-called*)))))

(deftest sync-retry-uses-rate-limit-header-delay-test
  (testing "429 with retry-after header sleeps until reset and passes resets-at"
    (let [attempt* (atom 0)
          retry-events* (atom [])
          slept* (atom [])]
      (with-redefs [eca.llm-api/prompt! (fn [_]
                                          (let [attempt (swap! attempt* inc)]
                                            (if (= 1 attempt)
                                              {:error {:status 429
                                                       :body "Rate limit exceeded"
                                                       :message "LLM response status: 429"
                                                       :headers {"retry-after" "7"}}}
                                              {:output-text "success"
                                               :usage {:input-tokens 1 :output-tokens 1}})))
                    eca.llm-api/sleep-with-cancel (fn [delay-ms cancelled?]
                                                    (swap! slept* conj delay-ms)
                                                    (not (cancelled?)))]
        (llm-api/sync-or-async-prompt!
         (make-prompt-opts
          {:stream false
           :on-retry (fn [event] (swap! retry-events* conj event))
           :on-error identity
           :on-message-received identity})))
      (is (= 2 @attempt*))
      (is (= [8000] @slept*) "7s from retry-after header + 1s buffer, no exponential backoff")
      (let [event (first @retry-events*)]
        (is (= 8000 (:delay-ms event)))
        (is (number? (:resets-at event)))))))

(deftest sync-rate-limit-default-max-wait-test
  (testing "default rateLimitMaxWaitSeconds rejects a long Codex usage-limit reset"
    (let [attempt* (atom 0)
          error* (atom nil)
          slept* (atom [])]
      (with-redefs [eca.llm-api/prompt! (fn [_]
                                          (swap! attempt* inc)
                                          (let [reset-epoch-s (+ (quot (System/currentTimeMillis) 1000) 7200)]
                                            {:error {:status 429
                                                     :body {:error {:type "usage_limit_reached"
                                                                    :resets_at reset-epoch-s}}
                                                     :message "OpenAI response status: 429"}}))
                    eca.llm-api/sleep-with-cancel (fn [delay-ms _]
                                                    (swap! slept* conj delay-ms)
                                                    true)]
        (llm-api/sync-or-async-prompt!
         (make-prompt-opts
          {:stream false
           :on-error (fn [error] (reset! error* error))
           :on-message-received identity})))
      (is (= 1 @attempt*))
      (is (empty? @slept*))
      (is (number? (:rate-limit-resets-at @error*))))))

(deftest rate-limit-default-max-wait-buffer-boundary-test
  (testing "59-second provider reset is allowed because the buffered delay is exactly 60 seconds"
    (let [attempt* (atom 0)
          slept* (atom [])]
      (with-redefs [eca.llm-api/prompt! (fn [_]
                                          (let [attempt (swap! attempt* inc)]
                                            (if (= 1 attempt)
                                              {:error {:status 429
                                                       :body "Rate limit exceeded"
                                                       :message "LLM response status: 429"
                                                       :headers {"retry-after" "59"}}}
                                              {:output-text "success"
                                               :usage {:input-tokens 1 :output-tokens 1}})))
                    eca.llm-api/sleep-with-cancel (fn [delay-ms cancelled?]
                                                    (swap! slept* conj delay-ms)
                                                    (not (cancelled?)))]
        (llm-api/sync-or-async-prompt!
         (make-prompt-opts
          {:stream false
           :on-error identity
           :on-message-received identity})))
      (is (= 2 @attempt*))
      (is (= [60000] @slept*))))

  (testing "60-second provider reset is rejected because the safety buffer makes 61 seconds"
    (let [attempt* (atom 0)
          error* (atom nil)
          slept* (atom [])]
      (with-redefs [eca.llm-api/prompt! (fn [_]
                                          (swap! attempt* inc)
                                          {:error {:status 429
                                                   :body "Rate limit exceeded"
                                                   :message "LLM response status: 429"
                                                   :headers {"retry-after" "60"}}})
                    eca.llm-api/sleep-with-cancel (fn [delay-ms _]
                                                    (swap! slept* conj delay-ms)
                                                    true)]
        (llm-api/sync-or-async-prompt!
         (make-prompt-opts
          {:stream false
           :on-error (fn [error] (reset! error* error))
           :on-message-received identity})))
      (is (= 1 @attempt*))
      (is (empty? @slept*))
      (is (number? (:rate-limit-resets-at @error*))))))

(deftest rate-limit-max-wait-config-override-test
  (testing "provider rateLimitMaxWaitSeconds lower than reset wait disables the retry, exposing resets-at"
    (let [attempt* (atom 0)
          error* (atom nil)]
      (with-redefs [eca.llm-api/prompt! (fn [_]
                                          (swap! attempt* inc)
                                          {:error {:status 429
                                                   :body "Rate limit exceeded"
                                                   :message "LLM response status: 429"
                                                   :headers {"retry-after" "60"}}})
                    eca.llm-api/sleep-with-cancel (fn [_ _] true)]
        (llm-api/sync-or-async-prompt!
         (make-prompt-opts
          {:stream false
           :config {:providers {"anthropic" {:key "test-key"
                                             :url "http://test"
                                             :rateLimitMaxWaitSeconds 30
                                             :models {"claude-sonnet-4-6" {:extraPayload {:stream false}}}}}}
           :on-error (fn [e] (reset! error* e))
           :on-message-received identity})))
      (is (= 1 @attempt*))
      (is (number? (:rate-limit-resets-at @error*)))))

  (testing "provider rateLimitMaxWaitSeconds higher than reset wait allows the retry"
    (let [attempt* (atom 0)
          slept* (atom [])]
      (with-redefs [eca.llm-api/prompt! (fn [_]
                                          (let [attempt (swap! attempt* inc)]
                                            (if (= 1 attempt)
                                              {:error {:status 429
                                                       :body "Rate limit exceeded"
                                                       :message "LLM response status: 429"
                                                       :headers {"retry-after" "60"}}}
                                              {:output-text "success"
                                               :usage {:input-tokens 1 :output-tokens 1}})))
                    eca.llm-api/sleep-with-cancel (fn [delay-ms cancelled?]
                                                    (swap! slept* conj delay-ms)
                                                    (not (cancelled?)))]
        (llm-api/sync-or-async-prompt!
         (make-prompt-opts
          {:stream false
           :config {:providers {"anthropic" {:key "test-key"
                                             :url "http://test"
                                             :rateLimitMaxWaitSeconds 120
                                             :models {"claude-sonnet-4-6" {:extraPayload {:stream false}}}}}}
           :on-error identity
           :on-message-received identity})))
      (is (= 2 @attempt*))
      (is (= [61000] @slept*)))))

(deftest sync-no-retry-on-auth-error-test
  (testing "does not retry on auth errors (401)"
    (let [attempt* (atom 0)
          on-error-called* (atom false)]
      (with-redefs [eca.llm-api/prompt! (fn [_]
                                          (swap! attempt* inc)
                                          {:error {:status 401
                                                   :body "Unauthorized"
                                                   :message "LLM response status: 401"}})
                    eca.llm-api/sleep-with-cancel (fn [_ _] true)]
        (llm-api/sync-or-async-prompt!
         (make-prompt-opts
          {:stream false
           :on-error (fn [_] (reset! on-error-called* true))
           :on-message-received identity})))
      (is (= 1 @attempt*))
      (is (true? @on-error-called*)))))

(deftest async-retry-on-custom-retry-rule-error-pattern-test
  (testing "retries async when custom retryRules errorPattern matches error message"
    (let [attempt* (atom 0)
          retry-events* (atom [])
          received-text* (atom "")
          on-error-called* (atom false)]
      (with-redefs [eca.llm-api/prompt! (fn [{:keys [on-message-received on-error]}]
                                          (let [attempt (swap! attempt* inc)]
                                            (if (= 1 attempt)
                                              (on-error {:message "Remote host terminated the handshake"})
                                              (do
                                                (on-message-received {:type :text :text "hello"})
                                                (on-message-received {:type :finish :finish-reason "stop"})))))
                    eca.llm-api/sleep-with-cancel (fn [_ cancelled?] (not (cancelled?)))]
        (llm-api/sync-or-async-prompt!
         (make-prompt-opts
          {:config {:providers {"anthropic" {:key "test-key"
                                             :url "http://test"
                                             :retryRules [{:errorPattern "terminated.*handshake"
                                                           :label "TLS handshake failed"}]
                                             :models {"claude-sonnet-4-6" {}}}}}
           :on-retry (fn [event] (swap! retry-events* conj event))
           :on-error (fn [_] (reset! on-error-called* true))
           :on-message-received (fn [{:keys [type text]}]
                                  (when (= :text type)
                                    (swap! received-text* str text)))})))
      (is (= 2 @attempt*))
      (is (= 1 (count @retry-events*)))
      (is (= "TLS handshake failed" (get-in (first @retry-events*) [:classified :error/label])))
      (is (false? @on-error-called*))
      (is (= "hello" @received-text*)))))

(deftest sync-retry-exhaustion-test
  (testing "calls on-error after all retries exhausted"
    (let [attempt* (atom 0)
          on-error-called* (atom false)]
      (with-redefs [eca.llm-api/prompt! (fn [_]
                                          (swap! attempt* inc)
                                          {:error {:status 429
                                                   :body "Rate limit exceeded"
                                                   :message "LLM response status: 429"}})
                    eca.llm-api/default-max-retries 3
                    eca.llm-api/sleep-with-cancel (fn [_ _] true)]
        (llm-api/sync-or-async-prompt!
         (make-prompt-opts
          {:stream false
           :on-error (fn [_] (reset! on-error-called* true))
           :on-message-received identity})))
      (is (= 4 @attempt*) "1 initial + 3 retries")
      (is (true? @on-error-called*)))))

(deftest configured-retry-policy-test
  (testing "uses configured retry count and backoff for a 503 auth_unavailable response"
    (let [attempt* (atom 0)
          delays* (atom [])
          retry-events* (atom [])
          final-error* (atom nil)
          error {:status 503
                 :body "{\"error\":{\"message\":\"auth_unavailable: no auth available (providers=codex, model=gpt-5.6-sol)\",\"type\":\"server_error\",\"code\":\"internal_server_error\"}}"
                 :message "OpenAI response status: 503 body: auth_unavailable"}
          delivered-error (assoc error
                                 :message "Anthropic server_error: auth_unavailable: no auth available (providers=codex, model=gpt-5.6-sol)"
                                 :code "server_error")]
      (with-redefs [eca.llm-api/prompt! (fn [_]
                                          (swap! attempt* inc)
                                          {:error error})
                    clojure.core/rand (constantly 0)
                    eca.llm-api/sleep-with-cancel (fn [delay-ms _]
                                                    (swap! delays* conj delay-ms)
                                                    true)]
        (llm-api/sync-or-async-prompt!
         (make-prompt-opts
          {:stream false
           :config {:providers {"anthropic" {:key "test-key"
                                               :url "http://test"
                                               :retry {:maxRetries 2
                                                       :baseDelayMs 100
                                                       :backoffMultiplier 3
                                                       :maxDelayMs 250}
                                               :models {"claude-sonnet-4-6" {:extraPayload {:stream false}}}}}}
           :on-retry (fn [event] (swap! retry-events* conj event))
           :on-error #(reset! final-error* %)
           :on-message-received identity})))
      (is (= 3 @attempt*) "one initial request plus two configured retries")
      (is (= [50 125] @delays*))
      (is (= 2 (count @retry-events*)))
      (is (= {:max-retries 2
              :base-delay-ms 100
              :backoff-multiplier 3.0
              :max-delay-ms 250}
             (:policy (first @retry-events*))))
      (is (= delivered-error @final-error*)))))

(deftest sync-retry-cancelled-test
  (testing "stops retrying when cancelled"
    (let [attempt* (atom 0)
          on-error-called* (atom false)
          cancelled* (atom false)]
      (with-redefs [eca.llm-api/prompt! (fn [_]
                                          (let [attempt (swap! attempt* inc)]
                                            (when (= 2 attempt)
                                              (reset! cancelled* true))
                                            {:error {:status 429
                                                     :body "Rate limit exceeded"
                                                     :message "LLM response status: 429"}}))
                    eca.llm-api/sleep-with-cancel (fn [_ cancelled?] (not (cancelled?)))]
        (llm-api/sync-or-async-prompt!
         (make-prompt-opts
          {:stream false
           :cancelled? #(deref cancelled*)
           :on-error (fn [_] (reset! on-error-called* true))
           :on-message-received identity})))
      (is (<= @attempt* 3) "should stop after cancellation")
      (is (true? @on-error-called*)))))

(deftest async-retry-on-overloaded-test
  (testing "retries async streaming on 503 overloaded and succeeds"
    (let [attempt* (atom 0)
          retry-events* (atom [])
          received-text* (atom "")
          on-error-called* (atom false)]
      (with-redefs [eca.llm-api/prompt! (fn [{:keys [on-message-received on-error]}]
                                          (let [attempt (swap! attempt* inc)]
                                            (if (= 1 attempt)
                                              (on-error {:status 503
                                                         :body "Service temporarily unavailable"
                                                         :message "LLM response status: 503"})
                                              (do
                                                (on-message-received {:type :text :text "hello"})
                                                (on-message-received {:type :finish :finish-reason "stop"})))))
                    eca.llm-api/sleep-with-cancel (fn [_ cancelled?] (not (cancelled?)))]
        (llm-api/sync-or-async-prompt!
         (make-prompt-opts
          {:on-retry (fn [event] (swap! retry-events* conj event))
           :on-error (fn [_] (reset! on-error-called* true))
           :on-message-received (fn [{:keys [type text]}]
                                  (when (= :text type)
                                    (swap! received-text* str text)))})))
      (is (= 2 @attempt*))
      (is (= 1 (count @retry-events*)))
      (is (false? @on-error-called*))
      (is (= "hello" @received-text*)))))

(deftest first-response-callback-is-atomic-test
  (testing "concurrent output callbacks emit first response once"
    (let [first-response-count* (atom 0)]
      (with-redefs [eca.llm-api/prompt! (fn [{:keys [on-message-received]}]
                                          (let [ready (java.util.concurrent.CountDownLatch. 2)
                                                go (java.util.concurrent.CountDownLatch. 1)
                                                workers (mapv (fn [text]
                                                                (future
                                                                  (.countDown ready)
                                                                  (.await go)
                                                                  (on-message-received {:type :text :text text})))
                                                              ["one" "two"])]
                                            (.await ready)
                                            (.countDown go)
                                            (doseq [worker workers]
                                              @worker)))]
        (llm-api/sync-or-async-prompt!
         (make-prompt-opts
          {:on-first-response-received (fn [& _] (swap! first-response-count* inc))
           :on-message-received identity
           :on-error identity})))
      (is (= 1 @first-response-count*)))))

(deftest async-retry-on-structured-server-error-test
  (testing "retries an OpenAI Responses server_error before output"
    (let [attempt* (atom 0)
          retry-events* (atom [])
          received-text* (atom "")
          on-error-called* (atom false)]
      (with-redefs [eca.llm-api/prompt! (fn [{:keys [on-message-received on-error]}]
                                          (let [attempt (swap! attempt* inc)]
                                            (if (= 1 attempt)
                                              (on-error {:code "server_error"
                                                         :message "Request failed"
                                                         :error/source :openai-responses
                                                         :response-id "resp_123"
                                                         :request-id "req_123"})
                                              (do
                                                (on-message-received {:type :text :text "hello"})
                                                (on-message-received {:type :finish :finish-reason "stop"})))))
                    eca.llm-api/sleep-with-cancel (fn [_ cancelled?] (not (cancelled?)))]
        (llm-api/sync-or-async-prompt!
         (make-prompt-opts
          {:on-retry (fn [event] (swap! retry-events* conj event))
           :on-error (fn [_] (reset! on-error-called* true))
           :on-message-received (fn [{:keys [type text]}]
                                  (when (= :text type)
                                    (swap! received-text* str text)))})))
      (is (= 2 @attempt*))
      (is (= :overloaded (get-in (first @retry-events*) [:classified :error/type])))
      (is (= "resp_123" (get-in (first @retry-events*) [:error-data :response-id])))
      (is (= "req_123" (get-in (first @retry-events*) [:error-data :request-id])))
      (is (false? @on-error-called*))
      (is (= "hello" @received-text*)))))

(deftest sync-retry-on-structured-server-error-test
  (testing "retries an OpenAI Responses server_error in sync mode"
    (let [attempt* (atom 0)
          retry-events* (atom [])
          on-error-called* (atom false)]
      (with-redefs [eca.llm-api/prompt! (fn [_opts]
                                          (if (= 1 (swap! attempt* inc))
                                            {:error {:code "server_error"
                                                     :message "Request failed"
                                                     :error/source :openai-responses
                                                     :response-id "resp_sync"
                                                     :request-id "req_sync"}}
                                            {:output-text "success"
                                             :usage {:input-tokens 1 :output-tokens 1}}))
                    eca.llm-api/sleep-with-cancel (fn [_ cancelled?] (not (cancelled?)))]
        (llm-api/sync-or-async-prompt!
         (make-prompt-opts
          {:stream false
           :on-retry (fn [event] (swap! retry-events* conj event))
           :on-error (fn [_] (reset! on-error-called* true))
           :on-message-received identity})))
      (is (= 2 @attempt*))
      (is (= :overloaded (get-in (first @retry-events*) [:classified :error/type])))
      (is (= "resp_sync" (get-in (first @retry-events*) [:error-data :response-id])))
      (is (= "req_sync" (get-in (first @retry-events*) [:error-data :request-id])))
      (is (false? @on-error-called*)))))

(deftest async-no-retry-after-visible-output-test
  (doseq [[label emit-output]
          [["text output"
            (fn [{:keys [on-message-received]}]
              (on-message-received {:type :text :text "partial"}))]
           ["reasoning output"
            (fn [{:keys [on-reason]}]
              (on-reason {:status :thinking :id "reason_1" :text "partial"}))]
           ["tool-call output"
            (fn [{:keys [on-prepare-tool-call]}]
              (on-prepare-tool-call {:id "call_1" :full-name "tool" :arguments-text "{"}))]
           ["web-search output"
            (fn [{:keys [on-server-web-search]}]
              (on-server-web-search {:status :started :id "search_1"}))]
           ["image-generation output"
            (fn [{:keys [on-server-image-generation]}]
              (on-server-image-generation {:status :started :id "image_1"}))]]]
    (testing (str "does not retry after " label)
      (let [attempt* (atom 0)
            sleep-calls* (atom 0)
            errors* (atom [])]
        (with-redefs [eca.llm-api/prompt! (fn [{:keys [on-error] :as callbacks}]
                                            (swap! attempt* inc)
                                            (emit-output callbacks)
                                            (on-error {:code "server_error"
                                                       :message "Request failed"
                                                       :error/source :openai-responses}))
                      eca.llm-api/sleep-with-cancel (fn [_ _]
                                                     (swap! sleep-calls* inc)
                                                     true)]
          (llm-api/sync-or-async-prompt!
           (make-prompt-opts
            {:on-error (fn [error] (swap! errors* conj error))
             :on-message-received identity})))
        (is (= 1 @attempt*))
        (is (zero? @sleep-calls*))
        (is (= [{:code "server_error"
                 :message "Request failed"
                 :error/source :openai-responses}]
               @errors*))))))

(deftest async-request-scoped-retry-test
  (testing "retries a replay-safe request after earlier visible output"
    (let [prompt-calls* (atom 0)
          request-retries* (atom 0)
          retry-events* (atom [])
          errors* (atom [])]
      (with-redefs [eca.llm-api/prompt! (fn [{:keys [on-prepare-tool-call on-message-received on-error retry-request]}]
                                          (swap! prompt-calls* inc)
                                          (on-prepare-tool-call {:id "call_1"
                                                                 :full-name "tool"
                                                                 :arguments-text "{}"})
                                          (retry-request
                                           {:error-data {:code "server_is_overloaded"
                                                         :message "Request failed"
                                                         :error/source :openai-responses}
                                            :attempt 0
                                            :replay-safe? true
                                            :on-give-up on-error
                                            :retry-fn (fn [next-attempt]
                                                        (is (= 1 next-attempt))
                                                        (swap! request-retries* inc)
                                                        (on-message-received {:type :text :text "recovered"})
                                                        (on-message-received {:type :finish :finish-reason "stop"}))}))
                    eca.llm-api/sleep-with-cancel (fn [_ cancelled?] (not (cancelled?)))]
        (llm-api/sync-or-async-prompt!
         (make-prompt-opts
          {:on-retry (fn [event] (swap! retry-events* conj event))
           :on-error (fn [error] (swap! errors* conj error))
           :on-prepare-tool-call identity
           :on-message-received identity})))
      (is (= 1 @prompt-calls*) "the outer prompt must not be replayed")
      (is (= 1 @request-retries*))
      (is (= 1 (count @retry-events*)))
      (is (= :overloaded (get-in (first @retry-events*) [:classified :error/type])))
      (is (empty? @errors*))))

  (testing "does not retry a request that is not replay-safe"
    (let [request-retries* (atom 0)
          sleep-calls* (atom 0)
          errors* (atom [])]
      (with-redefs [eca.llm-api/prompt! (fn [{:keys [on-message-received on-error retry-request]}]
                                          (on-message-received {:type :text :text "partial"})
                                          (retry-request
                                           {:error-data {:code "server_error"
                                                         :message "Request failed"
                                                         :error/source :openai-responses}
                                            :attempt 0
                                            :replay-safe? false
                                            :on-give-up on-error
                                            :retry-fn (fn [_]
                                                        (swap! request-retries* inc))}))
                    eca.llm-api/sleep-with-cancel (fn [_ _]
                                                   (swap! sleep-calls* inc)
                                                   true)]
        (llm-api/sync-or-async-prompt!
         (make-prompt-opts
          {:on-error (fn [error] (swap! errors* conj error))
           :on-message-received identity})))
      (is (zero? @request-retries*))
      (is (zero? @sleep-calls*))
      (is (= 1 (count @errors*)))))

  (testing "gives up when the request retry budget is exhausted"
    (let [request-retries* (atom 0)
          errors* (atom [])]
      (with-redefs [eca.llm-api/prompt! (fn [{:keys [on-message-received on-error retry-request]}]
                                          (on-message-received {:type :text :text "earlier output"})
                                          (retry-request
                                           {:error-data {:code "server_error"
                                                         :message "Request failed"
                                                         :error/source :openai-responses}
                                            :attempt 1
                                            :replay-safe? true
                                            :on-give-up on-error
                                            :retry-fn (fn [_]
                                                        (swap! request-retries* inc))}))
                    eca.llm-api/sleep-with-cancel (fn [_ _] true)]
        (llm-api/sync-or-async-prompt!
         (make-prompt-opts
          {:config {:providers {"anthropic" {:key "test-key"
                                               :url "http://test"
                                               :retry {:maxRetries 1}
                                               :models {"claude-sonnet-4-6" {}}}}}
           :on-error (fn [error] (swap! errors* conj error))
           :on-message-received identity})))
      (is (zero? @request-retries*))
      (is (= 1 (count @errors*)))))

  (testing "gives up when cancellation interrupts request retry backoff"
    (let [request-retries* (atom 0)
          errors* (atom [])]
      (with-redefs [eca.llm-api/prompt! (fn [{:keys [on-message-received on-error retry-request]}]
                                          (on-message-received {:type :text :text "earlier output"})
                                          (retry-request
                                           {:error-data {:code "server_error"
                                                         :message "Request failed"
                                                         :error/source :openai-responses}
                                            :attempt 0
                                            :replay-safe? true
                                            :on-give-up on-error
                                            :retry-fn (fn [_]
                                                        (swap! request-retries* inc))}))
                    eca.llm-api/sleep-with-cancel (fn [_ _] false)]
        (llm-api/sync-or-async-prompt!
         (make-prompt-opts
          {:on-error (fn [error] (swap! errors* conj error))
           :on-message-received identity})))
      (is (zero? @request-retries*))
      (is (= 1 (count @errors*))))))

(deftest async-duplicate-errors-delivered-once-test
  (testing "only the first terminal error reaches on-error when stacked requests fail together (#547)"
    (let [errors* (atom [])]
      (with-redefs [eca.llm-api/prompt! (fn [{:keys [on-error on-message-received]}]
                                          ;; visible output disables retry, like a long agentic turn
                                          (on-message-received {:type :text :text "partial"})
                                          ;; dead shared connection: the nested request errors first,
                                          ;; then each unwinding parent frame fires the same failure
                                          (on-error {:message "Connection closed unexpectedly: closed."})
                                          (on-error {:message "Connection closed unexpectedly: closed."}))]
        (llm-api/sync-or-async-prompt!
         (make-prompt-opts
          {:on-error (fn [error] (swap! errors* conj error))
           :on-message-received identity})))
      (is (= 1 (count @errors*))
          "duplicate errors after the first delivery must be dropped"))))

(deftest async-no-retry-on-context-overflow-test
  (testing "does not retry on context overflow"
    (let [attempt* (atom 0)
          on-error-called* (atom false)]
      (with-redefs [eca.llm-api/prompt! (fn [{:keys [on-error]}]
                                          (swap! attempt* inc)
                                          (on-error {:status 400
                                                     :body "prompt is too long: 273112 tokens > 200000 maximum"
                                                     :message "LLM response status: 400"}))
                    eca.llm-api/sleep-with-cancel (fn [_ _] true)]
        (llm-api/sync-or-async-prompt!
         (make-prompt-opts
          {:on-error (fn [_] (reset! on-error-called* true))
           :on-message-received identity})))
      (is (= 1 @attempt*))
      (is (true? @on-error-called*)))))

(deftest sync-retry-on-custom-retry-rule-test
  (testing "retries when custom retryRules status matches"
    (let [attempt* (atom 0)
          retry-events* (atom [])
          on-error-called* (atom false)]
      (with-redefs [eca.llm-api/prompt! (fn [_opts]
                                          (let [attempt (swap! attempt* inc)]
                                            (if (= 1 attempt)
                                              {:error {:status 418
                                                       :body "I'm a teapot"
                                                       :message "LLM response status: 418"}}
                                              {:output-text "success"
                                               :usage {:input-tokens 10 :output-tokens 5}})))
                    eca.llm-api/sleep-with-cancel (fn [_ cancelled?] (not (cancelled?)))]
        (llm-api/sync-or-async-prompt!
         (make-prompt-opts
          {:stream false
           :config {:providers {"anthropic" {:key "test-key"
                                             :url "http://test"
                                             :retryRules [{:status 418 :label "Proxy throttle"}]
                                             :models {"claude-sonnet-4-6" {:extraPayload {:stream false}}}}}}
           :on-retry (fn [event] (swap! retry-events* conj event))
           :on-error (fn [_] (reset! on-error-called* true))
           :on-message-received identity})))
      (is (= 2 @attempt*))
      (is (= 1 (count @retry-events*)))
      (is (= :retryable-custom (get-in (first @retry-events*) [:classified :error/type])))
      (is (= "Proxy throttle" (get-in (first @retry-events*) [:classified :error/label])))
      (is (false? @on-error-called*)))))

(deftest async-retry-on-custom-retry-rule-error-pattern-body-test
  (testing "retries async when custom retryRules errorPattern matches response body"
    (let [attempt* (atom 0)
          retry-events* (atom [])
          received-text* (atom "")
          on-error-called* (atom false)]
      (with-redefs [eca.llm-api/prompt! (fn [{:keys [on-message-received on-error]}]
                                          (let [attempt (swap! attempt* inc)]
                                            (if (= 1 attempt)
                                              (on-error {:status 500
                                                         :body "server capacity exceeded"
                                                         :message "LLM response status: 500"})
                                              (do
                                                (on-message-received {:type :text :text "hello"})
                                                (on-message-received {:type :finish :finish-reason "stop"})))))
                    eca.llm-api/sleep-with-cancel (fn [_ cancelled?] (not (cancelled?)))]
        (llm-api/sync-or-async-prompt!
         (make-prompt-opts
          {:config {:providers {"anthropic" {:key "test-key"
                                             :url "http://test"
                                             :retryRules [{:errorPattern "capacity.*exceeded"
                                                           :label "Capacity exceeded"}]
                                             :models {"claude-sonnet-4-6" {}}}}}
           :on-retry (fn [event] (swap! retry-events* conj event))
           :on-error (fn [_] (reset! on-error-called* true))
           :on-message-received (fn [{:keys [type text]}]
                                  (when (= :text type)
                                    (swap! received-text* str text)))})))
      (is (= 2 @attempt*))
      (is (= 1 (count @retry-events*)))
      (is (= "Capacity exceeded" (get-in (first @retry-events*) [:classified :error/label])))
      (is (false? @on-error-called*))
      (is (= "hello" @received-text*))))

  (testing "does not retry when no custom rule matches"
    (let [attempt* (atom 0)
          on-error-called* (atom false)]
      (with-redefs [eca.llm-api/prompt! (fn [{:keys [on-error]}]
                                          (swap! attempt* inc)
                                          (on-error {:status 418
                                                     :body "I'm a teapot"
                                                     :message "LLM response status: 418"}))
                    eca.llm-api/sleep-with-cancel (fn [_ _] true)]
        (llm-api/sync-or-async-prompt!
         (make-prompt-opts
          {:config {:providers {"anthropic" {:key "test-key"
                                             :url "http://test"
                                             :retryRules [{:status 599 :label "Something else"}]
                                             :models {"claude-sonnet-4-6" {}}}}}
           :on-error (fn [_] (reset! on-error-called* true))
           :on-message-received identity})))
      (is (= 1 @attempt*))
      (is (true? @on-error-called*)))))
